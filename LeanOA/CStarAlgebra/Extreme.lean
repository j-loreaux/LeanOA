import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Basic
import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.Abs
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Order
import Mathlib.Analysis.Convex.Extreme
import LeanOA.Mathlib.Misc
import LeanOA.Mathlib.LinearAlgebra.Complex.Module

open Set Metric
open scoped ComplexStarModule

@[simp]
lemma Set.extremePoints_Icc {a b : ‚Ñù} (hab : a ‚â§ b) :
    Set.extremePoints ‚Ñù (Icc a b) = {a, b} := by
  ext x
  rw [convex_Icc .. |>.mem_extremePoints_iff_convex_diff]
  constructor
  ¬∑ intro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
    have := eq_endpoints_or_mem_Ioo_of_mem_Icc h‚ÇÅ
    suffices x ‚àâ Ioo a b by grind
    intro hx
    have := h‚ÇÇ.isPreconnected.Icc_subset (a := a) (b := b) (by grind) (by grind)
    grind
  ¬∑ simp only [mem_insert_iff, mem_singleton_iff, mem_Icc]
    rintro (rfl | rfl)
    ¬∑ simpa using ‚ü®hab, convex_Ioc ..‚ü©
    ¬∑ simpa using ‚ü®hab, convex_Ico ..‚ü©

@[nontriviality]
lemma Set.extremePoints_eq_self {ùïú E : Type*} [Semiring ùïú] [PartialOrder ùïú]
    [AddCommMonoid E] [SMul ùïú E] [Subsingleton E] (A : Set E) :
    Set.extremePoints ùïú A = A :=
  subset_antisymm extremePoints_subset fun _ h ‚Ü¶ ‚ü®h, fun _ _ _ _ _ ‚Ü¶ Subsingleton.elim ..‚ü©

open Complex
lemma cfc_re_id {A : Type*} [CStarAlgebra A] {a : A} [IsStarNormal a] :
    cfc (re ¬∑ : ‚ÑÇ ‚Üí ‚ÑÇ) a = ‚Ñú a := by
  conv_rhs => rw [realPart_apply_coe, ‚Üê cfc_id' ‚ÑÇ a, ‚Üê cfc_star, ‚Üê cfc_add .., ‚Üê cfc_smul ..]
  refine cfc_congr fun x hx ‚Ü¶ ?_
  rw [Complex.re_eq_add_conj, ‚Üê smul_one_smul ‚ÑÇ 2‚Åª¬π]
  simp [div_eq_inv_mul]

open Complex
lemma cfc_im_id {A : Type*} [CStarAlgebra A] {a : A} [IsStarNormal a] :
    cfc (im ¬∑ : ‚ÑÇ ‚Üí ‚ÑÇ) a = ‚Ñë a := by
  suffices cfc (fun z : ‚ÑÇ ‚Ü¶ re z + I * im z) a = ‚Ñú a + I ‚Ä¢ ‚Ñë a by
    rw [cfc_add .., cfc_const_mul .., cfc_re_id] at this
    simpa
  simp [mul_comm I, re_add_im, cfc_id' .., realPart_add_I_smul_imaginaryPart]

lemma CStarAlgebra.one_mem_extremePoints_closedUnitBall {A : Type*} [CStarAlgebra A] :
    1 ‚àà extremePoints ‚Ñù (closedBall (0 : A) 1) := by
  nontriviality A
  /- Suppose that `1` is a convex combination of `x` and `y`. Then, since `1` is self
  adjoint, it is also a combination of their real and imaginary parts, which we
  call `a` and `b`. Moreover, `b` is a linear polynomial in the variable `a`, so we
  may write it as the continuous functional calculus applied to the appropriate
  function of `a`. -/
  refine ‚ü®by simp, fun x hx y hy hxy ‚Ü¶ ?_‚ü©
  let +nondep (eq := ha') a : A := ‚Ñú x
  let +nondep (eq := hb') b : A := ‚Ñú y
  simp only [mem_closedBall, dist_zero_right] at hx hy
  have ha : ‚Äña‚Äñ ‚â§ 1 := by simpa [ha'] using realPart.norm_le _ |>.trans hx
  have hb : ‚Äñb‚Äñ ‚â§ 1 := by simpa [hb'] using realPart.norm_le _ |>.trans hy
  obtain ‚ü®c‚ÇÅ, hc‚ÇÅ, c‚ÇÇ, hc‚ÇÇ, hc, hcab‚ü© := by simpa [openSegment] using hxy
  replace hcab : c‚ÇÅ ‚Ä¢ a + c‚ÇÇ ‚Ä¢ b = 1 := by simpa [ha', hb'] using congr((‚Ñú $hcab : A))
  have : b = c‚ÇÇ‚Åª¬π ‚Ä¢ 1 - c‚ÇÇ‚Åª¬π ‚Ä¢ c‚ÇÅ ‚Ä¢ a := by
    simpa [inv_smul_smul‚ÇÄ hc‚ÇÇ.ne', eq_sub_iff_add_eq'] using congr(c‚ÇÇ‚Åª¬π ‚Ä¢ $hcab)
  rw [this, ‚Üê cfc_id' ‚Ñù a, ‚Üê cfc_one ‚Ñù a, ‚Üê cfc_smul .., ‚Üê cfc_smul .., ‚Üê cfc_smul ..,
    ‚Üê cfc_sub .., ‚Üê cfc_smul .., ‚Üê cfc_add .., cfc_eq_cfc_iff_eqOn] at hcab
  /- By passing to functions, we will show that `a = 1`. In particular, the constant
  function `1` on the `‚Ñù`-spectrum of `a` is a convex combination of functions (one of
  which is the identity) which are bounded in absolute value by `1`. Since `1 : ‚Ñù` is
  extreme in `Icc (-1) 1`, we conclude that these functions must be `1` on the
  spectrum of `a`. -/
  obtain rfl : a = 1 := by
    refine CFC.eq_one_of_spectrum_subset_one (R := ‚Ñù) a fun r hr ‚Ü¶ ?_
    have h1_mem : (1 : ‚Ñù) ‚àà openSegment ‚Ñù r (c‚ÇÇ‚Åª¬π - c‚ÇÇ‚Åª¬π * c‚ÇÅ * r) :=
      ‚ü®c‚ÇÅ, c‚ÇÇ, hc‚ÇÅ, hc‚ÇÇ, hc, by simpa [mul_assoc] using hcab hr‚ü©
    have key : (1 : ‚Ñù) ‚àà extremePoints ‚Ñù (Icc (-1) 1) := by simp
    simp only [mem_singleton_iff]
    refine mem_extremePoints_iff_left.mp key |>.2 _ ?_ _ ?_ h1_mem
    ¬∑ simpa [abs_le] using (spectrum.norm_le_norm_of_mem hr).trans ha
    ¬∑ suffices c‚ÇÇ‚Åª¬π - c‚ÇÇ‚Åª¬π * c‚ÇÅ * r ‚àà spectrum ‚Ñù b by
        simpa [abs_le] using (spectrum.norm_le_norm_of_mem this).trans hb
      rw [this, ‚Üê Algebra.algebraMap_eq_smul_one, sub_eq_add_neg, sub_eq_add_neg]
      rwa [add_comm c‚ÇÇ‚Åª¬π, spectrum.add_mem_add_iff, ‚Üê spectrum.neg_eq, Set.neg_mem_neg, smul_smul,
        spectrum.smul_eq_smul _ _ (nonempty_of_mem hr), ‚Üê smul_eq_mul _ r,
        Set.smul_mem_smul_set_iff‚ÇÄ (by positivity)]
  /- Since `‚Ñú x = a = 1`, so too we conclude `‚Ñú y = b = 1`. -/
  obtain rfl : b = 1 := by
    simpa [‚Üê smul_assoc, ‚Üê sub_smul, (sub_eq_iff_eq_add.mpr hc.symm).symm, mul_sub, hc‚ÇÇ.ne']
  clear this hb ha hcab hb' hc hc‚ÇÇ hc‚ÇÅ c‚ÇÅ c‚ÇÇ hy hxy y
  /- Since `‚Ñú x = 1`, the real and imaginary parts of `x` commute, so `x` is normal. It
  then suffices to show that `‚Ñë x = 0`. -/
  have hx' : IsStarNormal x := by simp [isStarNormal_iff_commute_realPart_imaginaryPart, ‚Üê ha']
  suffices (‚Ñë x : A) = 0 by rw [‚Üê realPart_add_I_smul_imaginaryPart x, ‚Üê ha', this]; simp
  letI := spectralOrder A
  letI := spectralOrderedRing A
  /- Note that `‚Äñ1 + (‚Ñë x) ^ 2‚Äñ = ‚Äñ(‚Ñú x) ^ 2 + (‚Ñë x) ^ 2‚Äñ = ‚Äñstar x * x‚Äñ = ‚Äñx‚Äñ ^ 2 ‚â§ 1`.
  Therefore, `1 + (‚Ñë x) ^ 2 ‚â§ 1`, so `(‚Ñë x) ^ 2 ‚â§ 0`. Since `(‚Ñë x) ^ 2` is clearly nonnegative,
  we conclude that it is zero, and hence so also `‚Ñë x = 0`, as desired. -/
  rw [‚Üê sq_le_one_iff‚ÇÄ (by positivity), sq, ‚Üê CStarRing.norm_star_mul_self,
    star_mul_self_eq_realPart_sq_add_imaginaryPart_sq, ‚Üê ha', mul_one, ‚Üê sq,
    CStarAlgebra.norm_le_one_iff_of_nonneg _ (add_nonneg zero_le_one (‚Ñë x).2.sq_nonneg)] at hx
  rw [‚Üê norm_eq_zero, ‚Üê sq_eq_zero_iff, ‚Üê IsSelfAdjoint.norm_mul_self (‚Ñë x).2, ‚Üê sq, norm_eq_zero]
  exact le_antisymm (by simpa using hx) (‚Ñë x).2.sq_nonneg

section nonUnital
variable {A : Type*} [NonUnitalCStarAlgebra A]

-- what is the right generality for this? everything I try keeps timing out
lemma quasispectrum.norm_le_norm_of_mem {a : A} {x} (hx : x ‚àà quasispectrum ‚Ñù a) : ‚Äñx‚Äñ ‚â§ ‚Äña‚Äñ :=
  (spectrum.norm_le_norm_of_mem ((Unitization.quasispectrum_eq_spectrum_inr ‚Ñù a).symm ‚ñ∏ hx)).trans
    (by simp [Unitization.norm_def])

theorem star_self_conjugate_eq_self_of_mem_extremePoints_closedUnitBall {a : A}
    (ha : a ‚àà extremePoints ‚Ñù (closedBall 0 1)) : a * star a * a = a := by
  /- Suppose `a` is an extreme point of the closed unit ball. Then we want to show that
  `a * star a * a = a`. It suffices to show `a * |a| = a`. -/
  letI := CStarAlgebra.spectralOrder A
  letI := CStarAlgebra.spectralOrderedRing A
  suffices a * CFC.abs a = a by rw [mul_assoc, ‚Üê CFC.abs_mul_abs, ‚Üê mul_assoc, this, this]
  obtain ‚ü®ha, h‚ü© := ha
  simp only [mem_closedBall, dist_zero_right] at ha h
  /- Using the extremity of `a`, it suffices to show that `2 ‚Ä¢ |a| - |a| * |a|` is in the
  closed unit ball since `2‚Åª¬π ‚Ä¢ (2 ‚Ä¢ |a| - |a| * |a|) + 2‚Åª¬π ‚Ä¢ (a * |a|) = a`
  (and clearly `a * |a|` is in the closed unit ball since `a` is). -/
  refine @h _ ?_ ((2 : ‚Ñù) ‚Ä¢ a - a * CFC.abs a) ?_ ‚ü®2‚Åª¬π, 2‚Åª¬π, by simp [smul_sub, ‚Üê two_mul]‚ü©
  ¬∑ grw [norm_mul_le, CFC.norm_abs, ha, one_mul]
  ¬∑ /- To show this inequality (i.e., `‚Äñ2 ‚Ä¢ a - a * |a|‚Äñ ‚â§ 1`), we first
    show equality with `‚Äñ2 ‚Ä¢ |a| - |a| * |a|‚Äñ` and then pass to the
    continuous functional calculus, where we then use `norm_cfc‚Çô_le` to show the rest
    (using the fact that the elements in the quasispectrum of `|a|`
    are bounded between `0` and `1`). -/
    calc
    _ = ‚Äñ(2 : ‚Ñù) ‚Ä¢ CFC.abs a - CFC.abs a * CFC.abs a‚Äñ := by
      simp_rw [‚Üê sq_eq_sq‚ÇÄ (norm_nonneg _) (norm_nonneg _), sq, ‚Üê CStarRing.norm_star_mul_self]
      simp only [star_sub, star_smul, star_mul, mul_sub, mul_smul_comm, sub_mul, smul_mul_assoc]
      simp [CFC.abs_nonneg a |>.star_eq, mul_assoc, ‚Üê mul_assoc _ a, ‚Üê CFC.abs_mul_abs]
    _ = ‚Äñcfc‚Çô (fun x : ‚Ñù ‚Ü¶ x * (2 - x)) (CFC.abs a)‚Äñ := by
      congr
      ring_nf
      simp_rw [mul_comm _ (2 : ‚Ñù), sq]
      rw [cfc‚Çô_sub _ _, cfc‚Çô_const_mul _ _, cfc‚Çô_mul _ _, cfc‚Çô_id' ‚Ñù (CFC.abs a)]
    _ ‚â§ _ := norm_cfc‚Çô_le fun x hx ‚Ü¶ by
      have : 0 ‚â§ x ‚àß x ‚â§ 1 := by
        refine ‚ü®quasispectrum_nonneg_of_nonneg _ (by simp) _ hx, le_trans (Real.le_norm_self _) ?_‚ü©
        grw [quasispectrum.norm_le_norm_of_mem hx, CFC.norm_abs, ha]
      rw [Real.norm_of_nonneg] <;> nlinarith

theorem isIdempotentElem_star_mul_self_of_mem_extremePoints_closedUnitBall
    {a : A} (ha : a ‚àà extremePoints ‚Ñù (closedBall 0 1)) : IsIdempotentElem (star a * a) := by
  grind [star_self_conjugate_eq_self_of_mem_extremePoints_closedUnitBall ha, IsIdempotentElem]

theorem isIdempotentElem_self_mul_star_of_mem_extremePoints_closedUnitBall
    {a : A} (ha : a ‚àà extremePoints ‚Ñù (closedBall 0 1)) : IsIdempotentElem (a * star a) := by
  grind [star_self_conjugate_eq_self_of_mem_extremePoints_closedUnitBall ha, IsIdempotentElem]

end nonUnital

import LeanOA.ForMathlib.Algebra.Star.StarProjection
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Basic
import Mathlib.Topology.ContinuousMap.LocallyConstant
import Mathlib.Topology.ExtremallyDisconnected

namespace ContinuousMap
variable {X : Type*} [TopologicalSpace X]

-- move to `Mathlib.Topology.MetricSpace.Pseudo.Defs`?
/-- `Pi.single` as a continuous map `C(X, ‚Ñù)`. -/
noncomputable abbrev single [DiscreteTopology X] (i : X) : C(X, ‚Ñù) :=
  open Classical in .mk (Pi.single i 1)

@[simp] theorem isStarProjection_single [DiscreteTopology X] (i : X) :
    IsStarProjection (single i) := by constructor <;> ext <;> simp [Pi.single_apply]

@[simp] lemma mem_span_isStarProjection_of_finite [T1Space X] [Finite X]
    (f : C(X, ‚Ñù)) : f ‚àà Submodule.span ‚Ñù {p : C(X, ‚Ñù) | IsStarProjection p} := by
  have := Fintype.ofFinite X
  rw [show f = ‚àë i, f i ‚Ä¢ single i by ext; simp [Finset.sum_pi_single, ‚Üê Pi.single_smul]]
  exact Submodule.sum_mem _ fun i _ ‚Ü¶ Submodule.smul_mem _ _ <| by simp [Submodule.mem_span_of_mem]

end ContinuousMap

/-- A C‚ãÜ-algebra is **FS** if the set of self-adjoint elements has a dense subset of
elements with finite spectrum. -/
class CStarAlgebra.FiniteSpectrum (ùïú A : Type*) [RCLike ùïú] [TopologicalSpace A] [Ring A]
    [Algebra ùïú A] [StarRing A] : Prop where
  fs : {x : A | IsSelfAdjoint x} ‚äÜ closure {x : A | IsSelfAdjoint x ‚àß (spectrum ùïú x).Finite}

instance {ùïú A : Type*} [RCLike ùïú] [TopologicalSpace A] [Ring A] [Algebra ùïú A] [StarRing A]
    [Subsingleton A] : CStarAlgebra.FiniteSpectrum ùïú A where
  fs := by nontriviality A; exfalso; exact false_of_nontrivial_of_subsingleton A

variable {A : Type*} [TopologicalSpace A] [Ring A] [StarRing A] [Algebra ‚Ñù A]
  [ContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint]

/-- A self-adjoint element with finite spectrum in a C‚ãÜ-algebra is in the span of
star projections. -/
lemma IsSelfAdjoint.mem_span_isStarProjection_of_finite_spectrum {x : A}
    (hx : IsSelfAdjoint x) (h : (spectrum ‚Ñù x).Finite) :
    x ‚àà Submodule.span ‚Ñù {p : A | IsStarProjection p} := by
  have : Finite (spectrum ‚Ñù x) := Set.finite_coe_iff.mpr h
  convert (ContinuousMap.id ‚Ñù).restrict (spectrum ‚Ñù x) |>.mem_span_isStarProjection_of_finite
  refine ‚ü®fun _ ‚Ü¶ by simp_all, fun h' ‚Ü¶ Submodule.mem_span.mpr fun p hp ‚Ü¶ ?_‚ü©
  have := Submodule.mem_span.mp h' (.comap (cfcHom hx).toLinearMap p)
  simp_all [Set.subset_def, IsStarProjection.map, cfcHom_id]

/-- In a FS C‚ãÜ-algebra, the topological closure of the span of star
projections is exactly the submodule of the self-adjoint elements. -/
@[simp] theorem CStarAlgebra.FiniteSpectrum.topologicalClosure_span_isStarProjection
    [ContinuousConstSMul ‚Ñù A] [ContinuousAdd A] [StarModule ‚Ñù A] [T2Space A]
    [ContinuousStar A] [h : CStarAlgebra.FiniteSpectrum ‚Ñù A] :
    (Submodule.span ‚Ñù {x : A | IsStarProjection x}).topologicalClosure =
      selfAdjoint.submodule ‚Ñù A := by
  refine le_antisymm (fun x hx ‚Ü¶ closure_minimal (fun x hx ‚Ü¶ ?_) ?_ hx) fun x hx ‚Ü¶ ?_
  ¬∑ refine Submodule.span_induction (fun _ hx ‚Ü¶ hx.isSelfAdjoint) ?_ ?_ ?_ hx <;> aesop
  ¬∑ exact isClosed_eq continuous_id'.star continuous_id'
  ¬∑ exact closure_mono (fun y hy ‚Ü¶ hy.1.mem_span_isStarProjection_of_finite_spectrum hy.2) (h.fs hx)

theorem LocallyConstant.separatesPoints_subalgbraMap_toContinuousMapAlgHom_top (R : Type*)
    {X Y : Type*} [CommSemiring R] [TopologicalSpace X] [TopologicalSpace Y] [Nontrivial Y]
    [Semiring Y] [Algebra R Y] [IsTopologicalSemiring Y] [TotallySeparatedSpace X] :
    (Subalgebra.map (toContinuousMapAlgHom R : _ ‚Üí‚Çê[R] C(X, Y)) ‚ä§).SeparatesPoints := by
  intro x y hxy
  obtain ‚ü®U, hU, hxU, hyU : y ‚àâ U‚ü© := exists_isClopen_of_totally_separated hxy
  exact ‚ü®charFn Y hU, by simp_all [charFn]‚ü©

open ContinuousMap LocallyConstant in
instance {X : Type*} [TopologicalSpace X] [CompactSpace X] [TotallySeparatedSpace X] :
    CStarAlgebra.FiniteSpectrum ‚Ñù C(X, ‚Ñù) where
  fs x hx := by
    have : .range toContinuousMap ‚äÜ {x : C(X, ‚Ñù) | IsSelfAdjoint x ‚àß (spectrum ‚Ñù x).Finite} :=
      fun _ ‚ü®f, hf‚ü© ‚Ü¶ by  simp [‚Üê hf, spectrum_eq_range, range_finite, IsSelfAdjoint]
    apply closure_mono this
    simpa using Subalgebra.ext_iff.mp (subalgebra_topologicalClosure_eq_top_of_separatesPoints _
      (separatesPoints_subalgbraMap_toContinuousMapAlgHom_top ‚Ñù)) x

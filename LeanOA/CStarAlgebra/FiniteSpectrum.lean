import LeanOA.ForMathlib.Algebra.Star.StarProjection
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Basic
import Mathlib.Topology.ContinuousMap.LocallyConstant
import Mathlib.Topology.ExtremallyDisconnected

variable {ùïú A Y : Type*} [RCLike ùïú] [TopologicalSpace A] [TopologicalSpace Y]

namespace ContinuousMapZero

/-- A version of `Pi.single` as an element in `C(A, Y)‚ÇÄ` where `single i x 0 = 0`. -/
noncomputable abbrev single [DiscreteTopology A] [DecidableEq A] [Zero Y] [Zero A] (i : A)
    (x : Y) : C(A, Y)‚ÇÄ where
  toFun j := if j = 0 then 0 else (Pi.single i x : A ‚Üí Y) j
  map_zero' := by simp

lemma single_def [DiscreteTopology A] [DecidableEq A] [Zero Y] [Zero A]
    (i : A) (x : Y) (j : A) :
    single i x j = if j = 0 then 0 else (Pi.single i x : A ‚Üí Y) j := rfl

@[simp] lemma sigle_apply_of_ne_zero [DiscreteTopology A] [DecidableEq A] [Zero Y] [Zero A]
    (i : A) (x : Y) {j : A} (hj : j ‚â† 0) : single i x j = (Pi.single i x : A ‚Üí Y) j := by simp_all

@[simp] lemma mem_span_isStarProjection_of_finite [DiscreteTopology A] [Finite A] [Zero A]
    (f : C(A, ùïú)‚ÇÄ) : f ‚àà Submodule.span ùïú {p : ContinuousMapZero A ùïú | IsStarProjection p} := by
  have := Fintype.ofFinite A
  classical
  rw [show f = ‚àë i, f i ‚Ä¢ single i 1 by aesop (add simp [Pi.single_apply])]
  exact Submodule.sum_mem _ fun i _ ‚Ü¶ Submodule.smul_mem _ _ <| Submodule.mem_span_of_mem
    (by constructor <;> ext <;> simp_all [Pi.single_apply, apply_ite])

end ContinuousMapZero

variable (A) in
/-- A C‚ãÜ-algebra is **FS (Finite Spectrum)** if the set of self-adjoint elements has a dense subset
of elements with finite spectrum. -/
@[mk_iff]
class CStarAlgebra.FiniteSpectrum [NonUnitalRing A] [Module ‚Ñù A] [Star A] : Prop where
  fs : {x : A | IsSelfAdjoint x} ‚äÜ closure {x : A | IsSelfAdjoint x ‚àß (quasispectrum ‚Ñù x).Finite}

theorem CStarAlgebra.finiteSpectrum_iff_spectrum [Ring A] [Algebra ‚Ñù A] [Star A] :
    CStarAlgebra.FiniteSpectrum A ‚Üî
      {x : A | IsSelfAdjoint x} ‚äÜ closure {x | IsSelfAdjoint x ‚àß (spectrum ‚Ñù x).Finite} := by
  simp [quasispectrum_eq_spectrum_union_zero, CStarAlgebra.finiteSpectrum_iff]

instance [NonUnitalRing A] [Module ‚Ñù A] [StarRing A] [IsScalarTower ‚Ñù A A] [SMulCommClass ‚Ñù A A]
    [NonUnitalContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint] [Subsingleton A] :
    CStarAlgebra.FiniteSpectrum A where
  fs := by simp [Subsingleton.eq_zero, CFC.quasispectrum_zero_eq]

instance [Ring A] [Algebra ‚Ñù A] [Star A] [Subsingleton A] :
    CStarAlgebra.FiniteSpectrum A where fs := by simp [quasispectrum_eq_spectrum_union_zero]

section totallySeparatedSpace
variable [TotallySeparatedSpace A]

theorem LocallyConstant.separatesPoints_subalgbraMap_toContinuousMapAlgHom_top (R : Type*)
    [CommSemiring R] [Nontrivial Y] [Semiring Y] [Algebra R Y] [IsTopologicalSemiring Y] :
    (Subalgebra.map (toContinuousMapAlgHom R : _ ‚Üí‚Çê[R] C(A, Y)) ‚ä§).SeparatesPoints := by
  intro x y hxy
  obtain ‚ü®U, hU, hxU, hyU : y ‚àâ U‚ü© := exists_isClopen_of_totally_separated hxy
  exact ‚ü®charFn Y hU, by simp_all [charFn]‚ü©

open ContinuousMap LocallyConstant in
instance [CompactSpace A] : CStarAlgebra.FiniteSpectrum C(A, ‚Ñù) :=
  CStarAlgebra.finiteSpectrum_iff_spectrum.mpr fun x hx ‚Ü¶ by
    have : .range toContinuousMap ‚äÜ {x : C(A, ‚Ñù) | IsSelfAdjoint x ‚àß (spectrum ‚Ñù x).Finite} :=
      fun _ ‚ü®f, hf‚ü© ‚Ü¶ by simp [‚Üê hf, spectrum_eq_range, range_finite, IsSelfAdjoint]
    apply closure_mono this
    simpa using Subalgebra.ext_iff.mp (subalgebra_topologicalClosure_eq_top_of_separatesPoints _
      (separatesPoints_subalgbraMap_toContinuousMapAlgHom_top ‚Ñù)) x

set_option linter.unusedSectionVars false in
proof_wanted CStarAlgebra.finiteSpectrumContinuousMapComplex [CompactSpace A] :
    CStarAlgebra.FiniteSpectrum C(A, ‚ÑÇ)

end totallySeparatedSpace

variable [NonUnitalRing A] [StarRing A] [Module ‚Ñù A] [IsScalarTower ‚Ñù A A] [SMulCommClass ‚Ñù A A]
  [NonUnitalContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint]

/-- A self-adjoint element with finite quasispectrum in a non-unital C‚ãÜ-algebra is in the span of
star projections. -/
lemma IsSelfAdjoint.mem_span_isStarProjection_of_finite_quasispectrum {x : A}
    (hx : IsSelfAdjoint x) (h : (quasispectrum ‚Ñù x).Finite) :
    x ‚àà Submodule.span ‚Ñù {p : A | IsStarProjection p} := by
  have : Finite (quasispectrum ‚Ñù x) := Set.finite_coe_iff.mpr h
  refine Submodule.mem_span.mpr fun p hp ‚Ü¶ ?_
  simpa [cfc‚ÇôHom_id] using Submodule.mem_span.mp
    (ContinuousMapZero.id (quasispectrum ‚Ñù x)).mem_span_isStarProjection_of_finite
    (.comap (cfc‚ÇôHom (R := ‚Ñù) hx : _ ‚Üí‚Çó[‚Ñù] A) p)
    (by simp_all [Set.subset_def, IsStarProjection.map])

/-- In a FS C‚ãÜ-algebra, the topological closure of the span of star
projections is exactly the submodule of the self-adjoint elements. -/
@[simp] theorem CStarAlgebra.FiniteSpectrum.topologicalClosure_span_isStarProjection
    [ContinuousConstSMul ‚Ñù A] [ContinuousAdd A] [StarModule ‚Ñù A] [T2Space A]
    [ContinuousStar A] [h : CStarAlgebra.FiniteSpectrum A] :
    (Submodule.span ‚Ñù {x : A | IsStarProjection x}).topologicalClosure =
      selfAdjoint.submodule ‚Ñù A := by
  refine le_antisymm (fun x hx ‚Ü¶ closure_minimal (fun x hx ‚Ü¶ ?_) ?_ hx) fun x hx ‚Ü¶ ?_
  ¬∑ refine Submodule.span_induction (fun _ hx ‚Ü¶ hx.isSelfAdjoint) ?_ ?_ ?_ hx <;> aesop
  ¬∑ exact isClosed_eq continuous_id'.star continuous_id'
  ¬∑ exact closure_mono (fun y hy ‚Ü¶ hy.1.mem_span_isStarProjection_of_finite_quasispectrum hy.2)
      (h.fs hx)

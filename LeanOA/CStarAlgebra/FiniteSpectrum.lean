import LeanOA.ForMathlib.Algebra.Star.StarProjection
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Basic
import Mathlib.Topology.ContinuousMap.LocallyConstant
import Mathlib.Topology.ExtremallyDisconnected

variable {ğ•œ A Y : Type*} [RCLike ğ•œ] [TopologicalSpace A] [TopologicalSpace Y]

namespace ContinuousMapZero

/-- A version of `Pi.single` as an element in `C(A, Y)â‚€` where `single i x 0 = 0`. -/
noncomputable abbrev single [DiscreteTopology A] [DecidableEq A] [Zero Y] [Zero A] (i : A)
    (x : Y) : C(A, Y)â‚€ where
  toFun j := if j = 0 then 0 else (Pi.single i x : A â†’ Y) j
  map_zero' := by simp

lemma single_def [DiscreteTopology A] [DecidableEq A] [Zero Y] [Zero A]
    (i : A) (x : Y) (j : A) :
    single i x j = if j = 0 then 0 else (Pi.single i x : A â†’ Y) j := rfl

@[simp] lemma sigle_apply_of_ne_zero [DiscreteTopology A] [DecidableEq A] [Zero Y] [Zero A]
    (i : A) (x : Y) {j : A} (hj : j â‰  0) : single i x j = (Pi.single i x : A â†’ Y) j := by simp_all

@[simp] lemma mem_span_isStarProjection_of_finite [DiscreteTopology A] [Finite A] [Zero A]
    (f : C(A, ğ•œ)â‚€) : f âˆˆ Submodule.span ğ•œ {p : C(A, ğ•œ)â‚€ | IsStarProjection p} := by
  have := Fintype.ofFinite A
  classical
  rw [show f = âˆ‘ i, f i â€¢ single i 1 by aesop (add simp [Pi.single_apply])]
  exact Submodule.sum_mem _ fun i _ â†¦ Submodule.smul_mem _ _ <| Submodule.mem_span_of_mem
    (by constructor <;> ext <;> simp_all [Pi.single_apply, apply_ite])

end ContinuousMapZero

namespace ContinuousMap

variable (ğ•œ) in
/-- Lifting `C(A, â„)` to `C(A, â„‚)` using `Complex.ofReal`. -/
@[simps] def realToRCLike (f : C(A, â„)) : C(A, ğ•œ) where toFun x := RCLike.ofReal (f x)

@[simp] lemma isSelfAdjoint_realToRCLike {f : C(A, â„)} : IsSelfAdjoint (f.realToRCLike ğ•œ) := by
  ext; simp

@[simp] lemma spectrum_realToRCLike (f : C(A, â„)) :
    spectrum â„ (f.realToRCLike ğ•œ) = spectrum â„ f := by
  ext; simp [spectrum.mem_iff, isUnit_iff_forall_isUnit, RCLike.ext_iff (K := ğ•œ), Algebra.smul_def]

/-- Mapping `C(A, â„‚)` to `C(A, â„)` using `Complex.re`. -/
@[simps] def rclikeToReal (f : C(A, ğ•œ)) : C(A, â„) where toFun x := RCLike.re (f x)

@[simp] theorem rclikeToReal_realToComplex (f : C(A, â„)) :
    (f.realToRCLike ğ•œ).rclikeToReal = f := by ext; simp

theorem IsSelfAdjoint.realToRCLike_rclikeToReal {f : C(A, ğ•œ)} (hf : IsSelfAdjoint f) :
    f.rclikeToReal.realToRCLike ğ•œ = f := by
  ext
  simp only [realToRCLike_apply, rclikeToReal_apply, â† RCLike.conj_eq_iff_re]
  conv_rhs => rw [â† hf.star_eq]
  simp

variable (ğ•œ) in
open ContinuousMap in
theorem range_realToRCLike_eq_isSelfAdjoint :
    .range (realToRCLike ğ•œ) = {f : C(A, ğ•œ) | IsSelfAdjoint f} :=
  le_antisymm (fun _ âŸ¨_, hâŸ© â†¦ by simp [â† h]) fun f hf â†¦
    âŸ¨f.rclikeToReal, hf.realToRCLike_rclikeToRealâŸ©

variable (ğ•œ) in
@[simp] theorem isometry_realToRCLike [CompactSpace A] : Isometry (realToRCLike ğ•œ (A := A)) := by
  refine .of_dist_eq fun f g â†¦ ?_
  simp only [dist_eq_norm, norm_eq_iSup_norm, sub_apply, realToRCLike_apply, Real.norm_eq_abs]
  congr; ext
  rw [â† RCLike.ofReal_sub]
  simp [-map_sub]

end ContinuousMap

variable (A) in
/-- A Câ‹†-algebra is **FS (Finite Spectrum)** if the set of self-adjoint elements has a dense subset
of elements with finite spectrum. -/
@[mk_iff]
class CStarAlgebra.FiniteSpectrum [NonUnitalRing A] [Module â„ A] [Star A] : Prop where
  fs : {x : A | IsSelfAdjoint x} âŠ† closure {x : A | IsSelfAdjoint x âˆ§ (quasispectrum â„ x).Finite}

theorem CStarAlgebra.finiteSpectrum_iff_spectrum [Ring A] [Algebra â„ A] [Star A] :
    CStarAlgebra.FiniteSpectrum A â†”
      {x : A | IsSelfAdjoint x} âŠ† closure {x | IsSelfAdjoint x âˆ§ (spectrum â„ x).Finite} := by
  simp [quasispectrum_eq_spectrum_union_zero, CStarAlgebra.finiteSpectrum_iff]

instance [NonUnitalRing A] [Module â„ A] [StarRing A] [IsScalarTower â„ A A] [SMulCommClass â„ A A]
    [NonUnitalContinuousFunctionalCalculus â„ A IsSelfAdjoint] [Subsingleton A] :
    CStarAlgebra.FiniteSpectrum A where
  fs := by simp [Subsingleton.eq_zero, CFC.quasispectrum_zero_eq]

instance [Ring A] [Algebra â„ A] [Star A] [Subsingleton A] :
    CStarAlgebra.FiniteSpectrum A where fs := by simp [quasispectrum_eq_spectrum_union_zero]

section totallySeparatedSpace
variable [TotallySeparatedSpace A]

theorem LocallyConstant.separatesPoints_subalgbraMap_toContinuousMapAlgHom_top (R : Type*)
    [CommSemiring R] [Nontrivial Y] [Semiring Y] [Algebra R Y] [IsTopologicalSemiring Y] :
    (Subalgebra.map (toContinuousMapAlgHom R : _ â†’â‚[R] C(A, Y)) âŠ¤).SeparatesPoints := by
  intro x y hxy
  obtain âŸ¨U, hU, hxU, hyU : y âˆ‰ UâŸ© := exists_isClopen_of_totally_separated hxy
  exact âŸ¨charFn Y hU, by simp_all [charFn]âŸ©

open ContinuousMap LocallyConstant in
instance [CompactSpace A] : CStarAlgebra.FiniteSpectrum C(A, â„) :=
  CStarAlgebra.finiteSpectrum_iff_spectrum.mpr fun x hx â†¦ by
    have : .range toContinuousMap âŠ† {x : C(A, â„) | IsSelfAdjoint x âˆ§ (spectrum â„ x).Finite} :=
      fun _ âŸ¨f, hfâŸ© â†¦ by simp [â† hf, spectrum_eq_range, range_finite, IsSelfAdjoint]
    apply closure_mono this
    simpa using Subalgebra.ext_iff.mp (subalgebra_topologicalClosure_eq_top_of_separatesPoints _
      (separatesPoints_subalgbraMap_toContinuousMapAlgHom_top â„)) x

open ContinuousMap in
instance [CompactSpace A] : CStarAlgebra.FiniteSpectrum C(A, ğ•œ) :=
  CStarAlgebra.finiteSpectrum_iff_spectrum.mpr fun x hx â†¦
    have âŸ¨y, hyâŸ© := range_realToRCLike_eq_isSelfAdjoint ğ•œ (A := A) â–¸ hx
    have : realToRCLike ğ•œ '' _ âŠ† {x | IsSelfAdjoint x âˆ§ (spectrum â„ x).Finite} := by aesop
    closure_mono this <| hy â–¸ mem_closure_image (isometry_realToRCLike ğ•œ).continuous.continuousAt
      (CStarAlgebra.finiteSpectrum_iff_spectrum.mp inferInstance (.all y))

end totallySeparatedSpace

variable [NonUnitalRing A] [StarRing A] [Module â„ A] [IsScalarTower â„ A A] [SMulCommClass â„ A A]
  [NonUnitalContinuousFunctionalCalculus â„ A IsSelfAdjoint]

/-- A self-adjoint element with finite quasispectrum in a non-unital Câ‹†-algebra is in the span of
star projections. -/
lemma IsSelfAdjoint.mem_span_isStarProjection_of_finite_quasispectrum {x : A}
    (hx : IsSelfAdjoint x) (h : (quasispectrum â„ x).Finite) :
    x âˆˆ Submodule.span â„ {p : A | IsStarProjection p} := by
  have : Finite (quasispectrum â„ x) := Set.finite_coe_iff.mpr h
  refine Submodule.mem_span.mpr fun p hp â†¦ ?_
  simpa [cfcâ‚™Hom_id] using Submodule.mem_span.mp
    (ContinuousMapZero.id (quasispectrum â„ x)).mem_span_isStarProjection_of_finite
    (.comap (cfcâ‚™Hom (R := â„) hx : _ â†’â‚—[â„] A) p)
    (by simp_all [Set.subset_def, IsStarProjection.map])

/-- In a FS Câ‹†-algebra, the topological closure of the span of star
projections is exactly the submodule of the self-adjoint elements. -/
@[simp] theorem CStarAlgebra.FiniteSpectrum.topologicalClosure_span_isStarProjection
    [ContinuousConstSMul â„ A] [ContinuousAdd A] [StarModule â„ A] [T2Space A]
    [ContinuousStar A] [h : CStarAlgebra.FiniteSpectrum A] :
    (Submodule.span â„ {x : A | IsStarProjection x}).topologicalClosure =
      selfAdjoint.submodule â„ A := by
  refine le_antisymm (fun x hx â†¦ closure_minimal (fun x hx â†¦ ?_) ?_ hx) fun x hx â†¦ ?_
  Â· refine Submodule.span_induction (fun _ hx â†¦ hx.isSelfAdjoint) ?_ ?_ ?_ hx <;> aesop
  Â· exact isClosed_eq continuous_id'.star continuous_id'
  Â· exact closure_mono (fun y hy â†¦ hy.1.mem_span_isStarProjection_of_finite_quasispectrum hy.2)
      (h.fs hx)

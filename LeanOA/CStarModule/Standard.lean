/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Order
import Mathlib.Analysis.CStarAlgebra.Module.Defs
import Mathlib.Analysis.Normed.Lp.lpSpace

/-! # The standard Câ‹†-module -/

open scoped InnerProductSpace

namespace CStarModule

section Polarization

variable {E A : Type*} [NonUnitalCStarAlgebra A] [PartialOrder A] [StarOrderedRing A]
variable [NormedAddCommGroup E] [Module â„‚ E] [SMul Aáµáµ’áµ– E] [CStarModule A E]

open Complex

lemma polarization {x y : E} :
    4 â€¢ âŸªy, xâŸ«_A = âŸªx + y, x + yâŸ«_A - âŸªx - y, x - yâŸ«_A +
      I â€¢ âŸªx + I â€¢ y, x + I â€¢ yâŸ«_A - I â€¢ âŸªx - I â€¢ y, x - I â€¢ yâŸ«_A := by
  simp [smul_smul, smul_sub]
  abel

lemma polarization' {x y : E} :
    âŸªy, xâŸ«_A = (1 / 4 : â„‚) â€¢ (âŸªx + y, x + yâŸ«_A - âŸªx - y, x - yâŸ«_A +
      I â€¢ âŸªx + I â€¢ y, x + I â€¢ yâŸ«_A - I â€¢ âŸªx - I â€¢ y, x - I â€¢ yâŸ«_A) := by
  rw [â† (IsUnit.mk0 (4 : â„‚) (by norm_num)).smul_left_cancel, ofNat_smul_eq_nsmul â„‚ 4]
  simpa only [one_div, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, smul_inv_smulâ‚€]
    using CStarModule.polarization

end Polarization

variable {Î¹ : Type*} {E : Î¹ â†’ Type*}
variable {A : Type*} [NonUnitalCStarAlgebra A] [PartialOrder A] [StarOrderedRing A]
variable [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, NormedSpace â„‚ (E i)] [âˆ€ i, SMul Aáµáµ’áµ– (E i)]
variable [âˆ€ i, CStarModule A (E i)]

/-- The property that for `x : Î  i, E i`, the sum `âˆ‘' i, âŸªx i, x iâŸ«_A` converges.

Note: the condition that `âˆ‘' i, âŸªx i, x iâŸ«_A` converges is in general *strictly weaker* than
the condition `âˆ‘' i, â€–x iâ€– ^ 2` converges. -/
def MemStandard (x : Î  i, E i) : Prop := Summable fun i â†¦ âŸªx i, x iâŸ«_A

lemma MemStandard.of_memâ„“p {x : Î  i, E i} (hx : Memâ„“p (â€–x Â·â€–) 2) :
    MemStandard x :=
  Summable.of_norm <| by simpa [â† norm_sq_eq, memâ„“p_gen_iff] using hx

lemma dominated_convergence {x y : Î¹ â†’ A} (hx : Summable x) (hy_nonneg : âˆ€ i, 0 â‰¤ y i)
    (h_le : âˆ€ i, y i â‰¤ x i) : Summable y := by
  rw [summable_iff_vanishing] at hx âŠ¢
  intro u hu
  obtain âŸ¨Îµ, Îµ_pos, hÎµâŸ© := Metric.nhds_basis_closedBall.mem_iff.mp hu
  specialize hx (Metric.closedBall 0 Îµ) (Metric.closedBall_mem_nhds 0 Îµ_pos)
  peel hx with s t hst _
  refine hÎµ ?_
  simp only [Metric.mem_closedBall, dist_zero_right] at this âŠ¢
  refine le_trans ?_ this
  refine CStarAlgebra.norm_le_norm_of_nonneg_of_le (t.sum_nonneg fun i _ â†¦ (hy_nonneg i)) ?_
  gcongr
  exact h_le _

lemma MemStandard.zero : MemStandard (0 : Î  i, E i) := by
  simpa [MemStandard] using summable_zero

lemma MemStandard.add {x y : Î  i, E i} (hx : MemStandard x) (hy : MemStandard y) :
    MemStandard (x + y) := by
  rw [MemStandard] at hx hy âŠ¢
  refine dominated_convergence ((hx.add hy).add (hx.add hy)) (fun _ â†¦ inner_self_nonneg) fun i â†¦ ?_
  calc
    _ â‰¤ âŸª(x + y) i, (x + y) iâŸ«_A + âŸª(x - y) i, (x - y) iâŸ«_A :=
      le_add_of_nonneg_right inner_self_nonneg
    _ = _ := by simp; abel

lemma MemStandard.neg {x : Î  i, E i} (hx : MemStandard x) :
    MemStandard (-x) := by
  simpa [MemStandard]

lemma MemStandard.sub {x y : Î  i, E i} (hx : MemStandard x) (hy : MemStandard y) :
    MemStandard (x - y) := by
  rw [sub_eq_add_neg]
  exact hx.add hy.neg

lemma MemStandard.smul (z : â„‚) {x : Î  i, E i} (hx : MemStandard x) :
    MemStandard (z â€¢ x) := by
  simpa [MemStandard] using (hx.const_smul _).const_smul _

open scoped RightActions

lemma MemStandard.smul_right (a : A) {x : Î  i, E i} (hx : MemStandard x) :
    MemStandard (x <â€¢ a) := by
  simpa [MemStandard] using hx.mul_left (star a) |>.mul_right a

open Bornology in
lemma MemStandard.isBounded_norm {x : Î  i, E i} (hx : MemStandard x) :
    IsBounded (Set.range (â€–x Â·â€– ^ 2)) := by
  rw [MemStandard] at hx
  have := Metric.isBounded_range_of_tendsto_cofinite hx.tendsto_cofinite_zero
  rw [isBounded_iff_forall_norm_le] at this âŠ¢
  peel this with C _
  rintro - âŸ¨i, rflâŸ©
  specialize this _ âŸ¨i, rflâŸ©
  simpa [norm_sq_eq]

lemma MemStandard.summable_inner {x y : Î  i, E i} (hx : MemStandard x) (hy : MemStandard y) :
    Summable fun i â†¦ âŸªx i, y iâŸ«_A := by
  conv in âŸªx _, y _âŸ«_A => rw [polarization']
  apply_rules (config := { transparency := .reducible }) [Summable.const_smul, Summable.add, Summable.sub]
  Â· exact hy.add hx
  Â· exact hy.sub hx
  Â· exact hy.add (hx.smul _)
  Â· exact hy.sub (hx.smul _)

lemma MemStandard.subtype {x : Î  i, E i} (hx : MemStandard x) (s : Set Î¹) :
    MemStandard (fun i : s â†¦ x i) := by
  simpa [Function.comp_def] using Summable.subtype hx s

variable (E) in
/-- The standard Câ‹†-module  -/
def Standard : Type _ :=
  { carrier := {x | MemStandard x}
    zero_mem' := .zero
    add_mem' := .add
    smul_mem' := .smul : Submodule â„‚ (Î  i, E i) }

scoped[CStarAlgebra] notation "â„“Â²(" E ")" => CStarModule.Standard E

open scoped CStarAlgebra

namespace Standard

/-- Create a term of `â„“Â²(E)` from an `x : Î  i, E i` and a proof `hx : MemStandard x`.

Note, because `â„“Â²(E)` is defeq to the subtype `{x : Î  i, E i /â€Œ/ MemStandard x}`, Lean will accept
anonymous constructor syntax `âŸ¨x, hxâŸ© : â„“Â²(E)`, but this is defeq abuse, and can make it hard to work
with the resulting term. Instead, users should be prefer to use this bespoke `Standard.mk` function. -/
def mk (x : Î  i, E i) (hx : MemStandard x) : â„“Â²(E) := âŸ¨x, hxâŸ©

/-- The map from `â„“Â²(E)` to `Î  i, E i`. -/
@[coe]
def toPi (x : â„“Â²(E)) : Î  i, E i := Subtype.val x

instance : DFunLike â„“Â²(E) Î¹ (fun i â†¦ E i) where
  coe := Standard.toPi
  coe_injective' := Subtype.val_injective

@[ext] lemma ext {x y : â„“Â²(E)} (h : âˆ€ i, x i = y i) : x = y := DFunLike.ext _ _ h

@[simp] lemma coe_mk {x : Î  i, E i} (hx) : â‡‘(mk x hx) = x := rfl

instance : AddCommGroup â„“Â²(E) := Submodule.addCommGroup _

instance : Module â„‚ â„“Â²(E) := Submodule.module _

instance : SMul Aáµáµ’áµ– â„“Â²(E) where
  smul a x := âŸ¨_, x.property.smul_right (MulOpposite.unop a)âŸ©

@[simp] lemma memStandard (x : â„“Â²(E)) : MemStandard â‡‘x := x.property
@[simp] lemma coe_zero : â‡‘(0 : â„“Â²(E)) = (0 : Î  i, E i) := rfl
@[simp] lemma coe_add {x y : â„“Â²(E)} : â‡‘(x + y) = (x + y : Î  i, E i) := rfl
@[simp] lemma coe_neg {x : â„“Â²(E)} : â‡‘(-x) = (-x : Î  i, E i) := rfl
@[simp] lemma coe_sub {x y : â„“Â²(E)} : â‡‘(x - y) = (x - y : Î  i, E i) := rfl
@[simp] lemma coe_smul {c : â„‚} {x : â„“Â²(E)} : â‡‘(c â€¢ x) = (c â€¢ x : Î  i, E i) := rfl
@[simp] lemma coe_nsmul {n : â„•} {x : â„“Â²(E)} : â‡‘(n â€¢ x) = (n â€¢ x : Î  i, E i) := rfl
@[simp] lemma coe_zsmul {n : â„¤} {x : â„“Â²(E)} : â‡‘(n â€¢ x) = (n â€¢ x : Î  i, E i) := rfl
@[simp] lemma coe_smul_right {a : A} {x : â„“Â²(E)} : â‡‘(x <â€¢ a) = (x <â€¢ a : Î  i, E i) := rfl

noncomputable instance : Norm â„“Â²(E) where
  norm x := âˆšâ€–âˆ‘' i, âŸªx i, x iâŸ«_Aâ€–

lemma norm_def {x : â„“Â²(E)} : â€–xâ€– = âˆšâ€–âˆ‘' i, âŸªx i, x iâŸ«_Aâ€– := rfl

lemma summable_inner (x y : â„“Â²(E)) : Summable fun i â†¦ âŸªx i, y iâŸ«_A :=
  x.memStandard.summable_inner y.memStandard

noncomputable instance : Inner A â„“Â²(E) where
  inner x y := âˆ‘' i, âŸªx i, y iâŸ«_A

lemma inner_def {x y : â„“Â²(E)} : âŸªx, yâŸ«_A = âˆ‘' i, âŸªx i, y iâŸ«_A := rfl

lemma inner_apply_self_le_inner (x : â„“Â²(E)) (i : Î¹) : âŸªx i, x iâŸ«_A â‰¤ âŸªx, xâŸ«_A :=
  le_tsum x.memStandard _ fun _ _ â†¦ inner_self_nonneg

lemma sum_inner_apply_self_le_inner (x : â„“Â²(E)) (s : Finset Î¹) :
    âˆ‘ i in s, âŸªx i, x iâŸ«_A â‰¤ âŸªx, xâŸ«_A :=
  sum_le_tsum s (fun _ _ â†¦ inner_self_nonneg) x.memStandard

lemma tsum_inner_apply_self_le_inner (x : â„“Â²(E)) (s : Set Î¹) :
    âˆ‘' i : s, âŸªx i, x iâŸ«_A â‰¤ âŸªx, xâŸ«_A :=
  tsum_subtype_le _ _ (fun _ â†¦ inner_self_nonneg) x.memStandard

noncomputable instance : CStarModule A â„“Â²(E) where
  inner_add_right {x y z} := by
    simpa [inner_def] using tsum_add (x.summable_inner y) (x.summable_inner z)
  inner_self_nonneg := tsum_nonneg fun i => inner_self_nonneg
  inner_self {x} := by
    refine âŸ¨fun hx â†¦ ?_, fun h â†¦ by simp [h, inner_def]âŸ©
    ext i
    rw [coe_zero, Pi.zero_apply, â† inner_self]
    exact le_antisymm (hx â–¸ inner_apply_self_le_inner x i) inner_self_nonneg
  inner_op_smul_right {a x y} := by
    simpa only [coe_smul_right, Pi.smul_apply, inner_op_smul_right, inner_def]
      using x.summable_inner y |>.tsum_mul_right a
  inner_smul_right_complex {c x y} := by
    simpa only [coe_smul, Pi.smul_apply, inner_smul_right_complex, inner_def]
      using tsum_const_smul c <| x.summable_inner y
  star_inner x y := by simpa using (starL â„‚).map_tsum (f := fun i â†¦ âŸªx i, y iâŸ«_A)
  norm_eq_sqrt_norm_inner_self _ := rfl

noncomputable instance : NormedAddCommGroup â„“Â²(E) := normedAddCommGroup

instance : NormedSpace â„‚ â„“Â²(E) := .ofCore normedSpaceCore

lemma norm_apply_le (x : â„“Â²(E)) (i : Î¹) : â€–x iâ€– â‰¤ â€–xâ€– := by
  refine Real.le_sqrt_of_sq_le ?_
  rw [norm_sq_eq]
  exact CStarAlgebra.norm_le_norm_of_nonneg_of_le inner_self_nonneg (inner_apply_self_le_inner x i)

lemma norm_sum_inner_apply_le (x : â„“Â²(E)) (s : Finset Î¹) :
    â€–âˆ‘ i in s, âŸªx i, x iâŸ«_Aâ€– â‰¤ â€–xâ€– ^ 2 := by
  rw [norm_def, Real.sq_sqrt (by positivity)]
  exact CStarAlgebra.norm_le_norm_of_nonneg_of_le (s.sum_nonneg fun _ _ â†¦ inner_self_nonneg)
    (sum_inner_apply_self_le_inner x s)

/-- The coercion from `â„“Â²(E)` to `Î  i, E i` is uniformly continuous. -/
theorem uniformContinuous_coe :
    UniformContinuous ((â†‘) : â„“Â²(E) â†’ Î  i, E i) := by
  rw [uniformContinuous_pi]
  intro i
  rw [NormedAddCommGroup.uniformity_basis_dist.uniformContinuous_iff
    NormedAddCommGroup.uniformity_basis_dist]
  refine fun Îµ hÎµ â†¦ âŸ¨Îµ, hÎµ, fun f g (hfg : â€–f - gâ€– < Îµ) â†¦ ?_âŸ©
  exact norm_apply_le (f - g) i |>.trans_lt hfg

open Filter Topology

theorem tendsto_of_tendsto_pi {F : â„• â†’ â„“Â²(E)} (hF : CauchySeq F) {f : â„“Â²(E)}
    (hf : Tendsto (fun i â†¦ â‡‘(F i)) atTop (ğ“ f)) : Tendsto F atTop (ğ“ f) := by
  rw [Metric.nhds_basis_closedBall.tendsto_right_iff]
  intro Îµ hÎµ
  have hÎµ' : { p : â„“Â²(E) Ã— â„“Â²(E) | â€–p.1 - p.2â€– < Îµ / âˆš2 } âˆˆ uniformity â„“Â²(E) :=
    NormedAddCommGroup.uniformity_basis_dist.mem_of_mem (by positivity)
  refine (hF.eventually_eventually hÎµ').mono ?_
  rintro n (hn : âˆ€á¶  l in atTop, â€–F n - F lâ€– < Îµ / âˆš2)
  simp only [Metric.mem_closedBall, dist_eq_norm, norm_def, Real.sqrt_le_iff, hÎµ.le, true_and]
  obtain âŸ¨s, hsâŸ© := (F n - f).memStandard.tsum_vanishing (Metric.ball_mem_nhds 0 (by positivity : 0 < Îµ ^ 2 / 2))
  rw [â† sum_add_tsum_compl (s := s) (F n - f).memStandard, â† add_halves (Îµ ^ 2)]
  apply norm_add_le _ _ |>.trans ?_
  gcongr
  Â· apply le_of_tendsto (f := fun l â†¦ â€–âˆ‘ x âˆˆ s, âŸª(F n - F l) x, (F n - F l) xâŸ«_Aâ€–) (x := atTop) ?_ ?_
    Â· refine tendsto_norm.comp <| tendsto_finset_sum s fun i hi â†¦ ?_
      rw [tendsto_pi_nhds] at hf
      have := tendsto_const_nhds (x := F n i) |>.sub (hf i)
      refine (continuous_inner.tendsto _).comp (this.prod_mk_nhds this)
    Â· filter_upwards [hn] with m hm
      rw [â† Real.sqrt_sq (norm_nonneg _), Real.sqrt_lt (by positivity) (by positivity)] at hm
      refine norm_sum_inner_apply_le _ s |>.trans hm.le |>.trans <| by simp [div_pow]
  Â· simp only [Metric.mem_ball, dist_zero_right] at hs
    exact (hs _ disjoint_compl_left).le

instance instCompletSpace [âˆ€ i, CompleteSpace (E i)] : CompleteSpace â„“Â²(E) :=
  Metric.complete_of_cauchySeq_tendsto <| by
    intro x hx
    -- A Cauchy sequence in `â„“Â²(E)` is pointwise convergent; let `y` be the pointwise limit.
    obtain âŸ¨y, hyâŸ© := cauchySeq_tendsto_of_complete
      (uniformContinuous_coe.comp_cauchySeq hx)
    -- Since the Cauchy sequence is bounded, its pointwise limit `y` is in `â„“Â²(E)`.
    have hy' : MemStandard y := by
      rw [MemStandard, summable_iff_cauchySeq_finset, cauchySeq_finset_iff_vanishing_norm]
      intro Îµ hÎµ
      rw [NormedAddCommGroup.cauchySeq_iff] at hx
      have hÎµ8 : 0 < Îµ / 8 := by positivity
      specialize hx âˆš(Îµ / 8) (by positivity)
      obtain âŸ¨N, hNâŸ© := hx
      have hxN := (x N).memStandard
      rw [MemStandard, summable_iff_cauchySeq_finset, cauchySeq_finset_iff_vanishing_norm] at hxN
      peel hxN (Îµ / 8) hÎµ8 with s t ht hxN
      simp only [Function.comp_def, tendsto_pi_nhds] at hy
      refine lt_of_le_of_lt ?_ (half_lt_self hÎµ)
      refine le_of_tendsto (f := fun n â†¦ â€–âˆ‘ i âˆˆ t, âŸªx n i, x n iâŸ«_Aâ€–) (x := atTop) ?_ ?_
      Â· exact tendsto_norm.comp <| tendsto_finset_sum t fun i hi â†¦
          (continuous_inner.tendsto _).comp ((hy i).prod_mk_nhds (hy i))
      Â· filter_upwards [Ici_mem_atTop N] with m hm
        replace hN := (hN N le_rfl m hm).le
        have (j : Î¹) (a b : E j) :
            âŸªb, bâŸ«_A = âŸªa - b, a - bâŸ«_A + âŸªa, b - aâŸ«_A + âŸªb - a, aâŸ«_A + âŸªa, aâŸ«_A := by
          simp; abel
        conv =>
          enter [1, 1, 2, i]
          rw [this i (x N i) (x m i)]
          simp only [â† Pi.sub_apply]
        simp only [Finset.sum_add_distrib, Finset.sum_sub_distrib]
        have hâ‚ : â€–âˆ‘ i âˆˆ t, âŸª(x N - x m) i, (x N - x m) iâŸ«_Aâ€– â‰¤ Îµ / 8 := by
          refine norm_sum_inner_apply_le (x N - x m) t |>.trans ?_
          exact Real.le_sqrt (by positivity) (by positivity) |>.mp hN
        have hâ‚‚ : â€–âˆ‘ i âˆˆ t, âŸªx N i, (x m - x N) iâŸ«_Aâ€– â‰¤ Îµ / 8 := by
          classical
          let x' : â„“Â²(E) := mk (fun i â†¦ if i âˆˆ t then x N i else 0) <| by
            refine (MemStandard.zero (E := E)).congr_cofinite ?_
            filter_upwards [show (tá¶œ : Set Î¹) âˆˆ cofinite by simp] with i (hi : i âˆ‰ t)
            simp [hi]
          calc
            _ â‰¤ â€–x'â€– * â€–x m - x Nâ€– := by
              refine le_of_eq_of_le ?_ (norm_inner_le â„“Â²(E))
              rw [inner_def, tsum_eq_sum fun i (hi : i âˆ‰ t) â†¦ by simp [x', hi]]
              congr! 3 with i hi
              simp [x', hi]
            _ â‰¤ Îµ / 8 := by
              rw [â† Real.mul_self_sqrt hÎµ8.le]
              refine mul_le_mul ?_ (by rwa [â† norm_neg, neg_sub]) (by positivity) (by positivity)
              simp only [x', norm_def]
              convert Real.sqrt_le_sqrt hxN.le using 3
              rw [tsum_eq_sum fun i (hi : i âˆ‰ t) â†¦ by simp [x', hi]]
              congr! 2 with i hi
              all_goals simp [hi]
        -- ooh, we *really* want the `setm` tactic below, that would be *sooo* much nicer
        calc
          â€–âˆ‘ i âˆˆ t, âŸª(x N - x m) i, (x N - x m) iâŸ«_A + âˆ‘ i âˆˆ t, âŸªx N i, (x m - x N) iâŸ«_A +
              âˆ‘ i âˆˆ t, âŸª(x m - x N) i, x N iâŸ«_A + âˆ‘ i âˆˆ t, âŸªx N i, x N iâŸ«_Aâ€–
            â‰¤ â€–âˆ‘ i âˆˆ t, âŸª(x N - x m) i, (x N - x m) iâŸ«_Aâ€– + â€–âˆ‘ i âˆˆ t, âŸªx N i, (x m - x N) iâŸ«_Aâ€– +
                â€–âˆ‘ i âˆˆ t, âŸª(x m - x N) i, x N iâŸ«_Aâ€– + â€–âˆ‘ i âˆˆ t, âŸªx N i, x N iâŸ«_Aâ€– :=
            norm_add_le _ _ |>.trans <| add_le_add_right norm_addâ‚ƒ_le _
          _ â‰¤ Îµ / 8 + Îµ / 8 + Îµ / 8 + Îµ / 8 := by
            gcongr
            rw [â† norm_star, star_sum]
            simpa only [star_inner]
          _ = Îµ / 2 := by ring
    exact âŸ¨âŸ¨y, hy'âŸ©, tendsto_of_tendsto_pi hx hyâŸ©

end Standard

end CStarModule

import LeanOA.Mathlib.Analysis.CStarAlgebra.PositiveLinearMap
import Mathlib.Analysis.CStarAlgebra.GelfandNaimarkSegal

open scoped ComplexOrder

variable {A : Type*} [CStarAlgebra A] [PartialOrder A] [StarOrderedRing A]

lemma PositiveLinearMap.norm_apply_le (f : A ‚Üí‚Çö[‚ÑÇ] ‚ÑÇ) (x : A) : ‚Äñf x‚Äñ ‚â§ (f 1).re * ‚Äñx‚Äñ := by
  rw [‚Üê sq_le_sq‚ÇÄ (by positivity) (mul_nonneg (Complex.nonneg_iff.mp
      (f.map_nonneg zero_le_one) |>.1) (by positivity))]
  calc
    _ ‚â§ (f 1).re * (f (star x * x)).re := by
      have (x y : A) : ‚Äñf (star x * y)‚Äñ ^ 2 ‚â§ (f (star x * x)).re * (f (star y * y)).re := by
        simpa [‚Üê inner_conj_symm (f.toPreGNS y) (f.toPreGNS x), Complex.star_def, norm_star,
            ‚Üê sq, f.preGNS_inner_def, ‚Üê Complex.ofReal_pow, f.preGNS_norm_def, Real.sq_sqrt,
            Complex.nonneg_iff.mp (f.map_nonneg (star_mul_self_nonneg _))] using
          inner_mul_inner_self_le (ùïú := ‚ÑÇ) (f.toPreGNS x) (f.toPreGNS y)
      simpa using this 1 x
    _ ‚â§ (f 1).re * (‚Äñx‚Äñ ^ 2 * (f 1).re) := by
      refine mul_le_mul_of_nonneg_left ?_ (Complex.nonneg_iff.mp (f.map_nonneg zero_le_one) |>.1)
      have := by simpa [CStarRing.norm_star_mul_self, Algebra.algebraMap_eq_smul_one, ‚Üê sq] using
        f.apply_le_of_isSelfAdjoint _ (.star_mul_self x)
      convert Complex.le_def.mp this |>.1
      rw [‚Üê Complex.ofReal_pow, Complex.re_ofReal_mul]
    _ = _ := by ring

theorem PositiveLinearMap.norm_map_one (f : A ‚Üí‚Çö[‚ÑÇ] ‚ÑÇ) : ‚Äñf 1‚Äñ = (f 1).re := by
  by_cases! Subsingleton A
  ¬∑ simp [Subsingleton.eq_zero (1 : A)]
  exact le_antisymm (by simpa using f.norm_apply_le 1) (Complex.re_le_norm _)

theorem PositiveLinearMap.opNorm_eq_re_map_one (f : A ‚Üí‚Çö[‚ÑÇ] ‚ÑÇ) :
    ‚Äñf.toContinuousLinearMap‚Äñ = (f 1).re := by
  refine le_antisymm (f.toContinuousLinearMap.opNorm_le_bound
    (by simp [‚Üê norm_map_one]) f.norm_apply_le) ?_
  by_cases! Subsingleton A
  ¬∑ simp [Subsingleton.eq_zero (1 : A)]
  simpa [norm_map_one] using f.toContinuousLinearMap.le_opNorm 1

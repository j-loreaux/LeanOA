import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Order
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Unitary
import Mathlib.Analysis.CStarAlgebra.Exponential
import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.ExpLog
import LeanOA.ForMathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.Rpow.Basic
import LeanOA.ForMathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.Abs
import LeanOA.ForMathlib.Algebra.Star.Unitary
import LeanOA.ForMathlib.Misc
import LeanOA.ContinuousMap.Uniform
import LeanOA.ContinuousFunctionalCalculus.Continuity
import Mathlib.Topology.Algebra.OpenSubgroup
import Mathlib.Analysis.SpecialFunctions.Complex.Circle

/-! # Properties of unitary elements in a C‚ãÜ-algebra

## Main results

+ `CStarAlgebra.exists_sum_four_unitary`: every element `x` in a unital C‚ãÜ-algebra is a linear
  combination of four unitary elements, and the norm of each coefficient does not exceed `‚Äñx‚Äñ / 2`.
+ `CStarAlgebra.span_unitary`: a unital C‚ãÜ-algebra is spanned by its unitary elements.

## To do

+ if `‚Äñu - 1‚Äñ < 1`, then there is a selfadjoint `x` such that `u = exp(I ‚Ä¢ x)`.
+ if `‚Äñu - 1‚Äñ < 1`, then there is a path of unitaries from `u` to `1`.
+ if `‚Äñu - v‚Äñ < 1`, then `u = exp(I ‚Ä¢ x) v`.
+ the path component of the identity in the unitary group is the set of unitaries `u` which
  are finite products of exponential unitaries.
+ `unitary A` is locally path connected

-/

variable {A : Type*} [CStarAlgebra A] [PartialOrder A] [StarOrderedRing A]


section UnitarySpan

open scoped ComplexStarModule
open Complex

/-- If `a : A` is a selfadjoint element in a C‚ãÜ-algebra with `‚Äña‚Äñ ‚â§ 1`,
then `a + I ‚Ä¢ CFC.sqrt (1 - a ^ 2)` is unitary.

This is the key tool to show that a C‚ãÜ-algebra is spanned by its unitary elements. -/
lemma IsSelfAdjoint.self_add_I_smul_cfcSqrt_sub_sq_mem_unitary (a : A) (ha : IsSelfAdjoint a)
    (ha_norm : ‚Äña‚Äñ ‚â§ 1) : a + I ‚Ä¢ CFC.sqrt (1 - a ^ 2) ‚àà unitary A := by
  obtain (_ | _) := subsingleton_or_nontrivial A
  ¬∑ simp [Subsingleton.elim (a + I ‚Ä¢ CFC.sqrt (1 - a ^ 2)) 1]
  have key : a + I ‚Ä¢ CFC.sqrt (1 - a ^ 2) = cfc (fun x : ‚ÑÇ ‚Ü¶ x.re + I * ‚àö(1 - x.re ^ 2)) a := by
    rw [CFC.sqrt_eq_real_sqrt (1 - a ^ 2) ?nonneg]
    case nonneg =>
      rwa [sub_nonneg, ‚Üê CStarAlgebra.norm_le_one_iff_of_nonneg (a ^ 2), ha.sq_norm,
        sq_le_one_iff‚ÇÄ (by positivity)]
    -- I *really* want this to be solved with `cfc_pull`. This is a good example of a stress test.
    rw [cfc_add .., cfc_const_mul .., ‚Üê cfc_real_eq_complex (fun x ‚Ü¶ x) ha, cfc_id' ‚Ñù a,
      ‚Üê cfc_real_eq_complex (fun x ‚Ü¶ ‚àö(1 - x ^2)) ha, cfc‚Çô_eq_cfc, cfc_comp' (‚àö¬∑) (1 - ¬∑ ^ 2) a,
      cfc_sub .., cfc_pow .., cfc_const_one .., cfc_id' ..]
  rw [key, cfc_unitary_iff ..]
  intro x hx
  rw [‚Üê starRingEnd_apply, ‚Üê Complex.normSq_eq_conj_mul_self,
    Complex.normSq_ofReal_add_I_smul_sqrt_one_sub, Complex.ofReal_one]
  exact spectrum.norm_le_norm_of_mem (ha.spectrumRestricts.apply_mem hx) |>.trans ha_norm

@[simps]
noncomputable def selfAdjoint.unitarySelfAddISMul (a : selfAdjoint A)
    (ha_norm : ‚Äña‚Äñ ‚â§ 1) :
    unitary A :=
  ‚ü®(a : A) + I ‚Ä¢ CFC.sqrt (1 - a ^ 2 : A), a.2.self_add_I_smul_cfcSqrt_sub_sq_mem_unitary _ ha_norm‚ü©

lemma selfAdjoint.star_coe_unitarySelfAddISMul (a : selfAdjoint A) (ha_norm : ‚Äña‚Äñ ‚â§ 1) :
    (star (unitarySelfAddISMul a ha_norm) : A) = a - I ‚Ä¢ CFC.sqrt (1 - a ^ 2 : A) := by
  simp [a.2, IsSelfAdjoint.star_eq, ‚Üê sub_eq_add_neg,
    IsSelfAdjoint.of_nonneg (CFC.sqrt_nonneg (a := (1 - a ^ 2 : A)))]

@[simp high]
lemma selfAdjoint.realPart_unitarySelfAddISMul (a : selfAdjoint A) (ha_norm : ‚Äña‚Äñ ‚â§ 1) :
    ‚Ñú (unitarySelfAddISMul a ha_norm : A) = a := by
  simp [IsSelfAdjoint.imaginaryPart (x := CFC.sqrt (1 - a ^ 2 : A)) (by cfc_tac)]

/-- A stepping stone to `CStarAlgebra.exists_sum_four_unitary` that specifies the unitary
elements precisely. -/
lemma CStarAlgebra.norm_smul_two_inv_smul_add_four_unitary (x : A) (hx : x ‚â† 0) :
    let u‚ÇÅ : unitary A := selfAdjoint.unitarySelfAddISMul (‚Ñú (‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x))
      (by simpa [norm_smul, inv_mul_le_one‚ÇÄ (norm_pos_iff.2 hx)] using realPart.norm_le x)
    let u‚ÇÇ : unitary A := selfAdjoint.unitarySelfAddISMul (‚Ñë (‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x))
      (by simpa [norm_smul, inv_mul_le_one‚ÇÄ (norm_pos_iff.2 hx)] using imaginaryPart.norm_le x)
    x = ‚Äñx‚Äñ ‚Ä¢ (2‚Åª¬π : ‚Ñù) ‚Ä¢ (u‚ÇÅ + star u‚ÇÅ + I ‚Ä¢ (u‚ÇÇ + star u‚ÇÇ) : A) := by
  intro u‚ÇÅ u‚ÇÇ
  rw [smul_add, smul_comm _ I, unitary.coe_star, unitary.coe_star,
    ‚Üê realPart_apply_coe (u‚ÇÅ : A), ‚Üê realPart_apply_coe (u‚ÇÇ : A)]
  simp only [u‚ÇÅ, u‚ÇÇ, selfAdjoint.realPart_unitarySelfAddISMul,
    realPart_add_I_smul_imaginaryPart, norm_smul_norm_inv_smul]

/-- Every element `x` in a unital C‚ãÜ-algebra is a linear combination of four unitary elements,
and the norm of each coefficient does not exceed `‚Äñx‚Äñ / 2`. -/
lemma CStarAlgebra.exists_sum_four_unitary (x : A) :
    ‚àÉ u : Fin 4 ‚Üí unitary A, ‚àÉ c : Fin 4 ‚Üí ‚ÑÇ, x = ‚àë i, c i ‚Ä¢ (u i : A) ‚àß ‚àÄ i, ‚Äñc i‚Äñ ‚â§ ‚Äñx‚Äñ / 2 := by
  obtain (rfl | hx) := eq_or_ne x 0
  ¬∑ exact ‚ü®![1, -1, 1, -1], 0, by simp‚ü©
  ¬∑ have := norm_smul_two_inv_smul_add_four_unitary x hx
    extract_lets u‚ÇÅ u‚ÇÇ at this
    use ![u‚ÇÅ, star u‚ÇÅ, u‚ÇÇ, star u‚ÇÇ]
    use ![‚Äñx‚Äñ * 2‚Åª¬π, ‚Äñx‚Äñ * 2‚Åª¬π, ‚Äñx‚Äñ * 2‚Åª¬π * I, ‚Äñx‚Äñ * 2‚Åª¬π * I]
    constructor
    ¬∑ conv_lhs => rw [this]
      simp [Fin.sum_univ_four, ‚Üê Complex.coe_smul]
      module
    ¬∑ intro i
      fin_cases i
      all_goals simp [div_eq_mul_inv]

variable (A) in
open Submodule in
/-- A unital C‚ãÜ-algebra is spanned by its unitary elements. -/
lemma CStarAlgebra.span_unitary : span ‚ÑÇ (unitary A : Set A) = ‚ä§ := by
  rw [eq_top_iff]
  rintro x -
  obtain ‚ü®u, c, rfl, h‚ü© := CStarAlgebra.exists_sum_four_unitary x
  exact sum_mem fun i _ ‚Ü¶ Submodule.smul_mem _ _ (subset_span (u i).2)

end UnitarySpan

section ExpUnitary

-- if `‚Äñu - 1‚Äñ < 1`, then there is a selfadjoint `x` such that `u = exp(I ‚Ä¢ x)`.

variable {A : Type*} [CStarAlgebra A]

open Complex


@[aesop safe apply (rule_sets := [CStarAlgebra])] -- this has a bad discr tree key :-(
lemma IsSelfAdjoint.cfc_arg (u : A) : IsSelfAdjoint (cfc (arg ¬∑ : ‚ÑÇ ‚Üí ‚ÑÇ) u) := by
  simp [isSelfAdjoint_iff, ‚Üê cfc_star]

attribute [fun_prop] NormedSpace.exp_continuous


lemma Complex.norm_sub_one_sq_eq_of_norm_one {z : ‚ÑÇ} (hz : ‚Äñz‚Äñ = 1) :
    ‚Äñz - 1‚Äñ ^ 2 = 2 * (1 - z.re) := by
  have : z.im * z.im = 1 - z.re * z.re := by
    replace hz := sq_eq_one_iff.mpr (.inl hz)
    rw [Complex.sq_norm, normSq_apply] at hz
    linarith
  simp [Complex.sq_norm, normSq_apply, this]
  ring

open Metric in
lemma Complex.norm_sub_one_sq_eqOn_sphere :
    (sphere (0 : ‚ÑÇ) 1).EqOn (‚Äñ¬∑ - 1‚Äñ ^ 2) (fun z ‚Ü¶ 2 * (1 - z.re)) :=
  fun z hz ‚Ü¶ norm_sub_one_sq_eq_of_norm_one (by simpa using hz)

attribute [aesop 10% apply (rule_sets := [CStarAlgebra])] isStarNormal_of_mem_unitary

open Complex in
lemma unitary.two_mul_one_sub_le_norm_sub_one_sq {u : A} (hu : u ‚àà unitary A)
    {z : ‚ÑÇ} (hz : z ‚àà spectrum ‚ÑÇ u) :
    2 * (1 - z.re) ‚â§ ‚Äñu - 1‚Äñ ^ 2 := by
  rw [‚Üê Real.sqrt_le_left (by positivity)]
  have := spectrum.subset_circle_of_unitary hu hz
  simp only [mem_sphere_iff_norm, sub_zero] at this
  rw [‚Üê cfc_id' ‚ÑÇ u, ‚Üê cfc_one ‚ÑÇ u, ‚Üê cfc_sub ..]
  convert norm_apply_le_norm_cfc (fun z ‚Ü¶ z - 1) u hz
  simpa using congr(Real.sqrt $(norm_sub_one_sq_eq_of_norm_one this)).symm

open Complex in
lemma unitary.norm_sub_one_sq_eq {u : A} (hu : u ‚àà unitary A) {x : ‚Ñù}
    (hz : IsLeast (re '' (spectrum ‚ÑÇ u)) x) :
    ‚Äñu - 1‚Äñ ^ 2 = 2 * (1 - x) := by
  obtain (_ | _) := subsingleton_or_nontrivial A
  ¬∑ exfalso; apply hz.nonempty.of_image.ne_empty; simp
  rw [‚Üê cfc_id' ‚ÑÇ u, ‚Üê cfc_one ‚ÑÇ u, ‚Üê cfc_sub ..]
  have h_eqOn : (spectrum ‚ÑÇ u).EqOn (fun z ‚Ü¶ ‚Äñz - 1‚Äñ ^ 2) (fun z ‚Ü¶ 2 * (1 - z.re)) :=
    Complex.norm_sub_one_sq_eqOn_sphere.mono <| spectrum.subset_circle_of_unitary (ùïú := ‚ÑÇ) hu
  have h‚ÇÇ : IsGreatest ((fun z ‚Ü¶ 2 * (1 - z.re)) '' (spectrum ‚ÑÇ u)) (2 * (1 - x)) := by
    have : Antitone (fun y : ‚Ñù ‚Ü¶ 2 * (1 - y)) := by intro _ _ _; simp only; gcongr
    simpa [Set.image_image] using this.map_isLeast hz
  have h‚ÇÉ : IsGreatest ((‚Äñ¬∑ - 1‚Äñ ^ 2) '' spectrum ‚ÑÇ u) (‚Äñcfc (¬∑ - 1 : ‚ÑÇ ‚Üí ‚ÑÇ) u‚Äñ ^ 2) := by
    have := pow_left_monotoneOn (n := 2) |>.mono (s‚ÇÇ := ((‚Äñ¬∑ - 1‚Äñ) '' spectrum ‚ÑÇ u)) (by aesop)
    simpa [Set.image_image] using this.map_isGreatest (IsGreatest.norm_cfc (fun z : ‚ÑÇ ‚Ü¶ z - 1) u)
  exact h‚ÇÉ.unique (h_eqOn.image_eq ‚ñ∏ h‚ÇÇ)

-- move to `Analysis.CStarAlgebra.Spectrum`
theorem spectrum.norm_eq_one_of_unitary {ùïú : Type*} [NormedField ùïú] {E : Type*} [NormedRing E]
    [StarRing E] [CStarRing E] [NormedAlgebra ùïú E] [CompleteSpace E] {u : E} (hu : u ‚àà unitary E)
    ‚¶Éz : ùïú‚¶Ñ (hz : z ‚àà spectrum ùïú u) : ‚Äñz‚Äñ = 1 := by
  simpa using spectrum.subset_circle_of_unitary hu hz

lemma Complex.norm_le_re_iff_eq_norm {z : ‚ÑÇ} :
    ‚Äñz‚Äñ ‚â§ z.re ‚Üî z = ‚Äñz‚Äñ := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ replace h : z.re = ‚Äñz‚Äñ := le_antisymm (re_le_norm z) h
    apply ext
    ¬∑ simp [h]
    ¬∑ rw [re_eq_norm, nonneg_iff] at h
      simpa using h.2.symm
  ¬∑ rw [h]
    simp

lemma Complex.re_le_neg_norm_iff_eq_neg_norm {z : ‚ÑÇ} :
    z.re ‚â§ -‚Äñz‚Äñ ‚Üî z = -‚Äñz‚Äñ := by
  simpa [neg_eq_iff_eq_neg, le_neg] using norm_le_re_iff_eq_norm (z := -z)

lemma Complex.norm_le_im_iff_eq_I_mul_norm {z : ‚ÑÇ} :
    ‚Äñz‚Äñ ‚â§ z.im ‚Üî z = I * ‚Äñz‚Äñ := by
  have := norm_le_re_iff_eq_norm (z := -I * z)
  simp only [Complex.norm_mul, norm_neg, norm_I, one_mul, mul_re, neg_re, I_re,
    neg_zero, zero_mul, neg_im, I_im, zero_sub, ‚Üê neg_mul, neg_neg] at this
  rw [this, ‚Üê smul_eq_mul, eq_comm, ‚Üê inv_smul_eq_iff‚ÇÄ (by simp)]
  simp [‚Üê neg_inv, eq_comm]

lemma Complex.im_le_neg_norm_iff_eq_neg_I_mul_norm {z : ‚ÑÇ} :
    z.im ‚â§ -‚Äñz‚Äñ ‚Üî z = -(I * ‚Äñz‚Äñ) := by
  simpa [neg_eq_iff_eq_neg, le_neg] using norm_le_im_iff_eq_I_mul_norm (z := -z)

lemma unitary.norm_sub_one_lt_two_iff {u : A} (hu : u ‚àà unitary A) :
    ‚Äñu - 1‚Äñ < 2 ‚Üî -1 ‚àâ spectrum ‚ÑÇ u := by
  nontriviality A
  rw [‚Üê sq_lt_sq‚ÇÄ (by positivity) (by positivity)]
  constructor
  ¬∑ intro h h1
    have := unitary.two_mul_one_sub_le_norm_sub_one_sq hu h1 |>.trans_lt h
    norm_num at this
  ¬∑ contrapose!
    obtain ‚ü®x, hx‚ü© := spectrum.isCompact (ùïú := ‚ÑÇ) u |>.image continuous_re |>.exists_isLeast <| (spectrum.nonempty _).image _
    rw [unitary.norm_sub_one_sq_eq hu hx]
    obtain ‚ü®z, hz, rfl‚ü© := hx.1
    intro key
    replace key : z.re ‚â§ -1 := by linarith
    have hz_norm : ‚Äñz‚Äñ = 1 := spectrum.norm_eq_one_of_unitary hu hz
    rw [‚Üê hz_norm, Complex.re_le_neg_norm_iff_eq_neg_norm, hz_norm] at key
    exact key ‚ñ∏ hz

lemma unitary.spectrum_subset_slitPlane_of_norm_lt_two {u : A} (hu : u ‚àà unitary A)
    (hu_norm : ‚Äñu - 1‚Äñ < 2) :
    spectrum ‚ÑÇ u ‚äÜ slitPlane:= by
  intro z hz
  rw [mem_slitPlane_iff]
  have hz_norm : ‚Äñz‚Äñ = 1 := spectrum.norm_eq_one_of_unitary hu hz
  have := sq_eq_one_iff.mpr <| .inl hz_norm
  rw [‚Üê normSq_eq_norm_sq, normSq_apply] at this
  by_cases h : z.im = 0
  ¬∑ simp [h, ‚Üê sq] at this
    cases this with
    | inl h => simp [h]
    | inr h =>
      have := hz_norm ‚ñ∏ h.le
      rw [Complex.re_le_neg_norm_iff_eq_neg_norm, hz_norm, ofReal_one] at this
      rw [unitary.norm_sub_one_lt_two_iff hu] at hu_norm
      exact False.elim <| hu_norm (this ‚ñ∏ hz)
  ¬∑ exact .inr h

/-- The selfadjoint element obtained by taking the argument (using the principal branch and the
continuous functional calculus) of a unitary whose spectrum does not contain `-1`. This returns
`0` if the principal branch of the logarithm is not continuous on the spectrum of the unitary
element. -/
@[simps]
noncomputable def unitary.argSelfAdjoint (u : unitary A) : selfAdjoint A :=
  ‚ü®cfc (arg ¬∑ : ‚ÑÇ ‚Üí ‚ÑÇ) (u : A), .cfc_arg (u : A)‚ü©

open scoped Real in
lemma selfAdjoint.norm_sq_expUnitary_sub_one {x : selfAdjoint A} (hx : ‚Äñx‚Äñ ‚â§ œÄ) :
    ‚Äñ(expUnitary x - 1 : A)‚Äñ ^ 2 = 2 * (1 - Real.cos ‚Äñx‚Äñ) := by
  nontriviality A
  apply unitary.norm_sub_one_sq_eq (expUnitary x).2
  simp only [expUnitary_coe, AddSubgroupClass.coe_norm]
  rw [‚Üê CFC.exp_eq_normedSpace_exp, ‚Üê cfc_comp_smul I _ (x : A), cfc_map_spectrum .., ‚Üê x.2.spectrumRestricts.algebraMap_image]
  simp only [Set.image_image, coe_algebraMap, smul_eq_mul, mul_comm I, ‚Üê exp_eq_exp_‚ÑÇ, exp_ofReal_mul_I_re]
  refine ‚ü®?_, ?_‚ü©
  ¬∑ cases CStarAlgebra.norm_or_neg_norm_mem_spectrum x.2 with
    | inl h => exact ‚ü®_, h, rfl‚ü©
    | inr h => exact ‚ü®_, h, by simp‚ü©
  ¬∑ rintro - ‚ü®y, hy, rfl‚ü©
    exact Real.cos_abs y ‚ñ∏ Real.cos_le_cos_of_nonneg_of_le_pi (by positivity) hx <| spectrum.norm_le_norm_of_mem hy

open scoped Real in
open unitary selfAdjoint in
lemma argSelfAdjoint_expUnitary {x : selfAdjoint A} (hx : ‚Äñx‚Äñ < œÄ) :
    argSelfAdjoint (expUnitary x) = x := by
  nontriviality A
  ext
  have : spectrum ‚ÑÇ (expUnitary x : A) ‚äÜ slitPlane := by
    apply unitary.spectrum_subset_slitPlane_of_norm_lt_two (expUnitary x).2
    rw [‚Üê sq_lt_sq‚ÇÄ (by positivity) (by positivity), norm_sq_expUnitary_sub_one hx.le]
    calc
      2 * (1 - Real.cos ‚Äñx‚Äñ) < 2 * (1 - Real.cos œÄ) := by
        gcongr
        exact Real.cos_lt_cos_of_nonneg_of_le_pi (by positivity) le_rfl hx
      _ = 2 ^ 2 := by norm_num
  simp only [argSelfAdjoint_coe, expUnitary_coe]
  rw [‚Üê CFC.exp_eq_normedSpace_exp, ‚Üê cfc_comp_smul .., ‚Üê cfc_comp' (hg := ?_)]
  ¬∑ conv_rhs => rw [‚Üê cfc_id' ‚ÑÇ (x : A)]
    refine cfc_congr fun y hy ‚Ü¶ ?_
    rw [‚Üê x.2.spectrumRestricts.algebraMap_image] at hy
    obtain ‚ü®y, hy, rfl‚ü© := hy
    simp [‚Üê exp_eq_exp_‚ÑÇ, exp_ofReal_mul_I_re, mul_comm I, ‚Üê ofReal_mul, exp_ofReal_mul_I_re]
    replace hy : ‚Äñy‚Äñ < œÄ := spectrum.norm_le_norm_of_mem hy |>.trans_lt hx
    simp only [Real.norm_eq_abs, abs_lt] at hy
    rw [‚Üê Circle.coe_exp, Circle.arg_exp hy.1 hy.2.le]
  refine continuous_ofReal.comp_continuousOn <| continuousOn_arg.mono ?_
  rwa [expUnitary_coe, ‚Üê CFC.exp_eq_normedSpace_exp, ‚Üê cfc_comp_smul .., cfc_map_spectrum ..] at this

open scoped Real in
open unitary selfAdjoint in
lemma expUnitary_argSelfAdjoint {u : unitary A} (hu : ‚Äñ(u - 1 : A)‚Äñ < 2) :
    expUnitary (argSelfAdjoint u) = u := by
  ext
  have : ContinuousOn arg (spectrum ‚ÑÇ (u : A)) :=
    continuousOn_arg.mono <| unitary.spectrum_subset_slitPlane_of_norm_lt_two u.2 hu
  rw [expUnitary_coe, argSelfAdjoint_coe, ‚Üê CFC.exp_eq_normedSpace_exp, ‚Üê cfc_comp_smul .., ‚Üê cfc_comp' ..]
  conv_rhs => rw [‚Üê cfc_id' ‚ÑÇ (u : A)]
  refine cfc_congr fun y hy ‚Ü¶ ?_
  have hy‚ÇÅ : ‚Äñy‚Äñ = 1 := spectrum.norm_eq_one_of_unitary u.2 hy
  have : I * y.arg = log y :=
    Complex.ext (by simp [log_re, spectrum.norm_eq_one_of_unitary u.2 hy]) (by simp [log_im])
  simpa [‚Üê exp_eq_exp_‚ÑÇ, this] using exp_log (by aesop)

-- this can be generalized
@[simp]
lemma selfAdjoint.expUnitary_zero : expUnitary (0 : selfAdjoint A) = 1 := by
  ext
  simp

open scoped Real in
lemma unitary.norm_argSelfAdjoint_le_pi (u : unitary A) :
    ‚ÄñargSelfAdjoint u‚Äñ ‚â§ œÄ :=
  norm_cfc_le (by positivity) fun y hy ‚Ü¶ by simpa using abs_arg_le_pi y

lemma unitary.norm_argSelfAdjoint {u : unitary A} (hu : ‚Äñ(u - 1 : A)‚Äñ < 2) :
    ‚Äñ(u - 1 : A)‚Äñ ^ 2 = 2 * (1 - Real.cos ‚ÄñargSelfAdjoint u‚Äñ) := by
  conv_lhs => rw [‚Üê expUnitary_argSelfAdjoint hu]
  exact selfAdjoint.norm_sq_expUnitary_sub_one <| unitary.norm_argSelfAdjoint_le_pi u

lemma unitary.norm_argSelfAdjoint' {u : unitary A} (hu : ‚Äñ(u - 1 : A)‚Äñ < 2) :
    ‚ÄñargSelfAdjoint u‚Äñ = Real.arccos (1 - ‚Äñ(u - 1 : A)‚Äñ ^ 2 / 2) := by
  symm
  apply Real.arccos_eq_of_eq_cos (by positivity) (unitary.norm_argSelfAdjoint_le_pi u)
  linarith [unitary.norm_argSelfAdjoint hu]

open scoped Real in
open NormedSpace Complex unitary selfAdjoint in
lemma unitary.norm_expUnitary_smul_argSelfAdjoint_sub_one_le (u : unitary A)
    {t : ‚Ñù} (ht : t ‚àà Set.Icc 0 1) (hu : ‚Äñ(u - 1 : A)‚Äñ < 2) :
    ‚Äñ(expUnitary (t ‚Ä¢ argSelfAdjoint u) - 1 : A)‚Äñ ‚â§ ‚Äñ(u - 1 : A)‚Äñ := by
  have key : ‚Äñt ‚Ä¢ argSelfAdjoint u‚Äñ ‚â§ ‚ÄñargSelfAdjoint u‚Äñ := by
    rw [‚Üê one_mul ‚ÄñargSelfAdjoint u‚Äñ]
    simp_rw [AddSubgroupClass.coe_norm, val_smul, norm_smul, Real.norm_eq_abs, abs_of_nonneg ht.1]
    gcongr
    exact ht.2
  rw [‚Üê sq_le_sq‚ÇÄ (by positivity) (by positivity)]
  rw [selfAdjoint.norm_sq_expUnitary_sub_one (key.trans <| unitary.norm_argSelfAdjoint_le_pi u)]
  trans 2 * (1 - Real.cos ‚ÄñargSelfAdjoint u‚Äñ)
  ¬∑ gcongr
    exact Real.cos_le_cos_of_nonneg_of_le_pi (by positivity)
      (unitary.norm_argSelfAdjoint_le_pi u) key
  ¬∑ exact (unitary.norm_argSelfAdjoint hu).ge

lemma Metric.nhds_basis_ball_lt {X : Type*} [PseudoMetricSpace X] (x : X) (Œ¥ : ‚Ñù) (hŒ¥ : 0 < Œ¥) :
    (nhds x).HasBasis (fun Œµ ‚Ü¶ 0 < Œµ ‚àß Œµ < Œ¥) (ball x ¬∑) := by
  refine nhds_basis_ball.restrict fun Œµ hŒµ ‚Ü¶
    ‚ü®min Œ¥ Œµ / 2, by positivity, ?_, ball_subset_ball (le_of_lt ?_)‚ü©
  all_goals
    apply (half_lt_self (by positivity)).trans_le
    simp

lemma unitary.norm_sub_eq (u v : unitary A) :
    ‚Äñ(u - v : A)‚Äñ = ‚Äñ((u * star v : unitary A) - 1 : A)‚Äñ := calc
  ‚Äñ(u - v : A)‚Äñ = ‚Äñ(u * star v - 1 : A) * v‚Äñ := by simp [sub_mul, mul_assoc]
  _ = ‚Äñ((u * star v : unitary A) - 1 : A)‚Äñ := by simp

open selfAdjoint unitary in
lemma unitary.expUnitary_eq_mul_inv (u v : unitary A) (huv : ‚Äñ(u - v : A)‚Äñ < 2) :
    expUnitary (argSelfAdjoint (u * star v)) = u * star v :=
  expUnitary_argSelfAdjoint <| unitary.norm_sub_eq u v ‚ñ∏ huv

open scoped Real in
open selfAdjoint Metric in
/-- the maps `unitary.argSelfAdjoint` and `selfAdjoint.expUnitary` form a partial
homeomorphism between `ball (1 : unitary A) 2` and `ball (0 : selfAdjoint A) œÄ`. -/
noncomputable def unitary.partialHomeomorph :
    PartialHomeomorph (unitary A) (selfAdjoint A) where
  toFun := argSelfAdjoint
  invFun := expUnitary
  source := ball 1 2
  target := ball 0 œÄ
  map_source' := sorry
  map_target' := sorry
  left_inv' u hu := expUnitary_argSelfAdjoint <| by
    simpa [Subtype.dist_eq, dist_eq_norm] using hu
  right_inv' x hx := argSelfAdjoint_expUnitary <| by simpa using hx
  open_source := isOpen_ball
  open_target := isOpen_ball
  continuousOn_toFun := sorry
  continuousOn_invFun := sorry

open Real selfAdjoint unitary in
@[simps]
noncomputable
def unitary.pathToOne (u : unitary A) (hu : ‚Äñ(u - 1 : A)‚Äñ < 2) : Path 1 u where
  toFun t := expUnitary ((t : ‚Ñù) ‚Ä¢ argSelfAdjoint u)
  continuous_toFun := by
    simp only [continuous_induced_rng, Function.comp_def, selfAdjoint.expUnitary_coe]
    suffices Continuous fun x : unitInterval ‚Ü¶ cfc (fun z ‚Ü¶ x * arg z) (u : A) by fun_prop
    obtain (h | h) := subsingleton_or_nontrivial A
    ¬∑ convert continuous_const (y := (0 : A))
    refine continuous_cfc_right (hf := ?hf_cont) _ (u : A) ?h_cont
    case hf_cont => exact fun _ ‚Ü¶ ContinuousOn.mono (by fun_prop) (unitary.spectrum_subset_slitPlane_of_norm_lt_two u.2 hu)
    case h_cont =>
      apply UniformOnFun.continuous_of_lipschitzWith (fun _ : Set ‚ÑÇ ‚Ü¶ ‚ü®œÄ, by positivity‚ü©)
      simp only [Set.mem_singleton_iff, UniformOnFun.toFun_ofFun, forall_eq,
        lipschitzWith_iff_dist_le_mul, dist_eq_norm, Subtype.dist_eq, ‚Üê sub_mul,
        ‚Üê Complex.ofReal_sub, norm_mul, Complex.norm_real]
      rintro _ - _ _
      rw [mul_comm]
      gcongr
      exact Complex.abs_arg_le_pi _
  source' := by ext; simp
  target' := by simpa using expUnitary_argSelfAdjoint hu

variable (A) in
open Metric in
lemma unitary.ball_one_isPathConnected (Œ¥ : ‚Ñù) (hŒ¥‚ÇÄ : 0 < Œ¥) (hŒ¥‚ÇÇ : Œ¥ < 2) :
    IsPathConnected (ball (1 : unitary A) Œ¥) := by
  refine ‚ü®1, by simpa, fun {u} hu ‚Ü¶ ?_‚ü©
  have hu : ‚Äñ(u - 1 : A)‚Äñ < Œ¥ := by simpa [Subtype.dist_eq, dist_eq_norm] using hu
  refine ‚ü®pathToOne u (hu.trans hŒ¥‚ÇÇ), fun t ‚Ü¶ ?_‚ü©
  simpa [Subtype.dist_eq, dist_eq_norm] using
    unitary.norm_expUnitary_smul_argSelfAdjoint_sub_one_le u t.2 (hu.trans hŒ¥‚ÇÇ) |>.trans_lt hu

open Metric in
lemma unitary.ball_isPathConnected (u : unitary A) (Œ¥ : ‚Ñù) (hŒ¥‚ÇÄ : 0 < Œ¥) (hŒ¥‚ÇÇ : Œ¥ < 2) :
    IsPathConnected (ball (u : unitary A) Œ¥) := by
  convert unitary.ball_one_isPathConnected A Œ¥ hŒ¥‚ÇÄ hŒ¥‚ÇÇ |>.image (f := (u * ¬∑)) (by fun_prop)
  ext v
  rw [‚Üê inv_mul_cancel u]
  simp [- inv_mul_cancel, Subtype.dist_eq, dist_eq_norm, ‚Üê mul_sub]

open Metric in
instance : LocPathConnectedSpace (unitary A) :=
  .of_bases (nhds_basis_ball_lt ¬∑ 2 zero_lt_two) <| by
    simpa using unitary.ball_isPathConnected

section TopologicalGroup

variable {G : Type*} [TopologicalSpace G]

@[to_additive]
theorem Joined.mul {x y w z : G} [Mul G] [ContinuousMul G] (hxy : Joined x y) (hwz : Joined w z) :
    Joined (x * w) (y * z) := by
  obtain ‚ü®Œ≥‚ÇÅ‚ü© := hxy
  obtain ‚ü®Œ≥‚ÇÇ‚ü© := hwz
  use Œ≥‚ÇÅ.mul Œ≥‚ÇÇ
  all_goals simp

/-- The pointwise inverse of a path. -/
@[to_additive (attr := simps) "The pointwise negation of a path."]
def Path.inv {x y : G} [Inv G] [ContinuousInv G] (Œ≥ : Path x y) :
    Path (x‚Åª¬π) (y‚Åª¬π) where
  toFun := Œ≥‚Åª¬π
  continuous_toFun := map_continuous Œ≥ |>.inv
  source' := congr($(Œ≥.source)‚Åª¬π)
  target' := congr($(Œ≥.target)‚Åª¬π)

@[to_additive]
theorem Joined.inv {x y : G} [Inv G] [ContinuousInv G] (hxy : Joined x y) :
    Joined (x‚Åª¬π) (y‚Åª¬π) := by
  obtain ‚ü®Œ≥‚ü© := hxy
  use Œ≥.inv
  all_goals simp

variable (G)
/-- The path component of the identity in a topological monoid, as a submonoid. -/
@[to_additive (attr := simps)
"The path component of the identity in an additive topological monoid, as an additive submonoid."]
def Submonoid.pathComponentOne [Monoid G] [ContinuousMul G] : Submonoid G where
  carrier := pathComponent (1 : G)
  mul_mem' {g‚ÇÅ g‚ÇÇ} hg‚ÇÅ hg‚ÇÇ := by simpa using hg‚ÇÅ.mul hg‚ÇÇ
  one_mem' := mem_pathComponent_self 1

/-- The path component of the identity in a topological group, as a subgroup. -/
@[to_additive (attr := simps!)
"The path component of the identity in an additive topological group, as an additive subgroup."]
def Subgroup.pathComponentOne [Group G] [IsTopologicalGroup G] : Subgroup G where
  toSubmonoid := .pathComponentOne G
  inv_mem' {g} hg := by simpa using hg.inv

/-- The path component of the identity in a topological group is normal-/
@[to_additive]
instance Subgroup.Normal.pathComponentOne [Group G] [IsTopologicalGroup G] :
    (Subgroup.pathComponentOne G).Normal where
  conj_mem _ := fun ‚ü®Œ≥‚ü© g ‚Ü¶ ‚ü®‚ü®‚ü®(g * Œ≥ ¬∑ * g‚Åª¬π), by fun_prop‚ü©, by simp, by simp‚ü©‚ü©

/-- The path component of the identity in a locally path connected topological group,
as an open normal subgroup. -/
@[to_additive (attr := simps!)
"The path component of the identity in a locally path connected additive topological group,
as an open normal additive subgroup."]
def OpenNormalSubgroup.pathComponentOne [Group G]
    [IsTopologicalGroup G] [LocPathConnectedSpace G] :
    OpenNormalSubgroup (G) where
  toSubgroup := .pathComponentOne G
  isOpen' := .pathComponent 1
  isNormal' := .pathComponentOne G

namespace OpenNormalSubgroup

@[to_additive]
instance [Group G] [IsTopologicalGroup G] [LocPathConnectedSpace G] :
    IsClosed (OpenNormalSubgroup.pathComponentOne G : Set G) :=
  .pathComponent 1

end OpenNormalSubgroup

end TopologicalGroup

-- these instances can be generalized a bit, at least to `CStarRing`
instance {R : Type*} [Monoid R] [StarMul R] [TopologicalSpace R] [ContinuousStar R] :
    ContinuousStar (unitary R) where
  continuous_star := continuous_induced_rng.mpr <| by fun_prop

instance {R : Type*} [Monoid R] [StarMul R] [TopologicalSpace R] [ContinuousStar R] :
    ContinuousInv (unitary R) where
  continuous_inv := by simp_rw [‚Üê unitary.star_eq_inv]; fun_prop

instance : IsTopologicalGroup (unitary A) where

end ExpUnitary

import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Order
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Unitary
import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.ExpLog
import LeanOA.ForMathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.Rpow.Basic
import LeanOA.ForMathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.Abs
import LeanOA.ForMathlib.Algebra.Star.Unitary
import LeanOA.ForMathlib.Data.Complex.Norm
import LeanOA.ForMathlib.Data.Complex.Order
import LeanOA.ForMathlib.Analysis.Complex.Basic
import LeanOA.ForMathlib.Analysis.CStarAlgebra.Exponential
import LeanOA.ForMathlib.Topology.Connected.LocPathConnected
import LeanOA.ForMathlib.Misc
import LeanOA.ContinuousMap.Uniform
import LeanOA.ContinuousFunctionalCalculus.Continuity
import Mathlib.Topology.Algebra.OpenSubgroup
import Mathlib.Analysis.SpecialFunctions.Complex.Circle

/-! # Properties of unitary elements in a Câ‹†-algebra

## Main results

+ `CStarAlgebra.exists_sum_four_unitary`: every element `x` in a unital Câ‹†-algebra is a linear
  combination of four unitary elements, and the norm of each coefficient does not exceed `â€–xâ€– / 2`.
+ `CStarAlgebra.span_unitary`: a unital Câ‹†-algebra is spanned by its unitary elements.

-/

variable {A : Type*} [CStarAlgebra A] [PartialOrder A] [StarOrderedRing A]


section UnitarySpan

open scoped ComplexStarModule
open Complex

/-- If `a : A` is a selfadjoint element in a Câ‹†-algebra with `â€–aâ€– â‰¤ 1`,
then `a + I â€¢ CFC.sqrt (1 - a ^ 2)` is unitary.

This is the key tool to show that a Câ‹†-algebra is spanned by its unitary elements. -/
lemma IsSelfAdjoint.self_add_I_smul_cfcSqrt_sub_sq_mem_unitary (a : A) (ha : IsSelfAdjoint a)
    (ha_norm : â€–aâ€– â‰¤ 1) : a + I â€¢ CFC.sqrt (1 - a ^ 2) âˆˆ unitary A := by
  obtain (_ | _) := subsingleton_or_nontrivial A
  Â· simp [Subsingleton.elim (a + I â€¢ CFC.sqrt (1 - a ^ 2)) 1]
  have key : a + I â€¢ CFC.sqrt (1 - a ^ 2) = cfc (fun x : â„‚ â†¦ x.re + I * âˆš(1 - x.re ^ 2)) a := by
    rw [CFC.sqrt_eq_real_sqrt (1 - a ^ 2) ?nonneg]
    case nonneg =>
      rwa [sub_nonneg, â† CStarAlgebra.norm_le_one_iff_of_nonneg (a ^ 2), ha.sq_norm,
        sq_le_one_iffâ‚€ (by positivity)]
    -- I *really* want this to be solved with a `cfc_pull` tactic. This is a good example of a stress test.
    rw [cfc_add .., cfc_const_mul .., â† cfc_real_eq_complex (fun x â†¦ x) ha, cfc_id' â„ a,
      â† cfc_real_eq_complex (fun x â†¦ âˆš(1 - x ^2)) ha, cfcâ‚™_eq_cfc, cfc_comp' (âˆšÂ·) (1 - Â· ^ 2) a,
      cfc_sub .., cfc_pow .., cfc_const_one .., cfc_id' ..]
  rw [key, cfc_unitary_iff ..]
  intro x hx
  rw [â† starRingEnd_apply, â† Complex.normSq_eq_conj_mul_self,
    Complex.normSq_ofReal_add_I_smul_sqrt_one_sub, Complex.ofReal_one]
  exact spectrum.norm_le_norm_of_mem (ha.spectrumRestricts.apply_mem hx) |>.trans ha_norm

@[simps]
noncomputable def selfAdjoint.unitarySelfAddISMul (a : selfAdjoint A)
    (ha_norm : â€–aâ€– â‰¤ 1) :
    unitary A :=
  âŸ¨(a : A) + I â€¢ CFC.sqrt (1 - a ^ 2 : A), a.2.self_add_I_smul_cfcSqrt_sub_sq_mem_unitary _ ha_normâŸ©

lemma selfAdjoint.star_coe_unitarySelfAddISMul (a : selfAdjoint A) (ha_norm : â€–aâ€– â‰¤ 1) :
    (star (unitarySelfAddISMul a ha_norm) : A) = a - I â€¢ CFC.sqrt (1 - a ^ 2 : A) := by
  simp [a.2, IsSelfAdjoint.star_eq, â† sub_eq_add_neg,
    IsSelfAdjoint.of_nonneg (CFC.sqrt_nonneg (a := (1 - a ^ 2 : A)))]

@[simp high]
lemma selfAdjoint.realPart_unitarySelfAddISMul (a : selfAdjoint A) (ha_norm : â€–aâ€– â‰¤ 1) :
    â„œ (unitarySelfAddISMul a ha_norm : A) = a := by
  simp [IsSelfAdjoint.imaginaryPart (x := CFC.sqrt (1 - a ^ 2 : A)) (by cfc_tac)]

/-- A stepping stone to `CStarAlgebra.exists_sum_four_unitary` that specifies the unitary
elements precisely. The `let`s in the statement are intentional. -/
lemma CStarAlgebra.norm_smul_two_inv_smul_add_four_unitary (x : A) (hx : x â‰  0) :
    let uâ‚ : unitary A := selfAdjoint.unitarySelfAddISMul (â„œ (â€–xâ€–â»Â¹ â€¢ x))
      (by simpa [norm_smul, inv_mul_le_oneâ‚€ (norm_pos_iff.2 hx)] using realPart.norm_le x)
    let uâ‚‚ : unitary A := selfAdjoint.unitarySelfAddISMul (â„‘ (â€–xâ€–â»Â¹ â€¢ x))
      (by simpa [norm_smul, inv_mul_le_oneâ‚€ (norm_pos_iff.2 hx)] using imaginaryPart.norm_le x)
    x = â€–xâ€– â€¢ (2â»Â¹ : â„) â€¢ (uâ‚ + star uâ‚ + I â€¢ (uâ‚‚ + star uâ‚‚) : A) := by
  intro uâ‚ uâ‚‚
  rw [smul_add, smul_comm _ I, unitary.coe_star, unitary.coe_star,
    â† realPart_apply_coe (uâ‚ : A), â† realPart_apply_coe (uâ‚‚ : A)]
  simp only [uâ‚, uâ‚‚, selfAdjoint.realPart_unitarySelfAddISMul,
    realPart_add_I_smul_imaginaryPart, norm_smul_norm_inv_smul]

/-- Every element `x` in a unital Câ‹†-algebra is a linear combination of four unitary elements,
and the norm of each coefficient does not exceed `â€–xâ€– / 2`. -/
lemma CStarAlgebra.exists_sum_four_unitary (x : A) :
    âˆƒ u : Fin 4 â†’ unitary A, âˆƒ c : Fin 4 â†’ â„‚, x = âˆ‘ i, c i â€¢ (u i : A) âˆ§ âˆ€ i, â€–c iâ€– â‰¤ â€–xâ€– / 2 := by
  obtain (rfl | hx) := eq_or_ne x 0
  Â· exact âŸ¨![1, -1, 1, -1], 0, by simpâŸ©
  Â· have := norm_smul_two_inv_smul_add_four_unitary x hx
    extract_lets uâ‚ uâ‚‚ at this
    use ![uâ‚, star uâ‚, uâ‚‚, star uâ‚‚]
    use ![â€–xâ€– * 2â»Â¹, â€–xâ€– * 2â»Â¹, â€–xâ€– * 2â»Â¹ * I, â€–xâ€– * 2â»Â¹ * I]
    constructor
    Â· conv_lhs => rw [this]
      simp [Fin.sum_univ_four, â† Complex.coe_smul]
      module
    Â· intro i
      fin_cases i
      all_goals simp [div_eq_mul_inv]

variable (A) in
open Submodule in
/-- A unital Câ‹†-algebra is spanned by its unitary elements. -/
lemma CStarAlgebra.span_unitary : span â„‚ (unitary A : Set A) = âŠ¤ := by
  rw [eq_top_iff]
  rintro x -
  obtain âŸ¨u, c, rfl, hâŸ© := CStarAlgebra.exists_sum_four_unitary x
  exact sum_mem fun i _ â†¦ Submodule.smul_mem _ _ (subset_span (u i).2)

end UnitarySpan

section ExpUnitary

-- if `â€–u - 1â€– < 1`, then there is a selfadjoint `x` such that `u = exp(I â€¢ x)`.

variable {A : Type*} [CStarAlgebra A]

open Complex


@[aesop safe apply (rule_sets := [CStarAlgebra])] -- this has a bad discr tree key :-(
lemma IsSelfAdjoint.cfc_arg (u : A) : IsSelfAdjoint (cfc (arg Â· : â„‚ â†’ â„‚) u) := by
  simp [isSelfAdjoint_iff, â† cfc_star]

attribute [aesop 10% apply (rule_sets := [CStarAlgebra])] isStarNormal_of_mem_unitary

open Complex in
lemma unitary.two_mul_one_sub_le_norm_sub_one_sq {u : A} (hu : u âˆˆ unitary A)
    {z : â„‚} (hz : z âˆˆ spectrum â„‚ u) :
    2 * (1 - z.re) â‰¤ â€–u - 1â€– ^ 2 := by
  rw [â† Real.sqrt_le_left (by positivity)]
  have := spectrum.subset_circle_of_unitary hu hz
  simp only [mem_sphere_iff_norm, sub_zero] at this
  rw [â† cfc_id' â„‚ u, â† cfc_one â„‚ u, â† cfc_sub ..]
  convert norm_apply_le_norm_cfc (fun z â†¦ z - 1) u hz
  simpa using congr(Real.sqrt $(norm_sub_one_sq_eq_of_norm_one this)).symm

open Complex in
lemma unitary.norm_sub_one_sq_eq {u : A} (hu : u âˆˆ unitary A) {x : â„}
    (hz : IsLeast (re '' (spectrum â„‚ u)) x) :
    â€–u - 1â€– ^ 2 = 2 * (1 - x) := by
  obtain (_ | _) := subsingleton_or_nontrivial A
  Â· exfalso; apply hz.nonempty.of_image.ne_empty; simp
  rw [â† cfc_id' â„‚ u, â† cfc_one â„‚ u, â† cfc_sub ..]
  have h_eqOn : (spectrum â„‚ u).EqOn (fun z â†¦ â€–z - 1â€– ^ 2) (fun z â†¦ 2 * (1 - z.re)) :=
    Complex.norm_sub_one_sq_eqOn_sphere.mono <| spectrum.subset_circle_of_unitary (ğ•œ := â„‚) hu
  have hâ‚‚ : IsGreatest ((fun z â†¦ 2 * (1 - z.re)) '' (spectrum â„‚ u)) (2 * (1 - x)) := by
    have : Antitone (fun y : â„ â†¦ 2 * (1 - y)) := by intro _ _ _; simp only; gcongr
    simpa [Set.image_image] using this.map_isLeast hz
  have hâ‚ƒ : IsGreatest ((â€–Â· - 1â€– ^ 2) '' spectrum â„‚ u) (â€–cfc (Â· - 1 : â„‚ â†’ â„‚) uâ€– ^ 2) := by
    have := pow_left_monotoneOn (n := 2) |>.mono (sâ‚‚ := ((â€–Â· - 1â€–) '' spectrum â„‚ u)) (by aesop)
    simpa [Set.image_image] using this.map_isGreatest (IsGreatest.norm_cfc (fun z : â„‚ â†¦ z - 1) u)
  exact hâ‚ƒ.unique (h_eqOn.image_eq â–¸ hâ‚‚)

-- move to `Analysis.CStarAlgebra.Spectrum`
theorem spectrum.norm_eq_one_of_unitary {ğ•œ : Type*} [NormedField ğ•œ] {E : Type*} [NormedRing E]
    [StarRing E] [CStarRing E] [NormedAlgebra ğ•œ E] [CompleteSpace E] {u : E} (hu : u âˆˆ unitary E)
    â¦ƒz : ğ•œâ¦„ (hz : z âˆˆ spectrum ğ•œ u) : â€–zâ€– = 1 := by
  simpa using spectrum.subset_circle_of_unitary hu hz

lemma unitary.norm_sub_one_lt_two_iff {u : A} (hu : u âˆˆ unitary A) :
    â€–u - 1â€– < 2 â†” -1 âˆ‰ spectrum â„‚ u := by
  nontriviality A
  rw [â† sq_lt_sqâ‚€ (by positivity) (by positivity)]
  constructor
  Â· intro h h1
    have := unitary.two_mul_one_sub_le_norm_sub_one_sq hu h1 |>.trans_lt h
    norm_num at this
  Â· contrapose!
    obtain âŸ¨x, hxâŸ© := spectrum.isCompact (ğ•œ := â„‚) u |>.image continuous_re |>.exists_isLeast <| (spectrum.nonempty _).image _
    rw [unitary.norm_sub_one_sq_eq hu hx]
    obtain âŸ¨z, hz, rflâŸ© := hx.1
    intro key
    replace key : z.re â‰¤ -1 := by linarith
    have hz_norm : â€–zâ€– = 1 := spectrum.norm_eq_one_of_unitary hu hz
    rw [â† hz_norm, Complex.re_le_neg_norm_iff_eq_neg_norm, hz_norm] at key
    exact key â–¸ hz

lemma unitary.spectrum_subset_slitPlane_of_norm_lt_two {u : A} (hu : u âˆˆ unitary A)
    (hu_norm : â€–u - 1â€– < 2) :
    spectrum â„‚ u âŠ† slitPlane:= by
  intro z hz
  rw [mem_slitPlane_iff]
  have hz_norm : â€–zâ€– = 1 := spectrum.norm_eq_one_of_unitary hu hz
  have := sq_eq_one_iff.mpr <| .inl hz_norm
  rw [â† normSq_eq_norm_sq, normSq_apply] at this
  by_cases h : z.im = 0
  Â· simp [h, â† sq] at this
    cases this with
    | inl h => simp [h]
    | inr h =>
      have := hz_norm â–¸ h.le
      rw [Complex.re_le_neg_norm_iff_eq_neg_norm, hz_norm, ofReal_one] at this
      rw [unitary.norm_sub_one_lt_two_iff hu] at hu_norm
      exact False.elim <| hu_norm (this â–¸ hz)
  Â· exact .inr h

/-- The selfadjoint element obtained by taking the argument (using the principal branch and the
continuous functional calculus) of a unitary whose spectrum does not contain `-1`. This returns
`0` if the principal branch of the logarithm is not continuous on the spectrum of the unitary
element. -/
@[simps]
noncomputable def unitary.argSelfAdjoint (u : unitary A) : selfAdjoint A :=
  âŸ¨cfc (arg Â· : â„‚ â†’ â„‚) (u : A), .cfc_arg (u : A)âŸ©

open scoped Real in
lemma selfAdjoint.norm_sq_expUnitary_sub_one {x : selfAdjoint A} (hx : â€–xâ€– â‰¤ Ï€) :
    â€–(expUnitary x - 1 : A)â€– ^ 2 = 2 * (1 - Real.cos â€–xâ€–) := by
  nontriviality A
  apply unitary.norm_sub_one_sq_eq (expUnitary x).2
  simp only [expUnitary_coe, AddSubgroupClass.coe_norm]
  rw [â† CFC.exp_eq_normedSpace_exp, â† cfc_comp_smul I _ (x : A), cfc_map_spectrum .., â† x.2.spectrumRestricts.algebraMap_image]
  simp only [Set.image_image, coe_algebraMap, smul_eq_mul, mul_comm I, â† exp_eq_exp_â„‚, exp_ofReal_mul_I_re]
  refine âŸ¨?_, ?_âŸ©
  Â· cases CStarAlgebra.norm_or_neg_norm_mem_spectrum x.2 with
    | inl h => exact âŸ¨_, h, rflâŸ©
    | inr h => exact âŸ¨_, h, by simpâŸ©
  Â· rintro - âŸ¨y, hy, rflâŸ©
    exact Real.cos_abs y â–¸ Real.cos_le_cos_of_nonneg_of_le_pi (by positivity) hx <| spectrum.norm_le_norm_of_mem hy

open scoped Real in
open unitary selfAdjoint in
lemma argSelfAdjoint_expUnitary {x : selfAdjoint A} (hx : â€–xâ€– < Ï€) :
    argSelfAdjoint (expUnitary x) = x := by
  nontriviality A
  ext
  have : spectrum â„‚ (expUnitary x : A) âŠ† slitPlane := by
    apply unitary.spectrum_subset_slitPlane_of_norm_lt_two (expUnitary x).2
    rw [â† sq_lt_sqâ‚€ (by positivity) (by positivity), norm_sq_expUnitary_sub_one hx.le]
    calc
      2 * (1 - Real.cos â€–xâ€–) < 2 * (1 - Real.cos Ï€) := by
        gcongr
        exact Real.cos_lt_cos_of_nonneg_of_le_pi (by positivity) le_rfl hx
      _ = 2 ^ 2 := by norm_num
  simp only [argSelfAdjoint_coe, expUnitary_coe]
  rw [â† CFC.exp_eq_normedSpace_exp, â† cfc_comp_smul .., â† cfc_comp' (hg := ?_)]
  Â· conv_rhs => rw [â† cfc_id' â„‚ (x : A)]
    refine cfc_congr fun y hy â†¦ ?_
    rw [â† x.2.spectrumRestricts.algebraMap_image] at hy
    obtain âŸ¨y, hy, rflâŸ© := hy
    simp [â† exp_eq_exp_â„‚, exp_ofReal_mul_I_re, mul_comm I, â† ofReal_mul, exp_ofReal_mul_I_re]
    replace hy : â€–yâ€– < Ï€ := spectrum.norm_le_norm_of_mem hy |>.trans_lt hx
    simp only [Real.norm_eq_abs, abs_lt] at hy
    rw [â† Circle.coe_exp, Circle.arg_exp hy.1 hy.2.le]
  refine continuous_ofReal.comp_continuousOn <| continuousOn_arg.mono ?_
  rwa [expUnitary_coe, â† CFC.exp_eq_normedSpace_exp, â† cfc_comp_smul .., cfc_map_spectrum ..] at this

open scoped Real in
open unitary selfAdjoint in
lemma expUnitary_argSelfAdjoint {u : unitary A} (hu : â€–(u - 1 : A)â€– < 2) :
    expUnitary (argSelfAdjoint u) = u := by
  ext
  have : ContinuousOn arg (spectrum â„‚ (u : A)) :=
    continuousOn_arg.mono <| unitary.spectrum_subset_slitPlane_of_norm_lt_two u.2 hu
  rw [expUnitary_coe, argSelfAdjoint_coe, â† CFC.exp_eq_normedSpace_exp, â† cfc_comp_smul .., â† cfc_comp' ..]
  conv_rhs => rw [â† cfc_id' â„‚ (u : A)]
  refine cfc_congr fun y hy â†¦ ?_
  have hyâ‚ : â€–yâ€– = 1 := spectrum.norm_eq_one_of_unitary u.2 hy
  have : I * y.arg = log y :=
    Complex.ext (by simp [log_re, spectrum.norm_eq_one_of_unitary u.2 hy]) (by simp [log_im])
  simpa [â† exp_eq_exp_â„‚, this] using exp_log (by aesop)

open scoped Real in
lemma unitary.norm_argSelfAdjoint_le_pi (u : unitary A) :
    â€–argSelfAdjoint uâ€– â‰¤ Ï€ :=
  norm_cfc_le (by positivity) fun y hy â†¦ by simpa using abs_arg_le_pi y

lemma unitary.norm_argSelfAdjoint {u : unitary A} (hu : â€–(u - 1 : A)â€– < 2) :
    â€–(u - 1 : A)â€– ^ 2 = 2 * (1 - Real.cos â€–argSelfAdjoint uâ€–) := by
  conv_lhs => rw [â† expUnitary_argSelfAdjoint hu]
  exact selfAdjoint.norm_sq_expUnitary_sub_one <| unitary.norm_argSelfAdjoint_le_pi u

lemma unitary.norm_argSelfAdjoint' {u : unitary A} (hu : â€–(u - 1 : A)â€– < 2) :
    â€–argSelfAdjoint uâ€– = Real.arccos (1 - â€–(u - 1 : A)â€– ^ 2 / 2) := by
  symm
  apply Real.arccos_eq_of_eq_cos (by positivity) (unitary.norm_argSelfAdjoint_le_pi u)
  linarith [unitary.norm_argSelfAdjoint hu]

open scoped Real in
open NormedSpace Complex unitary selfAdjoint in
lemma unitary.norm_expUnitary_smul_argSelfAdjoint_sub_one_le (u : unitary A)
    {t : â„} (ht : t âˆˆ Set.Icc 0 1) (hu : â€–(u - 1 : A)â€– < 2) :
    â€–(expUnitary (t â€¢ argSelfAdjoint u) - 1 : A)â€– â‰¤ â€–(u - 1 : A)â€– := by
  have key : â€–t â€¢ argSelfAdjoint uâ€– â‰¤ â€–argSelfAdjoint uâ€– := by
    rw [â† one_mul â€–argSelfAdjoint uâ€–]
    simp_rw [AddSubgroupClass.coe_norm, val_smul, norm_smul, Real.norm_eq_abs, abs_of_nonneg ht.1]
    gcongr
    exact ht.2
  rw [â† sq_le_sqâ‚€ (by positivity) (by positivity)]
  rw [selfAdjoint.norm_sq_expUnitary_sub_one (key.trans <| unitary.norm_argSelfAdjoint_le_pi u)]
  trans 2 * (1 - Real.cos â€–argSelfAdjoint uâ€–)
  Â· gcongr
    exact Real.cos_le_cos_of_nonneg_of_le_pi (by positivity)
      (unitary.norm_argSelfAdjoint_le_pi u) key
  Â· exact (unitary.norm_argSelfAdjoint hu).ge

lemma Metric.nhds_basis_ball_lt {X : Type*} [PseudoMetricSpace X] (x : X) (Î´ : â„) (hÎ´ : 0 < Î´) :
    (nhds x).HasBasis (fun Îµ â†¦ 0 < Îµ âˆ§ Îµ < Î´) (ball x Â·) := by
  refine nhds_basis_ball.restrict fun Îµ hÎµ â†¦
    âŸ¨min Î´ Îµ / 2, by positivity, ?_, ball_subset_ball (le_of_lt ?_)âŸ©
  all_goals
    apply (half_lt_self (by positivity)).trans_le
    simp

lemma unitary.norm_sub_eq (u v : unitary A) :
    â€–(u - v : A)â€– = â€–((u * star v : unitary A) - 1 : A)â€– := calc
  â€–(u - v : A)â€– = â€–(u * star v - 1 : A) * vâ€– := by simp [sub_mul, mul_assoc]
  _ = â€–((u * star v : unitary A) - 1 : A)â€– := by simp

open selfAdjoint unitary in
lemma unitary.expUnitary_eq_mul_inv (u v : unitary A) (huv : â€–(u - v : A)â€– < 2) :
    expUnitary (argSelfAdjoint (u * star v)) = u * star v :=
  expUnitary_argSelfAdjoint <| unitary.norm_sub_eq u v â–¸ huv

open scoped Real in
open selfAdjoint Metric in
/-- the maps `unitary.argSelfAdjoint` and `selfAdjoint.expUnitary` form a partial
homeomorphism between `ball (1 : unitary A) 2` and `ball (0 : selfAdjoint A) Ï€`. -/
@[simps]
noncomputable def unitary.partialHomeomorph :
    PartialHomeomorph (unitary A) (selfAdjoint A) where
  toFun := argSelfAdjoint
  invFun := expUnitary
  source := ball 1 2
  target := ball 0 Ï€
  map_source' u hu := by
    simp only [mem_ball, Subtype.dist_eq, OneMemClass.coe_one, dist_eq_norm, sub_zero] at hu âŠ¢
    rw [unitary.norm_argSelfAdjoint' hu]
    calc
      Real.arccos (1 - â€–(u - 1 : A)â€– ^ 2 / 2) < Real.arccos (1 - 2 ^ 2 / 2) := by
        apply Real.arccos_lt_arccos (by norm_num) (by gcongr)
        linarith [(by positivity : 0 â‰¤ â€–(u - 1 : A)â€– ^ 2 / 2)]
      _ = Ï€ := by norm_num
  map_target' x hx := by
    simp only [mem_ball, Subtype.dist_eq, OneMemClass.coe_one, dist_eq_norm, sub_zero] at hx âŠ¢
    rw [â† sq_lt_sqâ‚€ (by positivity) (by positivity)]
    rw [selfAdjoint.norm_sq_expUnitary_sub_one hx.le]
    have : -1 < Real.cos â€–(x : A)â€– :=
      Real.cos_pi â–¸ Real.cos_lt_cos_of_nonneg_of_le_pi (by positivity) le_rfl hx
    simp [mul_sub, sq]
    linarith
  left_inv' u hu := expUnitary_argSelfAdjoint <| by
    simpa [Subtype.dist_eq, dist_eq_norm] using hu
  right_inv' x hx := argSelfAdjoint_expUnitary <| by simpa using hx
  open_source := isOpen_ball
  open_target := isOpen_ball
  continuousOn_toFun := by
    rw [Topology.IsInducing.subtypeVal.continuousOn_iff]
    simp [Function.comp_def]
    rw [isOpen_ball.continuousOn_iff]
    intro u (hu : dist u 1 < 2)
    obtain âŸ¨Îµ, huÎµ, hÎµ2âŸ© := exists_between (sq_lt_sqâ‚€ (by positivity) (by positivity) |>.mpr hu)
    have hÎµ : 0 < Îµ := lt_of_le_of_lt (by positivity) huÎµ
    have huÎµ' : dist u 1 < âˆšÎµ := Real.lt_sqrt_of_sq_lt huÎµ
    apply ContinuousOn.continuousAt ?_ (Metric.closedBall_mem_nhds_of_mem huÎµ')
    apply ContinuousOn.image_comp_continuous ?_ continuous_subtype_val
    apply continuousOn_cfc (s := sphere 0 1 âˆ© {z | 2 * (1 - z.re) â‰¤ Îµ}) ?_ _ ?_ |>.mono
    Â· rintro - âŸ¨v, hv, rflâŸ©
      simp only [Set.subset_inter_iff, Set.mem_setOf_eq]
      refine âŸ¨inferInstance, spectrum_subset_circle v, ?_âŸ©
      intro z hz
      simp only [Set.mem_setOf_eq]
      trans â€–(v - 1 : A)â€– ^ 2
      Â· exact unitary.two_mul_one_sub_le_norm_sub_one_sq v.2 hz
      Â· refine Real.le_sqrt (by positivity) (by positivity) |>.mp ?_
        simpa [Subtype.dist_eq, dist_eq_norm] using hv
    Â· exact isCompact_sphere 0 1 |>.inter_right <| isClosed_le (by fun_prop) (by fun_prop)
    Â· refine continuous_ofReal.comp_continuousOn <| continuousOn_arg.mono ?_
      apply subset_slitPlane_of_subset_sphere Set.inter_subset_left
      norm_num at hÎµ2 âŠ¢
      exact hÎµ2
  continuousOn_invFun := by fun_prop

open Metric in
@[fun_prop]
lemma unitary.continuousOn_argSelfAdjoint :
    ContinuousOn (argSelfAdjoint : unitary A â†’ selfAdjoint A) (ball (1 : unitary A) 2) :=
  partialHomeomorph.continuousOn_toFun

noncomputable instance : NormedSpace â„ (selfAdjoint A) where
  norm_smul_le := by simp [norm_smul]

open Real selfAdjoint unitary in
@[simps]
noncomputable
def unitary.pathToOne (u : unitary A) (hu : â€–(u - 1 : A)â€– < 2) : Path 1 u where
  toFun t := expUnitary ((t : â„) â€¢ argSelfAdjoint u)
  continuous_toFun := by fun_prop
  source' := by ext; simp
  target' := by simpa using expUnitary_argSelfAdjoint hu

variable (A) in
open Metric in
lemma unitary.ball_one_isPathConnected (Î´ : â„) (hÎ´â‚€ : 0 < Î´) (hÎ´â‚‚ : Î´ < 2) :
    IsPathConnected (ball (1 : unitary A) Î´) := by
  refine âŸ¨1, by simpa, fun {u} hu â†¦ ?_âŸ©
  have hu : â€–(u - 1 : A)â€– < Î´ := by simpa [Subtype.dist_eq, dist_eq_norm] using hu
  refine âŸ¨pathToOne u (hu.trans hÎ´â‚‚), fun t â†¦ ?_âŸ©
  simpa [Subtype.dist_eq, dist_eq_norm] using
    unitary.norm_expUnitary_smul_argSelfAdjoint_sub_one_le u t.2 (hu.trans hÎ´â‚‚) |>.trans_lt hu

open Metric in
lemma unitary.ball_isPathConnected (u : unitary A) (Î´ : â„) (hÎ´â‚€ : 0 < Î´) (hÎ´â‚‚ : Î´ < 2) :
    IsPathConnected (ball (u : unitary A) Î´) := by
  convert unitary.ball_one_isPathConnected A Î´ hÎ´â‚€ hÎ´â‚‚ |>.image (f := (u * Â·)) (by fun_prop)
  ext v
  rw [â† inv_mul_cancel u]
  simp [- inv_mul_cancel, Subtype.dist_eq, dist_eq_norm, â† mul_sub]

open Metric in
instance : LocPathConnectedSpace (unitary A) :=
  .of_bases (nhds_basis_ball_lt Â· 2 zero_lt_two) <| by
    simpa using unitary.ball_isPathConnected

instance {R : Type*} [Monoid R] [StarMul R] [TopologicalSpace R] [ContinuousStar R] :
    ContinuousStar (unitary R) where
  continuous_star := continuous_induced_rng.mpr <| by fun_prop

instance {R : Type*} [Monoid R] [StarMul R] [TopologicalSpace R] [ContinuousStar R] :
    ContinuousInv (unitary R) where
  continuous_inv := by simp_rw [â† unitary.star_eq_inv]; fun_prop

instance : IsTopologicalGroup (unitary A) where

end ExpUnitary

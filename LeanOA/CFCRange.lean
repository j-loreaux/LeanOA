import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.NonUnital
import Mathlib.Order.CompletePartialOrder
import Mathlib.Topology.ContinuousMap.StoneWeierstrass
import LeanOA.ForMathlib.Topology.Algebra.NonUnitalStarAlgebra
import LeanOA.Notation
import LeanOA.TopologicalAlgebra

/-! # range of the continuous functional calculus

This file contains results about the range of the continuous functional calculus, and consequences thereof.
-/

open Topology

open scoped CStarAlgebra

namespace Set

lemma isClosed_centralizer {M : Type*} (s : Set M) [Mul M] [TopologicalSpace M]
    [ContinuousMul M] [T2Space M] : IsClosed (centralizer s) := by
  rw [centralizer, setOf_forall]
  refine isClosed_sInter ?_
  rintro - âŸ¨m, ht, rflâŸ©
  refine isClosed_imp (by simp) <| isClosed_eq ?_ ?_
  all_goals fun_prop

end Set

namespace StarAlgebra

open StarSubalgebra

variable (R : Type*) {A : Type*} [CommSemiring R] [StarRing R] [Semiring A] [Algebra R A]
  [StarRing A] [StarModule R A] [TopologicalSpace A] [TopologicalSemiring A] [ContinuousStar A]
  [T2Space A]

lemma topologicalClosure_adjoin_le_centralizer_centralizer (s : Set A) :
    (adjoin R s).topologicalClosure â‰¤ centralizer R (centralizer R s) :=
  topologicalClosure_minimal (adjoin_le_centralizer_centralizer R s) (Set.isClosed_centralizer _)

lemma elemental.le_centralizer_centralizer (x : A) :
    elemental R x â‰¤ centralizer R (centralizer R {x}) :=
  topologicalClosure_adjoin_le_centralizer_centralizer R {x}

end StarAlgebra

section CFCRangeCommute

variable (ğ•œ : Type*) {A : Type*} {p : A â†’ Prop} [RCLike ğ•œ] [Ring A] [StarRing A] [Algebra ğ•œ A]
variable [TopologicalSpace A] [StarModule ğ•œ A] [ContinuousFunctionalCalculus ğ•œ p]

open StarAlgebra in
lemma ContinuousMap.elemental_eq_top (s : Set ğ•œ) [CompactSpace s] :
    elemental ğ•œ (ContinuousMap.restrict s (.id ğ•œ)) = âŠ¤ := by
  rw [StarAlgebra.elemental, â† polynomialFunctions.starClosure_topologicalClosure,
    polynomialFunctions.starClosure_eq_adjoin_X]
  congr
  exact Polynomial.toContinuousMap_X_eq_id.symm

theorem cfc_range {a : A} (ha : p a) :
    Set.range (cfc (R := ğ•œ) Â· a) = (cfcHom ha (R := ğ•œ)).range := by
  ext
  constructor
  Â· rintro âŸ¨f, rflâŸ©
    by_cases hf : ContinuousOn f (spectrum ğ•œ a)
    Â· simpa only [cfc_apply f a, SetLike.mem_coe] using âŸ¨_, rflâŸ©
    Â· simpa only [cfc_apply_of_not_continuousOn a hf] using zero_mem _
  Â· rintro âŸ¨f, rflâŸ©
    classical
    let f' (x : ğ•œ) : ğ•œ := if hx : x âˆˆ spectrum ğ•œ a then f âŸ¨x, hxâŸ© else 0
    have hff' : f = Set.restrict (spectrum ğ•œ a) f'  := by ext; simp [f']
    have : ContinuousOn f' (spectrum ğ•œ a) :=
      continuousOn_iff_continuous_restrict.mpr (hff' â–¸ map_continuous f)
    use f'
    simp only [cfc_apply f' a]
    congr!
    exact hff'.symm

variable [TopologicalRing A] [ContinuousStar A]

open StarAlgebra

open scoped ContinuousFunctionalCalculus in
theorem cfcHom_range {a : A} (ha : p a) :
    (cfcHom ha (R := ğ•œ)).range = elemental ğ•œ a := by
  rw [StarAlgHom.range_eq_map_top, â† ContinuousMap.elemental_eq_top, StarAlgebra.elemental,
    â† StarSubalgebra.topologicalClosure_map _ _ (cfcHom_isClosedEmbedding ha (R := ğ•œ)),
    StarAlgHom.map_adjoin]
  congr
  simpa using cfcHom_id ha

variable {ğ•œ}

theorem cfcHom_apply_mem_elemental {a : A} (ha : p a) (f : C(spectrum ğ•œ a, ğ•œ)) :
    cfcHom ha f âˆˆ elemental ğ•œ a :=
  cfcHom_range ğ•œ ha â–¸ âŸ¨f, rflâŸ©

variable [T2Space A]

open StarSubalgebra elemental in
theorem commute_cfcHom {a b : A} (ha : p a) (hbâ‚ : Commute a b)
    (hbâ‚‚ : Commute (star a) b) (f : C(spectrum ğ•œ a, ğ•œ)) :
    Commute (cfcHom ha f) b :=
  have hb : b âˆˆ centralizer ğ•œ {a} := by simpa [mem_centralizer_iff] using âŸ¨hbâ‚.eq, hbâ‚‚.eqâŸ©
  le_centralizer_centralizer ğ•œ a (cfcHom_apply_mem_elemental ha f) b (.inl hb) |>.symm

protected theorem IsSelfAdjoint.commute_cfcHom {a b : A} (ha : p a)
    (ha' : IsSelfAdjoint a) (hb : Commute a b) (f : C(spectrum ğ•œ a, ğ•œ)) :
    Commute (cfcHom ha f) b :=
  commute_cfcHom ha hb (ha'.star_eq.symm â–¸ hb) f

theorem commute_cfc {a b : A} (hbâ‚ : Commute a b)
    (hbâ‚‚ : Commute (star a) b) (f : ğ•œ â†’ ğ•œ) :
    Commute (cfc f a) b :=
  cfc_cases (fun x â†¦ Commute x b) a f (Commute.zero_left _)
    fun hf ha â†¦ commute_cfcHom ha hbâ‚ hbâ‚‚ âŸ¨_, hf.restrictâŸ©

protected theorem IsSelfAdjoint.commute_cfc {a b : A}
    (ha : IsSelfAdjoint a) (hbâ‚ : Commute a b) (f : ğ•œ â†’ ğ•œ) :
    Commute (cfc f a) b :=
  commute_cfc hbâ‚ (ha.star_eq.symm â–¸ hbâ‚) f

end CFCRangeCommute

namespace NonUnitalStarAlgebra

open NonUnitalStarSubalgebra

variable (R : Type*) {A : Type*} [CommSemiring R] [StarRing R] [NonUnitalSemiring A]
  [Module R A] [IsScalarTower R A A] [SMulCommClass R A A]
  [StarRing A] [StarModule R A] [TopologicalSpace A] [TopologicalSemiring A] [ContinuousStar A]
  [T2Space A] [ContinuousConstSMul R A]

lemma topologicalClosure_adjoin_le_centralizer_centralizer (s : Set A) :
    (adjoin R s).topologicalClosure â‰¤ centralizer R (centralizer R s) :=
  topologicalClosure_minimal _ (adjoin_le_centralizer_centralizer R s) (Set.isClosed_centralizer _)

lemma elemental.le_centralizer_centralizer (x : A) :
    elemental R x â‰¤ centralizer R (centralizer R {x}) :=
  topologicalClosure_adjoin_le_centralizer_centralizer R {x}

end NonUnitalStarAlgebra

section NonUnital


variable (ğ•œ : Type*) {A : Type*} {p : A â†’ Prop} [RCLike ğ•œ] [NonUnitalRing A] [StarRing A]
variable [Module ğ•œ A] [IsScalarTower ğ•œ A A] [SMulCommClass ğ•œ A A]
variable [TopologicalSpace A] [NonUnitalContinuousFunctionalCalculus ğ•œ p]

theorem cfcâ‚™_range {a : A} (ha : p a) :
    Set.range (cfcâ‚™ (R := ğ•œ) Â· a) = NonUnitalStarAlgHom.range (cfcâ‚™Hom ha (R := ğ•œ)) := by
  ext
  constructor
  Â· rintro âŸ¨f, rflâŸ©
    by_cases hf : ContinuousOn f (quasispectrum ğ•œ a) âˆ§ f 0 = 0
    Â· obtain âŸ¨hf, hfâ‚€âŸ© := hf
      simpa only [cfcâ‚™_apply f a, SetLike.mem_coe] using âŸ¨_, rflâŸ©
    Â· obtain (hf | hfâ‚€) := not_and_or.mp hf
      Â· simpa only [cfcâ‚™_apply_of_not_continuousOn a hf] using zero_mem _
      Â· simpa only [cfcâ‚™_apply_of_not_map_zero a hfâ‚€] using zero_mem _
  Â· rintro âŸ¨f, rflâŸ©
    classical
    let f' (x : ğ•œ) : ğ•œ := if hx : x âˆˆ quasispectrum ğ•œ a then f âŸ¨x, hxâŸ© else 0
    have hff' : f = Set.restrict (quasispectrum ğ•œ a) f'  := by ext; simp [f']
    have : ContinuousOn f' (quasispectrum ğ•œ a) :=
      continuousOn_iff_continuous_restrict.mpr (hff' â–¸ map_continuous f)
    have hf'â‚€ : f' 0 = 0 := by simp only [quasispectrum.zero_mem, â†“reduceDIte, f']; exact map_zero f
    use f'
    simp only [cfcâ‚™_apply f' a]
    congr!
    exact hff'.symm

variable [ContinuousConstSMul ğ•œ A]
variable [StarModule ğ•œ A]
variable [TopologicalRing A] [ContinuousStar A]

open NonUnitalStarAlgebra

-- this can be added after #18615
lemma ContinuousMapZero.elemental_eq_top {ğ•œ : Type*} [RCLike ğ•œ] {s : Set ğ•œ} [Zero s] (h0 : (0 : s) = (0 : ğ•œ))
    [CompactSpace s] : elemental ğ•œ (ContinuousMapZero.id h0) = âŠ¤ :=
  SetLike.ext'_iff.mpr (adjoin_id_dense h0).closure_eq

open scoped NonUnitalContinuousFunctionalCalculus in
theorem cfcâ‚™Hom_range {a : A} (ha : p a) :
    NonUnitalStarAlgHom.range (cfcâ‚™Hom ha (R := ğ•œ)) = elemental ğ•œ a := by
  rw [NonUnitalStarAlgHom.range_eq_map_top, â† ContinuousMapZero.elemental_eq_top rfl, NonUnitalStarAlgebra.elemental,
    â† NonUnitalStarSubalgebra.topologicalClosure_map _ _ (cfcâ‚™Hom_isClosedEmbedding ha (R := ğ•œ)),
    NonUnitalStarAlgHom.map_adjoin]
  congr
  simpa using cfcâ‚™Hom_id ha

variable {ğ•œ}

open scoped ContinuousMapZero

theorem cfcâ‚™Hom_apply_mem_elemental {a : A} (ha : p a) (f : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€) :
    cfcâ‚™Hom ha f âˆˆ elemental ğ•œ a :=
  cfcâ‚™Hom_range ğ•œ ha â–¸ âŸ¨f, rflâŸ©

variable [T2Space A]

open NonUnitalStarSubalgebra elemental in
theorem commute_cfcâ‚™Hom {a b : A} (ha : p a) (hbâ‚ : Commute a b)
    (hbâ‚‚ : Commute (star a) b) (f : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€) :
    Commute (cfcâ‚™Hom ha f) b :=
  have hb : b âˆˆ centralizer ğ•œ {a} := by simpa [mem_centralizer_iff] using âŸ¨hbâ‚.eq, hbâ‚‚.eqâŸ©
  le_centralizer_centralizer ğ•œ a (cfcâ‚™Hom_apply_mem_elemental ha f) b (.inl hb) |>.symm

protected theorem IsSelfAdjoint.commute_cfcâ‚™Hom {a b : A} (ha : p a)
    (ha' : IsSelfAdjoint a) (hb : Commute a b) (f : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€) :
    Commute (cfcâ‚™Hom ha f) b :=
  commute_cfcâ‚™Hom ha hb (ha'.star_eq.symm â–¸ hb) f

theorem commute_cfcâ‚™ {a b : A} (hbâ‚ : Commute a b)
    (hbâ‚‚ : Commute (star a) b) (f : ğ•œ â†’ ğ•œ) :
    Commute (cfcâ‚™ f a) b :=
  cfcâ‚™_cases (fun x â†¦ Commute x b) a f (Commute.zero_left _)
    fun hf hfâ‚€ ha â†¦ commute_cfcâ‚™Hom ha hbâ‚ hbâ‚‚ âŸ¨âŸ¨_, hf.restrictâŸ©, hfâ‚€âŸ©

protected theorem IsSelfAdjoint.commute_cfcâ‚™ {a b : A}
    (ha : IsSelfAdjoint a) (hbâ‚ : Commute a b) (f : ğ•œ â†’ ğ•œ) :
    Commute (cfcâ‚™ f a) b :=
  commute_cfcâ‚™ hbâ‚ (ha.star_eq.symm â–¸ hbâ‚) f

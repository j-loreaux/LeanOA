import Mathlib.Analysis.CStarAlgebra.Classes
import Mathlib.Analysis.Convex.Extreme
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.NonUnital
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Order

open NNReal CStarAlgebra Filter Set Function Metric
open scoped Topology

variable {A : Type*} [NonUnitalCStarAlgebra A] [PartialOrder A] [StarOrderedRing A]

theorem epsilon_compression {Œµ : ‚Ñù‚â•0} (a : A) (ha : 0 ‚â§ a) (f : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0) (hfc : Continuous f)
    (hf0 : f 0 = 0) (hf : Set.EqOn f 1 (Set.Ici Œµ)) (hfl : ‚àÄ x, f x ‚â§ 1) :
    ‚Äña - a * cfc‚Çô f a‚Äñ‚Çä ‚â§ Œµ := by
  have H1 (x : ‚Ñù‚â•0) : x - x * f x ‚â§ Œµ := by
    by_cases! h : x ‚â• Œµ
    ¬∑ simp [hf h]
    ¬∑ have : x - x * (f x) ‚â§ x := by
        nth_rw 1 [‚Üê mul_one x, ‚Üê mul_tsub]
        exact mul_le_of_le_one_right' tsub_le_self
      exact le_trans this h.le
  have H2 (x : ‚Ñù‚â•0) :  x * f x ‚â§ x := by
    by_cases! h : x ‚â• Œµ
    ¬∑ simp [hf h]
    ¬∑ exact mul_le_of_le_one_right' <| coe_le_one.mp (hfl x)
  nth_rw 1 2 [‚Üê cfc‚Çô_id (R := ‚Ñù‚â•0) a]
  rw [‚Üê cfc‚Çô_mul id f, ‚Üê cfc‚Çô_tsub id (ha := ha) (fun _ ‚Ü¶ id _ * f _)]
  ¬∑ refine nnnorm_cfc‚Çô_nnreal_le (A := A) fun x _ ‚Ü¶ H1 (id _)
  ¬∑ exact fun _ _ ‚Ü¶ H2 (id _)

theorem Tendsto_of_epsilon_compression (a : A) (ha : 0 ‚â§ a) (f : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0)
   (hfc : ‚àÄ Œµ > 0, Continuous (f Œµ)) (hf0 : ‚àÄ Œµ > 0, f Œµ 0 = 0)
    (hf : ‚àÄ Œµ > 0, Set.EqOn (f Œµ) 1 (Set.Ici Œµ))
    (hfl : ‚àÄ Œµ > 0, ‚àÄ x, f Œµ x ‚â§ 1) :
    Tendsto (fun (Œµ : ‚Ñù‚â•0) ‚Ü¶ ‚Äña - a * cfc‚Çô (f Œµ) a‚Äñ‚Çä) (ùìù[>] 0) (ùìù 0) := by
  refine (nhdsGT_basis 0).tendsto_iff (Metric.nhds_basis_closedBall) |>.mpr fun Œµ hŒµ ‚Ü¶ ?_
  lift Œµ to ‚Ñù‚â•0 using hŒµ.le
  exact ‚ü®Œµ, hŒµ, fun Œ¥ hŒ¥ ‚Ü¶ by
    simpa using epsilon_compression a ha (f Œ¥) (hfc Œ¥ hŒ¥.1)
      (hf0 Œ¥ hŒ¥.1) (hf Œ¥ hŒ¥.1) (hfl Œ¥  hŒ¥.1) |>.trans hŒ¥.2.le‚ü©

/-- `Œµ ‚Ü¶ x ‚Ü¶ min 1 (1 / Œµ * x)` -/
noncomputable def linearRamp (Œµ x : ‚Ñù‚â•0) := min 1 (1 / Œµ * x)

lemma continuous_linearRamp (Œµ : ‚Ñù‚â•0) : Continuous (linearRamp Œµ) :=
  Continuous.inf (continuous_const) (continuous_mul_left (1 / Œµ))

@[simp]
lemma linearRamp_apply (Œµ : ‚Ñù‚â•0) : linearRamp Œµ = min 1 (1 / Œµ * ¬∑) := rfl

theorem Tendsto_of_linearRamp_compression (a : A) (ha : 0 ‚â§ a) :
    Tendsto (fun (Œµ : ‚Ñù‚â•0) ‚Ü¶ ‚Äña - a * cfc‚Çô (linearRamp Œµ) a‚Äñ‚Çä) (ùìù[>] 0) (ùìù 0) :=
  Tendsto_of_epsilon_compression a ha linearRamp (fun _ ‚Ü¶ by simpa using by fun_prop)
    (by simp) (fun _ h _ ‚Ü¶ by simpa using (one_le_inv_mul‚ÇÄ h).mpr) (by simp)

theorem Tendsto_of_linearRampSq_compression (a : A) (ha : 0 ‚â§ a) :
    Tendsto (fun (Œµ : ‚Ñù‚â•0) ‚Ü¶ ‚Äña - a * cfc‚Çô ((¬∑ ^ 2) ‚àò (linearRamp Œµ)) a‚Äñ‚Çä) (ùìù[>] 0) (ùìù 0) :=
  Tendsto_of_epsilon_compression a ha (fun Œµ ‚Ü¶ (¬∑ ^ 2) ‚àò (linearRamp Œµ))
    (fun _ _ ‚Ü¶ by simpa using by fun_prop) (by simp)
    (fun _ h _ ‚Ü¶ by simpa using (one_le_inv_mul‚ÇÄ h).mpr)
    (fun _ _ _ ‚Ü¶ by simpa using
      (sq_le_one_iff‚ÇÄ <| zero_le (min 1 (_‚Åª¬π * _))).mpr <| min_le_left 1 (_‚Åª¬π * _))

-- move to `Mathlib.Topology.Order.LeftRightNhds` I think?
lemma nhdsGT_basis_Ioc {Œ± : Type*} [TopologicalSpace Œ±] [LinearOrder Œ±] [OrderTopology Œ±]
    [DenselyOrdered Œ±] [NoMaxOrder Œ±] (a : Œ±) :
    (ùìù[>] a).HasBasis (fun x ‚Ü¶ a < x) (Ioc a) := nhdsGT_basis a |>.to_hasBasis'
  (fun _ hac ‚Ü¶
    have ‚ü®b, hab, hbc‚ü© := exists_between hac
    ‚ü®b, hab, Ioc_subset_Ioo_right hbc‚ü©)
  fun _ hac ‚Ü¶ mem_of_superset ((nhdsGT_basis a).mem_of_mem hac) Ioo_subset_Ioc_self

/-- tent function -/
noncomputable def tent (z Œ¥ c x : ‚Ñù‚â•0) : ‚Ñù‚â•0 :=
   c * (1 - (x - z) / ‚ÄñŒ¥‚Äñ‚Çä)

@[simp]
lemma tent_apply {z Œ¥ c : ‚Ñù‚â•0} : tent z Œ¥ c =
  fun x ‚Ü¶ c * (1 - (x - z) / Œ¥) := rfl

lemma tent_le_c (z Œ¥ c x) : tent z Œ¥ c x ‚â§ c := by aesop (add simp [mul_le_of_le_one_right])

theorem continuous_tent (z Œ¥ c) : Continuous (tent z Œ¥ c) :=
  .comp (continuous_const.mul continuous_id) (by fun_prop)

/-- `Œ≥` function from Sakai -/
noncomputable def Œ≥ (Œµ z Œ¥ c : ‚Ñù‚â•0) : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0 :=
  fun x ‚Ü¶ (linearRamp Œµ) x + (tent z Œ¥ c) x

-- move to ...?
lemma two_pow_two {R : Type*} [Semiring R] : (2 : R) ^ 2 = 4 := by norm_num

-- move to `Mathlib.Data.Real.Sqrt`
lemma NNReal.one_lt_inv_sqrt {r : ‚Ñù‚â•0} (hr : 0 < r) (hr1 : r < 1) : 1 < (sqrt r)‚Åª¬π := by
  rw [lt_inv_iff_mul_lt, ‚Üê sq_lt_sq‚ÇÄ] <;> aesop

-- probably inline this unless we need it again? (Jon : I agree. Later, though?)
lemma cutoff {r : ‚Ñù‚â•0} (hr : 0 < r) (hr1 : r < 1) : min 1 (1 / sqrt r - 1) = 1 ‚Üî r ‚â§ 1 / 4 := by
  simp [le_tsub_iff_left (one_lt_inv_sqrt hr hr1).le, le_inv_iff_mul_le (by aesop : sqrt r ‚â† 0),
    ‚Üê sq_le_sq‚ÇÄ (by aesop : 0 ‚â§ 2 * sqrt r), one_add_one_eq_two, mul_pow, two_pow_two, mul_comm]

theorem abstract_approx_add {r x : ‚Ñù‚â•0} (h0r : 0 < r) (hr1 : r < 1)
    (c f : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0) (hcle : ‚àÄ y, c y ‚â§ min 1 (1 / sqrt r - 1)) (hxr : x < r)
    (hfl : ‚àÄ t, f t ‚â§ 1) : x * (f x + c x) ^ 2 ‚â§ 1 := by
  by_cases h : r ‚â§ 1 / 4
  ¬∑ rw [(cutoff h0r hr1).mpr h] at hcle
    refine le_trans (mul_le_mul (le_trans hxr.le h) (?_ : _ ‚â§ (2 : ‚Ñù‚â•0) ^ 2)
      (by positivity) (by positivity)) (by simp [two_pow_two])
    exact pow_le_pow_left' (one_add_one_eq_two (R := ‚Ñù‚â•0) ‚ñ∏ (add_le_add (hfl x) (hcle x))) _
  ¬∑ rw [‚Üê cutoff (by grind) hr1, inf_eq_left, not_le] at h
    simp_rw [min_eq_right_of_lt h] at hcle
    have : x * (f x + c x) ^ 2 ‚â§ x / r := by
      have : f x + c x ‚â§ 1 / sqrt r := by
        refine le_trans (add_le_add (hfl x) (hcle x)) (add_tsub_cancel_of_le (Œ± := ‚Ñù‚â•0) ?_ ‚ñ∏ le_rfl)
        exact one_div (sqrt r) ‚ñ∏ one_lt_inv_sqrt (by grind) (by grind) |>.le
      grw [mul_le_mul_of_nonneg_left (pow_le_pow_left' this 2) (by positivity)]
      simp [div_eq_mul_inv]
    grw [this, div_le_one_of_le‚ÇÄ hxr.le (by positivity)]

theorem abstract_approx_sub {r x : ‚Ñù‚â•0} (h0r : 0 < r) (hr1 : r < 1)
    (c f : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0) (hcle : ‚àÄ y, c y ‚â§ min 1 (1 / sqrt r - 1)) (hxr : x < r)
    (hfl : ‚àÄ t, f t ‚â§ 1) : x * (f x - c x) ^ 2 ‚â§ 1 := by
  refine le_trans ?_ (abstract_approx_add h0r hr1 c f hcle hxr hfl)
  gcongr
  exact le_add_of_le_of_nonneg tsub_le_self (zero_le _)

/- We aim to use abstract_approx_add with Œ¥ = (1 - t) / 2, r = (1 + t) / 2 for the t that is
   the center of the tent function. The minimum below selects the c that keeps the height
   of the tent less than min 1 (1 /sqrt r - 1). -/
/-- other tent function -/
noncomputable def t_tent (t : ‚Ñù‚â•0) := tent t ((1 - t)/2) (min 1 (1 / sqrt ((1 + t) / 2) - 1))

/- Must include a proof that `t_tent` is continuous to ensure cfc‚Çô works. -/

lemma contr_ave {t : ‚Ñù‚â•0} (ht1 : t < 1) : (1 + t) / 2 < 1 :=
  div_lt_one_of_lt <| lt_of_lt_of_eq (add_lt_add_right ht1 _) (one_add_one_eq_two)

lemma pos_ave {t : ‚Ñù‚â•0} (h0t : 0 < t) : 0 < (1 + t)/ 2 := by positivity

lemma t_tent_cap (t : ‚Ñù‚â•0) (x : ‚Ñù‚â•0) :
    t_tent t x ‚â§ (min 1 (1 / sqrt ((1 + t) / 2) - 1)) := by
  dsimp[t_tent]
  simp only [one_div, le_inf_iff]
  exact ‚ü®mul_le_of_le_one_of_le (min_le_left 1 ((sqrt ((1 + t) / 2))‚Åª¬π - 1)) (tsub_le_self),
    (le_trans (mul_le_of_le_one_right' (tsub_le_self))
      (min_le_right 1 ((sqrt ((1 + t) / 2))‚Åª¬π - 1)))‚ü©

lemma linearRamp_cap (Œµ t : ‚Ñù‚â•0) : linearRamp Œµ t ‚â§ 1 := by simp

lemma if_big_t_tent_zero {t x : ‚Ñù‚â•0} (h : ¬¨ (x < (1 + t) / 2)) : t_tent t x = 0 := by
  simp only [not_lt, t_tent, sub_def, coe_one, one_div, NNReal.coe_inv, Real.coe_sqrt,
    NNReal.coe_div, NNReal.coe_add, NNReal.coe_ofNat, Nat.ofNat_nonneg, Real.sqrt_div', inv_div,
    tent_apply,
    Real.coe_toNNReal', mul_eq_zero, Real.toNNReal_eq_zero, tsub_le_iff_right, zero_add] at h ‚ä¢
  -- maybe attribute stuff for `NNReal` with `grind`
  rw [‚Üê NNReal.coe_le_coe, NNReal.coe_div, NNReal.coe_add, NNReal.coe_ofNat, NNReal.coe_one] at h
  by_cases ht : (t : ‚Ñù) < 1
  ¬∑ right
    rw [le_div_iff‚ÇÄ (by simpa), max_eq_left (by simpa using ht.le)]
    grind
  ¬∑ left
    rw [min_eq_right]
    ¬∑ simp only [Real.toNNReal_eq_zero, tsub_le_iff_right, zero_add]
      apply div_le_one_of_le‚ÇÄ (by grind [Real.sqrt_le_sqrt])
      simp
    simp only [Real.toNNReal_le_one, tsub_le_iff_right]
    apply div_le_of_le_mul‚ÇÄ (by simp) (by simp)
    rw [Real.sqrt_le_iff]
    simp only [pos_add_self_iff, zero_lt_one, mul_nonneg_iff_of_pos_left, Real.sqrt_nonneg, mul_pow]
    rw [Real.sq_sqrt (by grind)]
    grind

theorem t_tent_linearRamp_approx_add {t Œµ x : ‚Ñù‚â•0} (h0t : 0 < t) (ht1 : t < 1)
  (hx : x ‚â§ 1) : x * (linearRamp Œµ x + t_tent t x) ^ 2 ‚â§ 1 := by
  by_cases hxt : x < (1 + t) / 2
  ¬∑ exact abstract_approx_add (x := x) (pos_ave h0t) (contr_ave ht1) (t_tent t) (linearRamp Œµ)
      (t_tent_cap t) (hxt) (linearRamp_cap Œµ)
  ¬∑ rw [if_big_t_tent_zero hxt, add_zero, ‚Üê one_pow 2]
    have B1 := (sq_le_sq‚ÇÄ ((zero_le (linearRamp Œµ x))) (zero_le_one)).mpr  <| linearRamp_cap Œµ x
    have B2 := mul_le_mul hx B1 (by positivity) (by positivity)
    rw [one_mul] at B2
    assumption

theorem t_tent_linearRamp_approx_sub {t Œµ x : ‚Ñù‚â•0} (h0t : 0 < t) (ht1 : t < 1)
    (hx : x ‚â§ 1) : x * (linearRamp Œµ x - t_tent t x) ^ 2 ‚â§ 1 := by
  refine le_trans ?_ (t_tent_linearRamp_approx_add (Œµ := Œµ) h0t ht1 hx)
  gcongr
  exact le_add_of_le_of_nonneg tsub_le_self (zero_le _)

theorem continuous_t_tent (t : ‚Ñù‚â•0) : Continuous (t_tent t) :=
  continuous_tent t ((1 - t)/2) (min 1 (1 / sqrt ((1 + t) / 2) - 1))

theorem continuous_approx_add {Œµ t : ‚Ñù‚â•0} :
  Continuous fun (x : ‚Ñù‚â•0) ‚Ü¶ x * (linearRamp Œµ x + t_tent t x) ^ 2 :=
  Continuous.mul (continuous_id) (Continuous.pow (Continuous.add
    (continuous_linearRamp Œµ) (continuous_t_tent t)) 2)

theorem continuous_approx_sub {Œµ t : ‚Ñù‚â•0} :
  Continuous fun (x : ‚Ñù‚â•0) ‚Ü¶ x * (linearRamp Œµ x - t_tent t x) ^ 2 :=
  Continuous.mul (continuous_id) (Continuous.pow (Continuous.sub
    (continuous_linearRamp Œµ) (continuous_t_tent t)) 2)


theorem quasispectrum_le_one (a : A) (ha : 0 ‚â§ a) (ha1 : ‚Äña‚Äñ‚Çä ‚â§ 1) (t : ‚Ñù‚â•0) :
    t ‚àà quasispectrum ‚Ñù‚â•0 a ‚Üí t ‚â§ 1 := by
 have B := (nnnorm_cfc‚Çô_nnreal_le_iff id a 1).mp
 have C := cfc‚Çô_id (R := ‚Ñù‚â•0) (A := A) (ha := ha)
 simp only [C, id_eq] at B
 have F := B ha1
 intro h
 exact F t h

theorem norm_cfc‚Çô_approx_add {Œµ t : ‚Ñù‚â•0} (a : A) (ha : 0 ‚â§ a) (ha1 : ‚Äña‚Äñ‚Çä ‚â§ 1) (h0t : 0 < t)
    (ht1 : t < 1) : ‚Äñcfc‚Çô (fun (x : ‚Ñù‚â•0) ‚Ü¶ x * (linearRamp Œµ x + t_tent t x) ^ 2) a‚Äñ‚Çä ‚â§ 1 := by
  refine nnnorm_cfc‚Çô_nnreal_le (A := A) ?_
  intro x hx
  have hx1 : x ‚â§ 1 := quasispectrum_le_one a ha ha1 x hx
  exact t_tent_linearRamp_approx_add (x := x) (Œµ := Œµ) (t := t) h0t ht1 hx1

theorem norm_cfc‚Çô_approx_sub {Œµ t : ‚Ñù‚â•0} (a : A) (ha : 0 ‚â§ a) (ha1 : ‚Äña‚Äñ‚Çä ‚â§ 1) (h0t : 0 < t)
    (ht1 : t < 1) : ‚Äñcfc‚Çô (fun (x : ‚Ñù‚â•0) ‚Ü¶ x * (linearRamp Œµ x - t_tent t x) ^ 2) a‚Äñ‚Çä ‚â§ 1 := by
  refine nnnorm_cfc‚Çô_nnreal_le (A := A) ?_
  intro x hx
  have hx1 : x ‚â§ 1 := quasispectrum_le_one a ha ha1 x hx
  exact t_tent_linearRamp_approx_sub (x := x) (Œµ := Œµ) (t := t) h0t ht1 hx1

/- To do:

 * Use cfc‚Çô and the CStar identity to get from the above that
   `‚Äña Œ≥Œµ‚Äñ ‚â§ 1` and `‚Äña sŒµ‚Äñ ‚â§ 1` with `Œ≥Œµ` and `sŒµ`
   the cfc‚Çô images of the functions `linearRamp Œµ x + t_tent t x` and
   `linearRamp Œµ x - t_tent t x`. (These were `Œ≥‚Çô` and `s‚Çô` in Sakai.)

 -/

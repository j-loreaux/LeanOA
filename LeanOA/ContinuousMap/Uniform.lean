import Mathlib.Topology.MetricSpace.Lipschitz
import Mathlib.Topology.UniformSpace.CompactConvergence

open Filter Topology
open scoped UniformConvergence NNReal

section ContinuousMap

variable {X Î± Î² : Type*} [TopologicalSpace X] [TopologicalSpace Î±] [UniformSpace Î²]

open UniformOnFun in
/-- `f : X â†’ C(Î±, Î²)` is continuous if any only if it is continuous when reinterpreted as a
map `f : X â†’ Î± â†’áµ¤[{K | IsCompact K}] Î²`. -/
theorem ContinuousMap.continuous_iff_continuous_uniformOnFun (f : X â†’ C(Î±, Î²)) :
    Continuous f â†” Continuous (fun x â†¦ ofFun {K | IsCompact K} (f x)) :=
  isUniformEmbedding_toUniformOnFunIsCompact.isInducing.continuous_iff

open UniformFun in
/-- When `Î±` is compact, `f : X â†’ C(Î±, Î²)` is continuous if any only if it is continuous when
reinterpreted as a map `f : X â†’ Î± â†’áµ¤ Î²`. -/
theorem ContinuousMap.continuous_iff_continuous_uniformFun (f : X â†’ C(Î±, Î²)) [CompactSpace Î±] :
    Continuous f â†” Continuous (fun x â†¦ ofFun (f x)) := by
  rw [continuous_iff_continuous_uniformOnFun]
  exact UniformOnFun.uniformEquivUniformFun Î² _ isCompact_univ
    |>.isUniformInducing.isInducing.continuous_iff

/-- Given functions `F i, f` which are continuous on a compact set `s`, `F` tends to `f`
uniformly on `s` if and only if the restrictions (as elements of `C(s, Î²)`) converge. -/
theorem ContinuousOn.tendsto_restrict_iff_tendstoUniformlyOn {s : Set Î±} [CompactSpace s]
    {f : Î± â†’ Î²} (hf : ContinuousOn f s) {Î¹ : Type*} {p : Filter Î¹}
    {F : Î¹ â†’ Î± â†’ Î²} (hF : âˆ€ i, ContinuousOn (F i) s) :
    Tendsto (fun i â†¦ âŸ¨_, (hF i).restrictâŸ© : Î¹ â†’ C(s, Î²)) p (ğ“ âŸ¨_, hf.restrictâŸ©) â†”
      TendstoUniformlyOn F f p s := by
  rw [ContinuousMap.tendsto_iff_tendstoUniformly, tendstoUniformlyOn_iff_tendstoUniformly_comp_coe]
  congr!

open UniformOnFun in
/-- A family `f : X â†’ Î± â†’ Î²`, each of which is continuous on a compact set `s : Set Î±` is
continuous in the topology `X â†’ Î± â†’áµ¤[{s}] Î²` if and only if the family of continuous restrictions
`X â†’ C(s, Î²)` is continuous. -/
theorem ContinuousOn.continuous_restrict_iff_continuous_uniformOnFun
    {f : X â†’ Î± â†’ Î²} {s : Set Î±} (hf : âˆ€ x, ContinuousOn (f x) s) [CompactSpace s] :
    Continuous (fun x â†¦ âŸ¨_, (hf x).restrictâŸ© : X â†’ C(s, Î²)) â†”
      Continuous (fun x â†¦ ofFun {s} (f x)) := by
  rw [ContinuousMap.continuous_iff_continuous_uniformFun, UniformOnFun.continuous_rng_iff]
  simp [Function.comp_def]

end ContinuousMap

section TendstoUniformlyBasis

namespace Filter.HasBasis

variable {X Î¹ Î¹X Î¹Î± Î¹Î² Î± Î² : Type*} {ğ”– : Set (Set Î±)} [UniformSpace Î²]

open UniformFun

/-- An anologue of `Filter.Tendsto.tendsto_right_iff` for `TendstoUniformlyOnFilter`. -/
lemma uniformity_tendstoUniformlyOnFilter_iff {F : X â†’ Î± â†’ Î²} {f : Î± â†’ Î²}
    {l : Filter X} {l' : Filter Î±} {pÎ² : Î¹Î² â†’ Prop} {sÎ² : Î¹Î² â†’ Set (Î² Ã— Î²)}
    (hÎ² : (uniformity Î²).HasBasis pÎ² sÎ²) :
    TendstoUniformlyOnFilter F f l l' â†”
      (âˆ€ i, pÎ² i â†’ âˆ€á¶  n in l Ã—Ë¢ l', (f n.2, F n.1 n.2) âˆˆ sÎ² i) := by
  rw [TendstoUniformlyOnFilter, hÎ².forall_iff]
  exact fun _ _ _ h â†¦ h.mp <| .of_forall <| by aesop

/-- An anologue of `Filter.Tendsto.tendsto_right_iff` for `TendstoUniformlyOn`. -/
lemma tendstoUniformlyOnFilter_iff {F : X â†’ Î± â†’ Î²} {f : Î± â†’ Î²}
    {l : Filter X} {l' : Filter Î±} {pX : Î¹X â†’ Prop} {sX : Î¹X â†’ Set X}
    {pÎ± : Î¹Î± â†’ Prop} {sÎ± : Î¹Î± â†’ Set Î±} {pÎ² : Î¹Î² â†’ Prop} {sÎ² : Î¹Î² â†’ Set (Î² Ã— Î²)}
    (hl : l.HasBasis pX sX) (hl' : l'.HasBasis pÎ± sÎ±)
    (hÎ² : (uniformity Î²).HasBasis pÎ² sÎ²) :
    TendstoUniformlyOnFilter F f l l' â†”
      (âˆ€ i, pÎ² i â†’ âˆƒ j k, (pX j âˆ§ pÎ± k) âˆ§ âˆ€ x a, x âˆˆ sX j â†’ a âˆˆ sÎ± k â†’ (f a, F x a) âˆˆ sÎ² i) := by
  simp [hÎ².uniformity_tendstoUniformlyOnFilter_iff, (hl.prod hl').eventually_iff]

/-- An anologue of `Filter.Tendsto.tendsto_right_iff` for `TendstoUniformly`. -/
lemma uniformity_tendstoUniformlyOn_iff {F : X â†’ Î± â†’ Î²} {f : Î± â†’ Î²}
    {l : Filter X} {s : Set Î±} {pÎ² : Î¹Î² â†’ Prop} {sÎ² : Î¹Î² â†’ Set (Î² Ã— Î²)}
    (hÎ² : (uniformity Î²).HasBasis pÎ² sÎ²) :
    TendstoUniformlyOn F f l s â†”
      (âˆ€ i, pÎ² i â†’ âˆ€á¶  n in l, âˆ€ x âˆˆ s, (f x, F n x) âˆˆ sÎ² i) := by
  rw [TendstoUniformlyOn, hÎ².forall_iff]
  exact fun _ _ _ h â†¦ h.mp <| .of_forall <| by aesop

/-- An anologue of `Filter.Tendsto.tendsto_iff` for `TendstoUniformlyOnFilter`. -/
lemma tendstoUniformlyOn_iff {F : X â†’ Î± â†’ Î²} {f : Î± â†’ Î²}
    {l : Filter X} {s : Set Î±} {pX : Î¹X â†’ Prop} {sX : Î¹X â†’ Set X} {pÎ² : Î¹Î² â†’ Prop}
    {sÎ² : Î¹Î² â†’ Set (Î² Ã— Î²)} (hl : l.HasBasis pX sX) (hÎ² : (uniformity Î²).HasBasis pÎ² sÎ²) :
    TendstoUniformlyOn F f l s â†”
      (âˆ€ i, pÎ² i â†’ âˆƒ j, pX j âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ sX j â†’ âˆ€ a âˆˆ s, (f a, F x a) âˆˆ sÎ² i) := by
  simp [hÎ².uniformity_tendstoUniformlyOn_iff, hl.eventually_iff]

/-- An anologue of `Filter.Tendsto.tendsto_iff` for `TendstoUniformlyOn`. -/
lemma uniformity_tendstoUniformly_iff {F : X â†’ Î± â†’ Î²} {f : Î± â†’ Î²}
    {l : Filter X} {pÎ² : Î¹Î² â†’ Prop} {sÎ² : Î¹Î² â†’ Set (Î² Ã— Î²)}
    (hÎ² : (uniformity Î²).HasBasis pÎ² sÎ²) :
    TendstoUniformly F f l â†”
      (âˆ€ i, pÎ² i â†’ âˆ€á¶  n in l, âˆ€ x, (f x, F n x) âˆˆ sÎ² i) := by
  rw [TendstoUniformly, hÎ².forall_iff]
  exact fun _ _ _ h â†¦ h.mp <| .of_forall <| by aesop

/-- An anologue of `Filter.Tendsto.tendsto_iff` for `TendstoUniformly`. -/
lemma tendstoUniformly_iff {F : X â†’ Î± â†’ Î²} {f : Î± â†’ Î²}
    {l : Filter X} {pX : Î¹X â†’ Prop} {sX : Î¹X â†’ Set X} (hl : l.HasBasis pX sX)
    {pÎ² : Î¹Î² â†’ Prop} {sÎ² : Î¹Î² â†’ Set (Î² Ã— Î²)} (hÎ² : (uniformity Î²).HasBasis pÎ² sÎ²) :
    TendstoUniformly F f l â†”
      (âˆ€ i, pÎ² i â†’ âˆƒ j, pX j âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ sX j â†’ âˆ€ a, (f a, F x a) âˆˆ sÎ² i) := by
  simp only [hÎ².uniformity_tendstoUniformly_iff, hl.eventually_iff]

end Filter.HasBasis

end TendstoUniformlyBasis

section Lipschitz

variable {X Î± Î² : Type*} {ğ”– : Set (Set Î±)} [PseudoMetricSpace X] [PseudoMetricSpace Î²]

/-- If `f : X â†’ Î± â†’áµ¤[ğ”–] Î²` is Lipschitz for each fixed `a âˆˆ s âˆˆ ğ”–`, with Lipschitz
constant depending on `s`, then `f` is continuous. -/
lemma UniformOnFun.continuous_of_lipschitzWith {f : X â†’ Î± â†’áµ¤[ğ”–] Î²}
    (C : Set Î± â†’ â„â‰¥0) (hf : âˆ€ s âˆˆ ğ”–, âˆ€ a âˆˆ s, LipschitzWith (C s) (fun x â†¦ toFun _ (f x) a)) :
    Continuous f := by
  simp_rw [continuous_iff_continuousAt, ContinuousAt, UniformOnFun.tendsto_iff_tendstoUniformlyOn,
    Metric.nhds_basis_closedBall.tendstoUniformlyOn_iff Metric.uniformity_basis_dist_le]
  refine fun xâ‚€ s hs Îµ hÎµ â†¦ âŸ¨Îµ / (C s + 1), by positivity, fun x hx a ha â†¦ ?_âŸ©
  simp only [Metric.mem_closedBall, dist_comm x, Function.comp_apply, Set.mem_setOf_eq] at hx âŠ¢
  calc
    _ â‰¤ C s * dist xâ‚€ x := (hf s hs a ha).dist_le_mul _ _
    _ â‰¤ C s * (Îµ / (C s + 1)) := by gcongr
    _ â‰¤ Îµ := by
      rw [â† mul_div_assoc, div_le_iffâ‚€' (by positivity)]
      gcongr
      simp

/-- If `f : X â†’ Î± â†’áµ¤ Î²` is Lipschitz for each fixed `a âˆˆ s âˆˆ ğ”–`, with Lipschitz
constant depending on `s`, then `f` is continuous. -/
lemma UniformFun.continuous_of_lipschitzWith {f : X â†’ Î± â†’áµ¤ Î²} (C : â„â‰¥0)
    (hf : âˆ€ a, LipschitzWith C (fun x â†¦ toFun (f x) a)) :
    Continuous f := by
  let e := UniformOnFun.uniformEquivUniformFun (Î± := Î±) (ğ”– := {Set.univ}) Î² (by simp)
  rw [e.symm.isUniformInducing.isInducing.continuous_iff]
  apply UniformOnFun.continuous_of_lipschitzWith (fun _ â†¦ C)
  rintro - - a -
  exact hf a

end Lipschitz

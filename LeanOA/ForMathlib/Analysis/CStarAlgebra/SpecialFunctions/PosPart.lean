/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import LeanOA.ForMathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.PosPart
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Isometric

/-! # C‚ãÜ-algebraic facts about `a‚Å∫` and `a‚Åª`.

[PR #18056](https://github.com/leanprover-community/mathlib4/pull/18506) upstreams the material about `span` to Mathlib.
-/

variable {A : Type*}

namespace CStarAlgebra

section Norm

variable [NonUnitalNormedRing A] [NormedSpace ‚Ñù A] [IsScalarTower ‚Ñù A A]
variable [SMulCommClass ‚Ñù A A] [RegularNormedAlgebra ‚Ñù A] [StarRing A] [CompleteSpace A]
variable [NonUnitalIsometricContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint]

@[simp]
lemma norm_posPart_le (a : A) : ‚Äña‚Å∫‚Äñ ‚â§ ‚Äña‚Äñ := by
  refine norm_cfc‚Çô_le fun x hx ‚Ü¶ ?_
  obtain (h | h) := le_or_lt x 0
  ¬∑ simp [_root_.posPart_def, max_eq_right h]
  ¬∑ simp only [_root_.posPart_def, max_eq_left h.le]
    rw [Unitization.quasispectrum_eq_spectrum_inr] at hx
    rw [‚Üê Unitization.norm_inr (ùïú := ‚Ñù) a]
    exact spectrum.norm_le_norm_of_mem hx

@[simp]
lemma norm_negPart_le (a : A) : ‚Äña‚Åª‚Äñ ‚â§ ‚Äña‚Äñ := by
  simpa [CFC.negPart_neg] using norm_posPart_le (-a)

lemma _root_.IsSelfAdjoint.norm_eq_max_norm_posPart_negPart (a : A) (ha : IsSelfAdjoint a := by cfc_tac) :
    ‚Äña‚Äñ = max ‚Äña‚Å∫‚Äñ ‚Äña‚Åª‚Äñ := by
  refine le_antisymm ?_ <| max_le (norm_posPart_le a) (norm_negPart_le a)
  conv_lhs => rw [‚Üê cfc‚Çô_id' ‚Ñù a]
  rw [norm_cfc‚Çô_le_iff ..]
  intro x hx
  obtain (hx' | hx') := le_total 0 x
  ¬∑ apply le_max_of_le_left
    refine le_of_eq_of_le ?_ <| norm_apply_le_norm_cfc‚Çô _ a hx
    rw [posPart_eq_self.mpr hx']
  ¬∑ apply le_max_of_le_right
    refine le_of_eq_of_le ?_ <| norm_apply_le_norm_cfc‚Çô _ a hx
    rw [negPart_eq_neg.mpr hx', norm_neg]

end Norm

section SpanNonneg

variable [NonUnitalCStarAlgebra A] [PartialOrder A] [StarOrderedRing A]

open Submodule

/-- A C‚ãÜ-algebra is spanned by nonnegative elements of norm at most `r` -/
lemma span_nonneg_inter_closedBall {r : ‚Ñù} (hr : 0 < r) :
    span ‚ÑÇ ({x : A | 0 ‚â§ x} ‚à© Metric.closedBall 0 r) = ‚ä§ := by
  rw [eq_top_iff, ‚Üê span_nonneg, span_le]
  intro x hx
  obtain (rfl | hx_pos) := eq_zero_or_norm_pos x
  ¬∑ exact zero_mem _
  ¬∑ suffices (r * ‚Äñx‚Äñ‚Åª¬π : ‚ÑÇ)‚Åª¬π ‚Ä¢ ((r * ‚Äñx‚Äñ‚Åª¬π : ‚ÑÇ) ‚Ä¢ x) = x by
      rw [‚Üê this]
      refine smul_mem _ _ (subset_span <| Set.mem_inter ?_ ?_)
      ¬∑ norm_cast
        exact smul_nonneg (by positivity) hx
      ¬∑ simp [mul_smul, norm_smul, abs_of_pos hr, inv_mul_cancel‚ÇÄ hx_pos.ne']
    apply inv_smul_smul‚ÇÄ
    norm_cast
    positivity

/-- A C‚ãÜ-algebra is spanned by nonnegative elements of norm less than `r`. -/
lemma span_nonneg_inter_ball {r : ‚Ñù} (hr : 0 < r) :
    span ‚ÑÇ ({x : A | 0 ‚â§ x} ‚à© Metric.ball 0 r) = ‚ä§ := by
  rw [eq_top_iff, ‚Üê span_nonneg_inter_closedBall (half_pos hr)]
  gcongr
  exact Metric.closedBall_subset_ball <| half_lt_self hr

/-- A C‚ãÜ-algebra is spanned by nonnegative contractions. -/
lemma span_nonneg_inter_unitClosedBall :
    span ‚ÑÇ ({x : A | 0 ‚â§ x} ‚à© Metric.closedBall 0 1) = ‚ä§ :=
  span_nonneg_inter_closedBall zero_lt_one

/-- A C‚ãÜ-algebra is spanned by nonnegative strict contractions. -/
lemma span_nonneg_inter_unitBall :
    span ‚ÑÇ ({x : A | 0 ‚â§ x} ‚à© Metric.ball 0 1) = ‚ä§ :=
  span_nonneg_inter_ball zero_lt_one

end SpanNonneg

end CStarAlgebra

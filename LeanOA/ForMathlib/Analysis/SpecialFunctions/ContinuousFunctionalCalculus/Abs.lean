/-
Copyright (c) 2024 Jon Bannon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jon Bannon, Jireh Loreaux
-/

import LeanOA.ForMathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.Rpow.Basic
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Instances
import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.Rpow.Basic
import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.PosPart.Basic

/-!
# Absolute value of an operator defined via the continuous functional calculus

This file defines the absolute value via the non-unital continuous functional calculus
and includes foundational API.

## Main declarations

+ `CFC.abs`: The absolute value declaration as `abs a := sqrt (star a) * a`.

-/

open scoped NNReal

namespace CFC

section Generic

variable {A : Type*}

section NonUnital

section Real

variable [NonUnitalRing A] [StarRing A] [TopologicalSpace A]
variable [PartialOrder A] [StarOrderedRing A] [Module â„ A] [SMulCommClass â„ A A] [IsScalarTower â„ A A]

variable [NonUnitalContinuousFunctionalCalculus â„ A IsSelfAdjoint]
variable [NonnegSpectrumClass â„ A]

/-- The absolute value of an operator, using the nonunital continuous functional calculus. -/
noncomputable def abs (a : A) := sqrt (star a * a)

@[simp]
lemma abs_neg (a : A) : abs (-a) = abs a := by
  simp [abs]

@[simp]
lemma abs_nonneg {a : A} : 0 â‰¤ abs a := sqrt_nonneg

lemma abs_star (a : A) (ha : IsStarNormal a := by cfc_tac) : abs (star a) = abs a := by
  simp [abs, star_comm_self']

@[simp]
lemma abs_zero : abs (0 : A) = 0 := by
  simp [abs]

variable [IsTopologicalRing A] [T2Space A]

lemma abs_mul_abs_self (a : A) : abs a * abs a = star a * a :=
  sqrt_mul_sqrt_self _ <| star_mul_self_nonneg _

lemma abs_nnrpow_two (a : A) : abs a ^ (2 : â„â‰¥0) = star a * a := by
  simp only [abs_nonneg, nnrpow_two]
  apply abs_mul_abs_self

lemma abs_nnrpow_two_mul (a : A) (x : â„â‰¥0) :
    abs a ^ (2 * x) = (star a * a) ^ x := by rw [â† nnrpow_nnrpow, abs_nnrpow_two]

lemma abs_nnrpow (a : A) (x : â„â‰¥0) :
    abs a ^ x = (star a * a) ^ (x / 2) := by
  simp only [â† abs_nnrpow_two_mul, mul_div_left_comm, ne_eq, OfNat.ofNat_ne_zero,
    not_false_eq_true, div_self, mul_one]

lemma abs_of_nonneg (a : A) (ha : 0 â‰¤ a := by cfc_tac) : abs a = a := by
  rw [abs, ha.star_eq, sqrt_mul_self a ha]

lemma abs_of_nonpos (a : A) (ha : a â‰¤ 0 := by cfc_tac) : abs a = -a := by
  simpa using abs_of_nonneg (-a)

lemma abs_eq_norm (a : A) (ha : IsSelfAdjoint a := by cfc_tac) :
    abs a = cfcâ‚™ (â€–Â·â€–) a := by
  conv_lhs => rw [abs, ha.star_eq, sqrt_eq_real_sqrt .., â† cfcâ‚™_id' â„ a, â† cfcâ‚™_mul .., â† cfcâ‚™_comp' ..]
  simp [â† sq, Real.sqrt_sq_eq_abs]

protected lemma posPart_add_negPart (a : A) (ha : IsSelfAdjoint a := by cfc_tac) : abs a = aâº + aâ» := by
  rw [CFC.posPart_def, CFC.negPart_def, â† cfcâ‚™_add .., abs_eq_norm a ha]
  exact cfcâ‚™_congr fun x hx â†¦ (posPart_add_negPart x).symm

lemma abs_sub_self (a : A) (ha : IsSelfAdjoint a := by cfc_tac) : abs a - a = 2 â€¢ aâ» := by
 simpa [two_smul] using
    congr($(CFC.posPart_add_negPart a) - $((CFC.posPart_sub_negPart a).symm))

lemma abs_add_self (a : A) (ha : IsSelfAdjoint a := by cfc_tac) : abs a + a = 2 â€¢ aâº := by
  simpa [two_smul] using
    congr($(CFC.posPart_add_negPart a) + $((CFC.posPart_sub_negPart a).symm))

@[simp]
lemma abs_abs (a : A) : abs (abs a) = abs a :=
  abs_of_nonneg ..

variable [StarModule â„ A]

@[simp]
lemma abs_smul_nonneg {R : Type*} [Semiring R] [SMulWithZero R â„â‰¥0] [SMul R A]
    [IsScalarTower R â„â‰¥0 A] (r : R) (a : A) :
    abs (r â€¢ a) = r â€¢ abs a := by
  suffices âˆ€ r : â„â‰¥0, abs (r â€¢ a) = r â€¢ abs a by simpa using this (r â€¢ 1)
  intro r
  rw [abs, sqrt_eq_iff _ _ (star_mul_self_nonneg _) (smul_nonneg (by positivity) abs_nonneg)]
  simp [mul_smul_comm, smul_mul_assoc, abs_mul_abs_self]

@[simp]
lemma abs_smul (r : â„) (a : A) : abs (r â€¢ a) = |r| â€¢ abs a := by
  cases r using Real.nnreal_induction_on
  all_goals simp [â† NNReal.smul_def]

end Real

section RCLike

variable {ğ•œ A : Type*} {p : A â†’ Prop} [RCLike ğ•œ]
variable [NonUnitalRing A] [TopologicalSpace A] [Module ğ•œ A]
variable [StarRing A] [PartialOrder A] [StarOrderedRing A]
variable [IsScalarTower ğ•œ A A] [SMulCommClass ğ•œ A A]
variable [NonUnitalContinuousFunctionalCalculus ğ•œ A p]

open ComplexOrder

lemma cfcâ‚™_norm_sq_nonneg {f : ğ•œ â†’ ğ•œ} {a : A} : 0 â‰¤ cfcâ‚™ (fun z â†¦ star (f z) * (f z)) a :=
  cfcâ‚™_nonneg fun _ _ â†¦ star_mul_self_nonneg _

lemma cfcâ‚™_norm_nonneg (f : ğ•œ â†’ ğ•œ) (a : A) : 0 â‰¤ cfcâ‚™ (fun z : ğ•œ â†¦ (â€–f zâ€– : ğ•œ)) a :=
  cfcâ‚™_nonneg fun _ _ â†¦ by simp

variable [Module â„ A] [SMulCommClass â„ A A] [IsScalarTower â„ A A]
variable [NonnegSpectrumClass â„ A] [IsTopologicalRing A] [T2Space A]
variable [NonUnitalContinuousFunctionalCalculus â„ A IsSelfAdjoint]

variable [StarModule ğ•œ A] [StarModule â„ A] [IsScalarTower â„ ğ•œ A] in
lemma abs_rclike_smul (r : ğ•œ) (a : A) : abs (r â€¢ a) = â€–râ€– â€¢ abs a := by
  trans abs (â€–râ€– â€¢ a)
  Â· simp [abs, mul_smul_comm, smul_mul_assoc, abs_mul_abs_self, star_smul, â† smul_assoc]
    simp only [RCLike.real_smul_eq_coe_smul (K := ğ•œ)]
    simp [-algebraMap_smul, â† smul_mul_assoc, smul_smul, â† mul_comm (starRingEnd _ _), RCLike.conj_mul, sq]
  Â· simp [abs_smul]

lemma abs_sq_eq_cfcâ‚™_norm_sq (a : A) (ha : p a := by cfc_tac) :
    abs a ^ (2 : â„â‰¥0) = cfcâ‚™ (fun z : ğ•œ â†¦ (â€–zâ€– ^ 2 : ğ•œ)) a := by
  conv_lhs => rw [abs_nnrpow_two, â† cfcâ‚™_id' ğ•œ a, â† cfcâ‚™_star, â† cfcâ‚™_mul ..]
  simp [RCLike.conj_mul]

lemma abs_eq_cfcâ‚™_norm (a : A) (ha : p a := by cfc_tac) :
    abs a = cfcâ‚™ (fun z : ğ•œ â†¦ (â€–zâ€– : ğ•œ)) a := by
  rw [abs, sqrt_eq_iff _ _ (hb := cfcâ‚™_norm_nonneg _ _), â† abs_nnrpow_two, abs_sq_eq_cfcâ‚™_norm_sq (ğ•œ := ğ•œ) a ha]
  conv_lhs => rw [â† cfcâ‚™_id' ğ•œ a, â† cfcâ‚™_mul ..]
  simp [sq, cfcâ‚™_id' ğ•œ a]

lemma cfcâ‚™_comp_norm (f : ğ•œ â†’ ğ•œ) (a : A) (ha : p a := by cfc_tac)
    (hf : ContinuousOn f ((fun z â†¦ (â€–zâ€– : ğ•œ)) '' quasispectrum ğ•œ a) := by cfc_cont_tac) :
    cfcâ‚™ f (abs a) = cfcâ‚™ (fun x â†¦ f â€–xâ€–) a := by
  obtain (hf0 | hf0) := em (f 0 = 0)
  Â· rw [cfcâ‚™_comp' f (fun x â†¦ (â€–xâ€– : ğ•œ)) a, â† abs_eq_cfcâ‚™_norm a]
  Â· rw [cfcâ‚™_apply_of_not_map_zero _ hf0, cfcâ‚™_apply_of_not_map_zero _ (fun h â†¦ (hf0 <| by simpa using h).elim)]

end RCLike

end NonUnital

section Unital

section Real

variable [Ring A] [StarRing A] [PartialOrder A] [StarOrderedRing A] [TopologicalSpace A] [Algebra â„ A] [IsTopologicalRing A] [T2Space A]
variable [ContinuousFunctionalCalculus â„ A IsSelfAdjoint]
variable [NonnegSpectrumClass â„ A]

@[simp]
lemma abs_one : abs (1 : A) = 1 := by
  simp [abs]

variable [StarModule â„ A]

lemma abs_algebraMap_real (c : â„) : abs (algebraMap â„ A c) = algebraMap â„ A |c| := by
  simp [Algebra.algebraMap_eq_smul_one]

@[simp]
lemma abs_algebraMap_nnreal (x : â„â‰¥0) : abs (algebraMap â„â‰¥0 A x) = algebraMap â„â‰¥0 A x := by
  simp [Algebra.algebraMap_eq_smul_one]

@[simp]
lemma abs_natCast (n : â„•) : abs (n : A) = n := by
  simpa only [map_natCast, Nat.abs_cast] using abs_algebraMap_real (n : â„)

@[simp]
lemma abs_ofNat (n : â„•) [n.AtLeastTwo] : abs (ofNat(n) : A) = ofNat(n) := by
  simpa using abs_natCast n

@[simp]
lemma abs_intCast (n : â„¤) : abs (n : A) = |n| := by
  cases n with
  | ofNat _ => simp
  | negSucc n =>
    rw [Int.cast_negSucc, abs_neg, abs_natCast, â† Int.cast_natCast]
    congr

end Real

section Complex

variable {ğ•œ A : Type*} {p : A â†’ Prop} [RCLike ğ•œ]
variable [Ring A] [TopologicalSpace A] [StarRing A] [PartialOrder A]
variable [StarOrderedRing A] [Algebra ğ•œ A]
variable [ContinuousFunctionalCalculus ğ•œ A p]
variable [Algebra â„ A] [NonnegSpectrumClass â„ A] [IsTopologicalRing A] [T2Space A]
variable [ContinuousFunctionalCalculus â„ A IsSelfAdjoint]

variable [StarModule ğ•œ A] [StarModule â„ A] [IsScalarTower â„ ğ•œ A] in
lemma abs_algebraMap_rclike (c : ğ•œ) : abs (algebraMap ğ•œ A c) = algebraMap â„ A (norm c : â„) := by
  simp [Algebra.algebraMap_eq_smul_one, abs_rclike_smul c, abs_one]

lemma cfc_comp_norm (f : ğ•œ â†’ ğ•œ) (a : A) (ha : p a := by cfc_tac)
    (hf : ContinuousOn f ((fun z â†¦ (â€–zâ€– : ğ•œ)) '' spectrum ğ•œ a) := by cfc_cont_tac) :
    cfc f (abs a) = cfc (fun x â†¦ f â€–xâ€–) a := by
  rw [abs_eq_cfcâ‚™_norm a (ğ•œ := ğ•œ), cfcâ‚™_eq_cfc, â† cfc_comp' ..]

lemma abs_sq (a : A) : (abs a) ^ 2 = star a * a := by
  rw [sq, abs_mul_abs_self]

end Complex

end Unital

end Generic

section CStar

/- This section requires `A` to be a `CStarRing` -/

variable (A : Type*) [NonUnitalNormedRing A] [StarRing A]
variable [PartialOrder A] [StarOrderedRing A] [NormedSpace â„ A] [SMulCommClass â„ A A] [IsScalarTower â„ A A]
variable [NonUnitalContinuousFunctionalCalculus â„ A IsSelfAdjoint]
variable [NonnegSpectrumClass â„ A] [CStarRing A]

open CFC

@[simp]
lemma abs_eq_zero_iff {a : A}  : abs a = 0 â†” a = 0 := by
  rw [CFC.abs, sqrt_eq_zero_iff _, CStarRing.star_mul_self_eq_zero_iff]

@[simp]
lemma norm_abs {a : A} : â€–abs aâ€– = â€–aâ€– := by
  rw [â† sq_eq_sqâ‚€ (norm_nonneg _) (norm_nonneg _), sq, sq, â† CStarRing.norm_star_mul_self,
    abs_nonneg.star_eq, CFC.abs_mul_abs_self, CStarRing.norm_star_mul_self]

end CStar

end CFC

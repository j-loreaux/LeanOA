import LeanOA.Mathlib.Misc
import LeanOA.Lp.lpSpace

open scoped lp ENNReal NNReal

variable {Î¹ ğ•œ : Type*} {E F G : Î¹ â†’ Type*} [RCLike ğ•œ]
variable [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)]
  [âˆ€ i, NormedAddCommGroup (F i)] [âˆ€ i, NormedSpace ğ•œ (F i)]
  [âˆ€ i, NormedAddCommGroup (G i)] [âˆ€ i, NormedSpace ğ•œ (G i)]

open ENNReal

variable {p q : â„â‰¥0âˆ} (r : â„â‰¥0âˆ) [hpqr : p.HolderTriple q r]

-- we could take `B` to be a bundled `lp (fun i â†¦ E i â†’L[ğ•œ] F i â†’L[ğ•œ] G i) âˆ`, but this has
-- downsides. For example, we would then have to bundle `fun i â†¦ (B i).flip`. Moreover, if you
-- have such a bundling, it is relatively easy to apply this lemma.
-- â‰¤ â€–Bâ€– * â€–eâ€–_p * â€–fâ€–_q
theorem Memâ„“p.of_bilin_of_top_left (B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] G i)
    {K : â„} (hBK : âˆ€ i, â€–B iâ€– â‰¤ K) {e : Î  i, E i} {f : Î  i, F i}
    (he : Memâ„“p e âˆ) (hf : Memâ„“p f q) :
    Memâ„“p (fun i â†¦ B i (e i) (f i)) q := by
  obtain (h | h) := isEmpty_or_nonempty Î¹
  Â· exact Memâ„“p.all _ -- this result should move to the other file
  obtain âŸ¨C, hCâŸ© := by
    simpa [memâ„“p_infty_iff, BddAbove, Set.Nonempty, Set.range, upperBounds] using he
  refine .of_norm <| hf.norm.const_mul (K * C) |>.mono fun i â†¦ ?_
  simp only [Real.norm_eq_abs]
  have hC_nonneg : 0 â‰¤ C := norm_nonneg _ |>.trans <| hC (Classical.arbitrary Î¹)
  replace hK_nonneg : 0 â‰¤ K := norm_nonneg (B (Classical.arbitrary Î¹)) |>.trans <| hBK _
  rw [abs_of_nonneg (by positivity), abs_of_nonneg (by positivity)]
  calc
    â€–B i (e i) (f i)â€– â‰¤ â€–B iâ€– * â€–e iâ€– * â€–f iâ€– := (B i (e i)).le_of_opNorm_le ((B i).le_opNorm _) _
    _ â‰¤ K * C * â€–f iâ€– := by gcongr; exacts [hBK i, hC i]

theorem Memâ„“p.of_bilin_of_top_right (B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] G i)
    {K : â„} (hBK : âˆ€ i, â€–B iâ€– â‰¤ K) {e : Î  i, E i} {f : Î  i, F i}
    (he : Memâ„“p e p) (hf : Memâ„“p f âˆ) :
    Memâ„“p (fun i â†¦ B i (e i) (f i)) p :=
  hf.of_bilin_of_top_left (fun i â†¦ (B i).flip) (by simpa using hBK) he

theorem Memâ„“p.of_bilin_of_zero_left (B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] G i)
    {e : Î  i, E i} {f : Î  i, F i} (he : Memâ„“p e 0) :
    Memâ„“p (fun i â†¦ B i (e i) (f i)) 0 := by
  rw [memâ„“p_zero_iff] at he âŠ¢
  apply he.subset
  rw [â† Set.compl_subset_compl, Set.compl_setOf, Set.compl_setOf]
  simp +contextual

theorem Memâ„“p.of_bilin_of_zero_right (B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] G i)
    {e : Î  i, E i} {f : Î  i, F i} (hf : Memâ„“p f 0) :
    Memâ„“p (fun i â†¦ B i (e i) (f i)) 0 :=
  hf.of_bilin_of_zero_left (fun i â†¦ (B i).flip)

theorem Real.inner_Lr_le_Lp_mul_Lq_of_nonneg {Î¹ : Type*} (s : Finset Î¹) {f g : Î¹ â†’ â„} {p q r : â„}
    (hpqr : p.HolderTriple q r) (hf : âˆ€ i âˆˆ s, 0 â‰¤ f i) (hg : âˆ€ i âˆˆ s, 0 â‰¤ g i) :
    âˆ‘ i âˆˆ s, (f i * g i) ^ r â‰¤ (âˆ‘ i âˆˆ s, f i ^ p) ^ (r / p) * (âˆ‘ i âˆˆ s, g i ^ q) ^ (r / q) := by
  have := hpqr.holderConjugate_div_div
  have hp := hpqr.pos
  have hq := hpqr.symm.pos
  have hr := hpqr.pos'
  calc âˆ‘ i âˆˆ s, (f i * g i) ^ r
    _ = âˆ‘ i âˆˆ s, (f i) ^ r * (g i) ^ r := by
      refine s.sum_congr rfl fun i hi â†¦ ?_
      rw [mul_rpow (hf i hi) (hg i hi)]
    _ â‰¤ (âˆ‘ i âˆˆ s, f i ^ p) ^ (r / p) * (âˆ‘ i âˆˆ s, g i ^ q) ^ (r / q) := by
      apply Real.inner_le_Lp_mul_Lq_of_nonneg s this (fun i hi â†¦ Real.rpow_nonneg (hf i hi) _)
        (fun i hi â†¦ Real.rpow_nonneg (hg i hi) _) |>.trans_eq
      congr! 2
      all_goals try simp only [fieldEq]
      all_goals
        refine s.sum_congr rfl fun i hi â†¦ ?_
        rw [â† Real.rpow_mul (by grind)]
        grind

lemma Memâ„“p.holder_top_left_bound
    {e : (i : Î¹) â†’ E i} {f : (i : Î¹) â†’ F i} (B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] G i)
    {K C D : â„} (hBK : âˆ€ i, â€–B iâ€– â‰¤ K) (hK : 0 â‰¤ K) (hC : 0 â‰¤ C)
    (hCe : âˆ€ i, â€–e iâ€– â‰¤ C) (hKe : âˆ€ s, âˆ‘ i âˆˆ s, â€–f iâ€– ^ q.toReal â‰¤ D) (s : Finset Î¹) :
    âˆ‘ i âˆˆ s, â€–B i (e i) (f i)â€– ^ q.toReal â‰¤ (K * C) ^ q.toReal * D := by
  grw [â† hKe s, s.mul_sum]
  apply s.sum_le_sum fun i hi â†¦ ?_
  rw [â† Real.mul_rpow (by positivity) (by positivity)]
  gcongr
  exact (B i (e i)).le_of_opNorm_le ((B i).le_of_opNorm_le_of_le (hBK i) (hCe i)) _

lemma Memâ„“p.holder_top_right_bound
    {e : (i : Î¹) â†’ E i} {f : (i : Î¹) â†’ F i} (B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] G i)
    {K C D : â„} (hBK : âˆ€ i, â€–B iâ€– â‰¤ K) (hK : 0 â‰¤ K) (hD : 0 â‰¤ D)
    (hCe : âˆ€ s, âˆ‘ i âˆˆ s, â€–e iâ€– ^ p.toReal â‰¤ C) (hDf : âˆ€ i, â€–f iâ€– â‰¤ D) (s : Finset Î¹) :
    âˆ‘ i âˆˆ s, â€–B i (e i) (f i)â€– ^ p.toReal â‰¤ (K * D) ^ p.toReal * C :=
  Memâ„“p.holder_top_left_bound (B Â· |>.flip) (by simpa) hK hD hDf hCe s

lemma Memâ„“p.holder_gen_bound {e : (i : Î¹) â†’ E i} {f : (i : Î¹) â†’ F i}
    (hp : 0 < p.toReal) (hq : 0 < q.toReal)
    (B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] G i) {K C D : â„} (hBK : âˆ€ i, â€–B iâ€– â‰¤ K)
    (hK : 0 â‰¤ K) (hC : 0 â‰¤ C) (hCe : âˆ€ s, âˆ‘ i âˆˆ s, â€–e iâ€– ^ p.toReal â‰¤ C)
    (hDf : âˆ€ s, âˆ‘ i âˆˆ s, â€–f iâ€– ^ q.toReal â‰¤ D) (s : Finset Î¹) :
    âˆ‘ i âˆˆ s, â€–B i (e i) (f i)â€– ^ r.toReal â‰¤
      K ^ r.toReal * C ^ (r.toReal / p.toReal) * D ^ (r.toReal / q.toReal) := by
  have hpqr := hpqr.toReal r hp hq
  have hr := hpqr.pos'
  suffices âˆ‘ i âˆˆ s, (â€–e iâ€– * â€–f iâ€–) ^ r.toReal â‰¤
      C ^ (r.toReal / p.toReal) * D ^ (r.toReal / q.toReal) from calc
    âˆ‘ i âˆˆ s, â€–B i (e i) (f i)â€– ^ r.toReal
    _ â‰¤ K ^ r.toReal * âˆ‘ i âˆˆ s, (â€–e iâ€– * â€–f iâ€–) ^ r.toReal := by
      rw [s.mul_sum]
      gcongr with i hi
      rw [â† Real.mul_rpow (by positivity) (by positivity), â† mul_assoc]
      gcongr
      exact (B i (e i)).le_of_opNorm_le ((B i).le_of_opNorm_le (hBK i) _) _
    _ â‰¤ _ := by
      rw [mul_assoc]
      gcongr
  calc
    _ â‰¤ (âˆ‘ i âˆˆ s, â€–e iâ€– ^ p.toReal) ^ (r.toReal / p.toReal) *
        (âˆ‘ i âˆˆ s, â€–f iâ€– ^ q.toReal) ^ (r.toReal / q.toReal) := by
      apply Real.inner_Lr_le_Lp_mul_Lq_of_nonneg s hpqr <;> (intros; positivity)
    _ â‰¤ _ := by
      gcongr
      Â· exact hCe s
      Â· exact hDf s

lemma Memâ„“p.holder {e : (i : Î¹) â†’ E i} {f : (i : Î¹) â†’ F i} (he : Memâ„“p e p) (hf : Memâ„“p f q)
    (B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] G i) {K : â„} (hBK : âˆ€ i, â€–B iâ€– â‰¤ K) :
    Memâ„“p (fun i â†¦ B i (e i) (f i)) r := by
  obtain (h | h) := isEmpty_or_nonempty Î¹
  Â· exact Memâ„“p.all _ -- this result should move to the other file
  have hK : 0 â‰¤ K := norm_nonneg (B (Classical.arbitrary Î¹)) |>.trans <| hBK _
  have hpqr' := hpqr.inv_eq
  obtain (rfl | rfl | hp) := p.trichotomy
  Â· simp_all only [ENNReal.inv_zero, top_add, inv_eq_top]
    exact he.of_bilin_of_zero_left B
  Â· simp_all only [inv_top, zero_add, inv_inj]
    exact he.of_bilin_of_top_left B hBK hf
  obtain (rfl | rfl | hq) := q.trichotomy
  Â· simp_all only [ENNReal.inv_zero, add_top, inv_eq_top]
    exact hf.of_bilin_of_zero_right B
  Â· simp_all only [inv_top, add_zero, inv_inj]
    exact he.of_bilin_of_top_right B hBK hf
  obtain âŸ¨C, hC, hCeâŸ© := memâ„“p_gen_iff'' hp |>.mp he
  obtain âŸ¨D, hD, hDfâŸ© := memâ„“p_gen_iff'' hq |>.mp hf
  exact memâ„“p_gen' <| Memâ„“p.holder_gen_bound r hp hq B hBK hK hC hCe hDf

/-- The map between `lp` spaces satisfying `ENNReal.HolderTriple` induced by a
uniformly bounded family of continuous bilinear maps on the underlying spaces. -/
@[simps]
def lp.holder (B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] G i) {K : â„} (hBK : âˆ€ i, â€–B iâ€– â‰¤ K)
    (e : lp E p) (f : lp F q) :
    lp G r where
  val := fun i â†¦ B i (e i) (f i)
  property := (lp.memâ„“p e).holder _ (lp.memâ„“p f) B hBK

set_option backward.isDefEq.respectTransparency false in
/-- `lp.holder` as a bilinear map. -/
@[simps!]
def lp.holderâ‚— (B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] G i) {K : â„} (hBK : âˆ€ i, â€–B iâ€– â‰¤ K) :
    lp E p â†’â‚—[ğ•œ] lp F q â†’â‚—[ğ•œ] lp G r :=
  .mkâ‚‚ ğ•œ (lp.holder r B hBK)
    (fun _ _ _ â†¦ by ext; simp)
    (fun _ _ _ â†¦ by ext; simp)
    (fun _ _ _ â†¦ by ext; simp)
    (fun _ _ _ â†¦ by ext; simp)

/-- `lp.holder` as a continuous bilinear map. -/
noncomputable def lp.holderL [Fact (1 â‰¤ p)] [Fact (1 â‰¤ q)] [Fact (1 â‰¤ r)]
    (B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] G i) {K : â„â‰¥0} (hBK : âˆ€ i, â€–B iâ€– â‰¤ K) :
    lp E p â†’L[ğ•œ] lp F q â†’L[ğ•œ] lp G r :=
  lp.holderâ‚— r B hBK |>.mkContinuousâ‚‚ K fun e f â†¦ by
    obtain âŸ¨(rfl | hp), (rfl | hq)âŸ© := And.intro p.dichotomy q.dichotomy
    Â· obtain rfl : r = âŠ¤ := ENNReal.HolderTriple.unique âˆ âˆ r âˆ
      refine lp.norm_le_of_forall_le (by positivity) fun i â†¦ ?_
      refine (B i).le_of_opNormâ‚‚_le_of_le (hBK i) ?_ ?_
      all_goals exact lp.norm_apply_le_norm (by simp) ..
    Â· obtain rfl : r = q := ENNReal.HolderTriple.unique âˆ q r q
      refine lp.norm_le_of_forall_sum_le (zero_lt_one.trans_le hq) (by positivity) fun s â†¦ ?_
      rw [Real.mul_rpow (by positivity) (by positivity)]
      refine Memâ„“p.holder_top_left_bound B hBK
        (by positivity) (by positivity) (lp.norm_apply_le_norm (by simp) _) ?_ s
      exact lp.sum_rpow_le_norm_rpow (zero_lt_one.trans_le hq) f
    Â· obtain rfl : r = p := ENNReal.HolderTriple.unique p âˆ r p
      refine lp.norm_le_of_forall_sum_le (zero_lt_one.trans_le hp) (by positivity) fun s â†¦ ?_
      rw [mul_right_comm, Real.mul_rpow (by positivity) (by positivity)]
      refine Memâ„“p.holder_top_right_bound B hBK
        (by positivity) (by positivity) ?_ (lp.norm_apply_le_norm (by simp) _) s
      exact lp.sum_rpow_le_norm_rpow (zero_lt_one.trans_le hp) e
    Â· have hpqr := hpqr.toReal r (zero_lt_one.trans_le hp) (zero_lt_one.trans_le hq)
      have hp := hpqr.pos
      have hq := hpqr.symm.pos
      refine lp.norm_le_of_forall_sum_le hpqr.pos' (by positivity) fun s â†¦ ?_
      simp only [holderâ‚—_apply_apply_coe]
      calc
        _ â‰¤ K ^ r.toReal * (â€–eâ€– ^ p.toReal) ^ (r.toReal / p.toReal) *
          (â€–fâ€– ^ q.toReal) ^ (r.toReal / q.toReal) :=
          Memâ„“p.holder_gen_bound r hp hq B hBK (by positivity) (by positivity)
            (lp.sum_rpow_le_norm_rpow hp e) (lp.sum_rpow_le_norm_rpow hq f) s
        _ â‰¤ _ := by
          rw [â† Real.rpow_mul, â† Real.rpow_mul]
          Â· simp only [â† mul_div_assoc, ne_eq, hp.ne', not_false_eq_true, mul_div_cancel_leftâ‚€,
            hq.ne', fieldLe]
            rw [Real.mul_rpow, Real.mul_rpow]
            all_goals positivity
          all_goals positivity

lemma lp.norm_holderL_le [Fact (1 â‰¤ p)] [Fact (1 â‰¤ q)] [Fact (1 â‰¤ r)]
    (B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] G i) {K : â„â‰¥0} (hBK : âˆ€ i, â€–B iâ€– â‰¤ K) :
    â€–lp.holderL (p := p) (q := q) r B hBKâ€– â‰¤ K :=
  LinearMap.mkContinuousâ‚‚_norm_le _ K.2 _

variable {H : Type*} [NormedAddCommGroup H] [NormedSpace ğ•œ H] [CompleteSpace H]

variable (p q) in
/-- The natural pairing between `lp E p` and `lp F q` (for HÃ¶lder conjugate `p q : â„â‰¥0âˆ`) with
values in a space `H` induced by a family of bilinear maps `B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] H`.

This is given by `âˆ‘' i, B (e i) (f i)`.

In the special case when `B := (NormedSpace.inclusionInDoubleDual ğ•œ E).flip`, which is
definitionally the same as `B := ContinuousLinearMap.id ğ•œ (E â†’L[ğ•œ] ğ•œ)`, this is the natural map
`lp (fun _ â†¦ StrongDual ğ•œ E) p â†’L[ğ•œ] StrongDual ğ•œ (lp E q)`.
-/
noncomputable def lp.dualPairing [Fact (1 â‰¤ p)] [Fact (1 â‰¤ q)] [p.HolderConjugate q]
    (B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] H) {K : â„â‰¥0} (hBK : âˆ€ i, â€–B iâ€– â‰¤ K) :
    lp E p â†’L[ğ•œ] lp F q â†’L[ğ•œ] H :=
  (lp.tsumCLM Î¹ ğ•œ H |>.postcomp <| lp F q) âˆ˜L (lp.holderL 1 B hBK)

lemma lp.dualPairing_apply [Fact (1 â‰¤ p)] [Fact (1 â‰¤ q)] [p.HolderConjugate q]
    (B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] H) {K : â„â‰¥0} (hBK : âˆ€ i, â€–B iâ€– â‰¤ K)
    (e : lp E p) (f : lp F q) :
    lp.dualPairing p q B hBK e f = âˆ‘' i, B i (e i) (f i) :=
  rfl

lemma lp.norm_dualPairing [Fact (1 â‰¤ p)] [Fact (1 â‰¤ q)] [p.HolderConjugate q]
    (B : (i : Î¹) â†’ E i â†’L[ğ•œ] F i â†’L[ğ•œ] H) {K : â„â‰¥0} (hBK : âˆ€ i, â€–B iâ€– â‰¤ K) :
    â€–lp.dualPairing p q B hBKâ€– â‰¤ K := calc
  â€–lp.dualPairing p q B hBKâ€–
  _ â‰¤ â€–(tsumCLM Î¹ ğ•œ H).postcomp (lp F q)â€– * â€–holderL 1 B hBKâ€– :=
    ContinuousLinearMap.opNorm_comp_le _ _
  _ â‰¤ 1 * K := by
    gcongr
    Â· exact ContinuousLinearMap.norm_postcomp_le _ |>.trans lp.norm_tsumCLM
    Â· exact norm_holderL_le 1 B hBK
  _ = K := one_mul _

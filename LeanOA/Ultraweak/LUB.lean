import LeanOA.Ultraweak.Uniformity
import LeanOA.ComplexOrder
import LeanOA.Mathlib.Algebra.Order.Star.Basic
import LeanOA.Mathlib.Analysis.Complex.Basic
import LeanOA.CFC
import LeanOA.Ultraweak.ContinuousFunctionalCalculus
import LeanOA.Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.Rpow.Basic
import LeanOA.CStarAlgebra.PositiveLinearFunctional
import LeanOA.Mathlib.Algebra.Order.Star.Conjugate

/-! # `œÉ(M, P)` is a conditionally complete partial order

This file establishes some nice order-theoretic properties of `œÉ(M, P)`.
Since the order on `œÉ(M, P)` coincides with the order on `M`, these transfer.
In particular, we show that it:

+ is a conditionally complete partial order (i.e. every nonempty directed set which is bounded above
  has a least upper bound);
+ satisfies `SupConvergenceClass` (monotone functions converge to their supremum)
+ and conjugation preserves suprema

-/


variable {M P : Type*} [CStarAlgebra M] [PartialOrder M] [StarOrderedRing M]
variable [NormedAddCommGroup P] [NormedSpace ‚ÑÇ P] [Predual ‚ÑÇ M P] [CompleteSpace P]

namespace Ultraweak

open scoped ComplexOrder ComplexStarModule Topology
open Filter Set Bornology StarOrderedRing

/-- An increasing net of elements which is bounded above in `œÉ(M, P)` converges
to its least upper bound.

I'll note that this uses that `œÉ(M, P)` is an `OrderClosedTopology` to conclude
the element to which is converges is indeed the least upper bound. -/
lemma DirectedOn.exists_isLUB (s : Set œÉ(M, P)) (hs : DirectedOn (¬∑ ‚â§ ¬∑) s)
    (hnon : s.Nonempty) (hbd : BddAbove s) :
    ‚àÉ x : œÉ(M, P), IsLUB s x ‚àß Tendsto (Subtype.val : s ‚Üí œÉ(M, P)) atTop (ùìù x) := by
  /- Since `s` is nonempty, we may take the intersection with `Ici x‚ÇÄ` for some
  `x‚ÇÄ ‚àà s`. This set is still directed, but now it is also bounded above and below.
  Hence it is norm bounded. -/
  let ‚ü®x‚ÇÄ, hx‚ÇÄ‚ü© := hnon
  have hbd' : BddAbove (ofUltraweak '' (s ‚à© Ici x‚ÇÄ)) :=
    monotone_ofUltraweak.map_bddAbove hbd.inter_of_left
  have hbd'' : BddBelow (ofUltraweak '' (s ‚à© Ici x‚ÇÄ)) := by
    use ofUltraweak x‚ÇÄ
    rintro - ‚ü®x, hx, rfl‚ü©
    aesop
  obtain ‚ü®r, hr‚ü© := isBounded_of_bddAbove_of_bddBelow hbd' hbd'' |>.subset_closedBall 0
  /- The net `s` of elements is eventually bounded. -/
  have h_map_le : map (Subtype.val : s ‚Üí œÉ(M, P)) atTop ‚â§
      ùìü (ofUltraweak ‚Åª¬π' Metric.closedBall 0 r) := by
    simp only [le_principal_iff, mem_map]
    refine mem_of_superset (Ici_mem_atTop (‚ü®x‚ÇÄ, hx‚ÇÄ‚ü© : s)) ?_
    intro ‚ü®x, hx‚ü© hxx‚ÇÄ
    simp only [mem_Ici, Subtype.mk_le_mk, mem_preimage, Metric.mem_closedBall,
      dist_zero_right] at hxx‚ÇÄ ‚ä¢
    simpa using hr ‚ü®_, ‚ü®hx, hxx‚ÇÄ‚ü©, rfl‚ü©
  /- The subtype `‚Ü•s` is directed and nonempty. -/
  have : IsDirectedOrder s := ‚ü®hs.directed_val‚ü©
  have : Nonempty s := hnon.to_subtype
  /- To see that the net `s` is cauchy in `œÉ(M, P)` it suffices to check that for
  any continuous positive linear functional `œÜ`, applying `œÜ` to `s` is also cauchy.
  However, since this is a net in `‚ÑÇ` which is bounded above, it in fact converges,
  and is therefore cauchy. -/
  have h_cauchy : Cauchy (map ((‚Üë) : s ‚Üí œÉ(M, P)) atTop) := by
    apply cauchy_of_forall_posCLM_of_eventually (by simpa using h_map_le) fun œÜ ‚Ü¶ ?_
    have hœÜ := OrderHomClass.mono œÜ
    exact Tendsto.cauchy_map <| tendsto_atTop_ciSup (hœÜ.comp (Subtype.mono_coe (¬∑ ‚àà s))) <| by
      simpa [‚Üê Function.comp_def, Set.range_comp]
        using (OrderHomClass.mono œÜ |>.map_bddAbove hbd)
  /- Since the closed ball is compact (and therefore complete) and this cauchy net is
  eventually within it, it converges to some element `x`. -/
  obtain ‚ü®x, -, hx‚ü© := isCompact_closedBall ‚ÑÇ P (0 : M) r |>.isComplete _ h_cauchy h_map_le
  refine ‚ü®x, ?_, hx‚ü©
  /- Since the net is increasing, and the topology on `œÉ(M, P)` is order closed, the
  limit is the least upper bound. -/
  simpa [setOf] using isLUB_of_tendsto_atTop (Œ≤ := s) (Subtype.mono_coe (¬∑ ‚àà s)) hx

/-- `œÉ(M, P)` is a conditionally complete partial order. Since this is only dependent upon the
order, not the topology, the same is true of `M`. -/
noncomputable instance : ConditionallyCompletePartialOrderSup œÉ(M, P) where
  sSup s :=
    open Classical in
    if h : DirectedOn (¬∑ ‚â§ ¬∑) s ‚àß s.Nonempty ‚àß BddAbove s
    then (DirectedOn.exists_isLUB s h.1 h.2.1 h.2.2).choose
    else 0
  isLUB_csSup_of_directed s h_dir h_non hbdd := by
    rw [dif_pos (by grind)]
    exact (DirectedOn.exists_isLUB s h_dir h_non hbdd).choose_spec.1

/-- An increasing net of elements which is bounded above in `œÉ(M, P)` converges
to its least upper bound. -/
instance : SupConvergenceClass œÉ(M, P) where
  tendsto_coe_atTop_isLUB a s hsa := by
    by_cases! h : (atTop : Filter s) = ‚ä•
    ¬∑ simp [h]
    rw [atTop_neBot_iff] at h
    obtain ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© := h
    replace h‚ÇÅ : s.Nonempty := Set.nonempty_coe_sort.mp h‚ÇÅ
    replace h‚ÇÇ : DirectedOn (¬∑ ‚â§ ¬∑) s := by
      rw [directedOn_iff_directed]
      obtain ‚ü®h‚ÇÇ‚ü© := h‚ÇÇ
      exact h‚ÇÇ
    obtain ‚ü®u, hu‚ÇÅ, hu‚ÇÇ‚ü© := DirectedOn.exists_isLUB s h‚ÇÇ h‚ÇÅ ‚ü®_, hsa.1‚ü©
    exact hsa.unique hu‚ÇÅ ‚ñ∏ hu‚ÇÇ

omit [CompleteSpace P] in
private theorem isLUB_star_right_conjugate_aux (a u : œÉ(M, P)) (s : Set œÉ(M, P))
    [IsDirectedOrder s] [Nonempty s] (h : IsLUB s u)
    (h‚ÇÅ : Tendsto (Subtype.val : s ‚Üí œÉ(M, P)) atTop (ùìù u))
    (œÜ : œÉ(M, P) ‚ÜíP[‚ÑÇ] ‚ÑÇ) :
    Tendsto (fun x : s ‚Ü¶ œÜ (a * x)) atTop (ùìù (œÜ (a * u))) := by
  /- It clearly suffices to show `x ‚Ü¶ ‚ÄñœÜ (a * (u - x))‚Äñ` tends to zero. -/
  rw [tendsto_iff_norm_sub_tendsto_zero]
  conv => enter [1, x]; rw [norm_sub_rev, ‚Üê map_sub, ‚Üê mul_sub]
  /- `fun x ‚Ü¶ u - ‚Üëx` tends to zero since `Subtype.val` tends to `u`.
  And since `œÜ` is continuous, `fun x ‚Ü¶ ‚àö‚ÄñœÜ (u - x)‚Äñ` also tends to zero. -/
  have h‚ÇÅ : Tendsto (fun x : s ‚Ü¶ u - x) atTop (ùìù 0) := by
    simpa using (tendsto_sub_nhds_zero_iff.mpr h‚ÇÅ |>.neg)
  have h‚ÇÇ : Tendsto (fun x : s ‚Ü¶ ‚àö‚ÄñœÜ (u - x)‚Äñ) atTop (ùìù 0) := by
    have := Real.continuous_sqrt.comp' continuous_norm |>.comp' (map_continuous œÜ)
    simpa [- map_sub] using this.tendsto _ |>.comp <| h‚ÇÅ
  /- The map `x ‚Ü¶ ‚àö‚ÄñœÜ (a * (u - x) * star a)‚Äñ` is eventually bounded because `œÜ` is norm
  continuous (since it is ultraweakly continuous), and it argument is eventually smaller than the
  nonnegative element `a * (u - x‚ÇÄ) * star a`, where `x‚ÇÄ ‚àà s` is arbitrary. -/
  obtain ‚ü®c, hcu‚ü© : ‚àÉ c, ‚àÄ·∂† (x : s) in atTop, |‚àö‚ÄñœÜ (a * (u - x) * star a)‚Äñ| ‚â§ c := by
    have x‚ÇÄ : s := Classical.arbitrary s
    let œÜ' := œÜ.comp (toUltraweakPosCLM P) |>.toContinuousLinearMap
    use |‚àö(‚ÄñœÜ'‚Äñ * ‚ÄñofUltraweak (a * (u - x‚ÇÄ.val) * star a)‚Äñ)|
    filter_upwards [Ici_mem_atTop x‚ÇÄ] with x (hx : x‚ÇÄ ‚â§ x)
    gcongr
    calc
      ‚ÄñœÜ (a * (u - x) * star a)‚Äñ ‚â§ ‚ÄñœÜ (a * (u - x‚ÇÄ) * star a)‚Äñ :=
        CStarAlgebra.norm_le_norm_of_nonneg_of_le -- hitting a nail with a nuke
          (map_nonneg œÜ <| star_right_conjugate_nonneg (by simpa using h.1 x.prop) a)
          (OrderHomClass.mono œÜ <| star_right_conjugate_le_conjugate (by grw [hx]) a)
      _ = ‚ÄñœÜ' (ofUltraweak (a * (u - ‚Üëx‚ÇÄ) * star a))‚Äñ := by simp [œÜ']
      _ ‚â§ ‚ÄñœÜ'‚Äñ * ‚ÄñofUltraweak (a * (u - ‚Üëx‚ÇÄ) * star a)‚Äñ := œÜ'.le_opNorm _
  /- By the Cauchy-Schwarz inequality,
    ‚ÄñœÜ (a * (u - x))‚Äñ ‚â§ ‚ÄñœÜ (a * ‚àö(u - x) * ‚àö(u - x))‚Äñ
    _ ‚â§ ‚àö‚ÄñœÜ (a * (u - x) * star a)‚Äñ * ‚àö‚ÄñœÜ (u - x)‚Äñ.
  Since the first factor is bounded and the latter tendsto to zero, the product tends to zero.
  Hence `œÜ (a * (u - x))` tends to zero by the squeeze theorem. -/
  refine squeeze_zero (fun _ ‚Ü¶ by positivity) (fun x ‚Ü¶ ?_) <| bdd_le_mul_tendsto_zero' c hcu h‚ÇÇ
  have hux : 0 ‚â§ u - x := sub_nonneg.mpr <| h.1 x.prop
  rw [‚Üê CFC.sqrt_mul_sqrt_self' (u - x)]
  have := œÜ.toPositiveLinearMap.cauchy_schwarz_mul_star
    (a * CFC.sqrt (u - x)) (star (CFC.sqrt (u - x)))
  simpa [(CFC.sqrt_nonneg (u - x)).star_eq, mul_assoc]

/-- If `s` is a nonempty directed set which is bounded above with supremum `u`,
then so is `(a * ¬∑ * star a) '' s`, and its least upper bound is `a * u * star a`. -/
lemma DirectedOn.isLUB_star_right_conjugate (a u : œÉ(M, P)) (s : Set œÉ(M, P))
    (hd : DirectedOn (¬∑ ‚â§ ¬∑) s) (hnon : s.Nonempty) (h : IsLUB s u) :
    IsLUB (conjOrderHom a '' s) (a * u * star a) := by
  /- Clearly `fun x : s ‚Ü¶ ‚Üëx` converges to `u`. For any invertible element `b`, since
  `(b * ¬∑ * star b)` is an order isomorphism, we find that `(b * ¬∑ * star b) '' s` has
  `b * u * star b` as its least upper bound, and hence `(b * ¬∑ * star b)` tends to
  `b * u * star b`. And since `(a * ¬∑ * star a)` is monotone, it suffices to show that this
  converges to `a * u * star a` (along `atTop : Filter ‚Ü•s`). -/
  have : Nonempty s := hnon.to_subtype
  have : IsDirectedOrder s := directedOn_iff_isDirectedOrder.mp hd
  have h‚ÇÅ : Tendsto (¬∑ : s ‚Üí œÉ(M, P)) atTop (ùìù u) :=
    tendsto_atTop_isLUB (Subtype.mono_coe (¬∑ ‚àà s)) <| Subtype.range_coe ‚ñ∏ h
  have h‚ÇÇ (b : œÉ(M, P)) (hb : IsUnit b) :
      Tendsto (fun x : s ‚Ü¶ b * x * star b) atTop (ùìù (b * u * star b)) := by
    refine tendsto_atTop_isLUB (conjOrderHom b |>.monotone.comp <| Subtype.mono_coe (¬∑ ‚àà s)) ?_
    convert h.conjugate_star_right_of_isUnit b hb
    ext
    simp
  suffices Tendsto (fun x : s ‚Ü¶ a * x * star a) atTop (ùìù (a * u * star a)) by
    convert isLUB_of_tendsto_atTop (conjOrderHom a |>.monotone.comp <|
      Subtype.mono_coe (¬∑ ‚àà s)) this
    ext
    simp
  /- Since this function has eventually bounded range (eventually bounded below by `a * x‚ÇÄ * star a`
  for any fixed `x‚ÇÄ ‚àà s`, and bounded above by `a * u * star a`), it suffices to check that for
  any positive continuous linear functional `œÜ : œÉ(M, P) ‚ÜíP[‚ÑÇ] ‚ÑÇ` that `fun x ‚Ü¶ œÜ (a * x * star a)`
  tends to `œÜ (a * u * star a)`. -/
  refine tendsto_of_forall_posCLM_of_disjoint ?hbdd fun œÜ ‚Ü¶ ?htends
  case hbdd =>
    have x‚ÇÄ : s := Classical.arbitrary s
    simp only [disjoint_cobounded_iff]
    refine ‚ü®_, image_mem_map (Ici_mem_atTop x‚ÇÄ), ?_‚ü©
    rw [‚Üê isBounded_image_ofUltraweak]
    apply isBounded_of_bddAbove_of_bddBelow <;> simp only [image_image]
    ¬∑ refine monotone_ofUltraweak.comp (conjOrderHom a).monotone |>.map_bddAbove ‚ü®u, h.1‚ü© |>.mono ?_
      rintro - ‚ü®x, hx, rfl‚ü©
      exact ‚ü®x.val, x.prop, rfl‚ü©
    ¬∑ exact monotone_ofUltraweak.comp (conjOrderHom a).monotone |>.comp (Subtype.mono_coe (¬∑ ‚àà s))
        |>.map_bddBelow ‚ü®x‚ÇÄ, fun _ ‚Ü¶ id‚ü©
  /- By the previous lemma `fun x ‚Ü¶ œÜ (a * x)` and `fun x ‚Ü¶ œÜ (x * star a)` tend to `œÜ (a * u)`
  and `œÜ (u * star a)`, respectively. -/
  have h‚ÇÉ : Tendsto (fun x : s ‚Ü¶ œÜ (a * x)) atTop (ùìù (œÜ (a * u))) :=
    isLUB_star_right_conjugate_aux a u s h h‚ÇÅ œÜ
  have h‚ÇÑ : Tendsto (fun x : s ‚Ü¶ œÜ (x * star a)) atTop (ùìù (œÜ (u * star a))) := by
    simp_rw +singlePass [tendsto_iff_norm_sub_tendsto_zero, norm_sub_rev,
      ‚Üê map_sub, ‚Üê mul_sub, ‚Üê sub_mul] at h‚ÇÉ ‚ä¢
    apply h‚ÇÉ.congr fun x ‚Ü¶ ?_
    convert norm_star (œÜ ((u - x) * star a))
    rw [‚Üê map_star œÜ, star_mul, star_star, (sub_nonneg.mpr (h.1 x.prop)).star_eq]
  /- Obviously there is some `z : ‚ÑÇ` so that `z + a` is invertible.
  So we note that `fun x ‚Ü¶ œÜ ((z + a) * x * star (z + a))` tends to `(z + a) * u * star (z + a)`
  (because `z + a` is invertible). But at the same time, by expanding the terms out, we see that
  `fun x ‚Ü¶ z * star z * œÜ x + star z * œÜ (a * x) + z * œÜ (x * star a) + œÜ (a * x * star a)`.
  The first thre terms converge to `z * star z * œÜ u + star z * œÜ (a * u) + z * œÜ (u * star a)`
  and since the entirety converges to `(z + a) * u * star (z + a)` we obtain the desired convergence
  of `fun x ‚Ü¶ œÜ (a * x * star a)` to `œÜ (a * u * star a)`. -/
  obtain ‚ü®z, hz‚ü© : ‚àÉ z : ‚ÑÇ, IsUnit (algebraMap ‚ÑÇ œÉ(M, P) z + a) := by
    suffices spectrum ‚ÑÇ (-a) ‚â† Set.univ by simpa [Set.ne_univ_iff_exists_notMem, spectrum.mem_iff]
    simpa using spectrum.isCompact (starAlgEquiv M P (-a)) |>.ne_univ
  have key (x : œÉ(M, P)) : œÜ (a * x * star a) =
      œÜ ((algebraMap ‚ÑÇ œÉ(M, P) z + a) * x * star (algebraMap ‚ÑÇ œÉ(M, P) z + a)) -
      (z * star z * œÜ x + star z * œÜ (a * x) + z * œÜ (x * star a)) := by
    simp [Algebra.algebraMap_eq_smul_one, add_mul, mul_add]
    ring
  simp only [key]
  apply_rules [Tendsto.sub, Tendsto.add, Tendsto.const_mul]
  ¬∑ exact (map_continuous œÜ).tendsto _ |>.comp <| h‚ÇÇ _ hz
  ¬∑ exact (map_continuous œÜ).tendsto _ |>.comp <| h‚ÇÅ

end Ultraweak

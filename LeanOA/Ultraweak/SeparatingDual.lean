import LeanOA.Ultraweak.ContinuousStar
import LeanOA.PositiveContinuousLinearMap
import Mathlib.Analysis.LocallyConvex.WeakDual
import Mathlib.Analysis.CStarAlgebra.PositiveLinearMap
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Basic

open scoped Ultraweak ComplexOrder ComplexStarModule

instance CFC.instSelfAdjointDecompose {A : Type*} [NonUnitalRing A] [Module â„ A]
    [SMulCommClass â„ A A] [IsScalarTower â„ A A] [StarRing A] [TopologicalSpace A]
    [NonUnitalContinuousFunctionalCalculus â„ A IsSelfAdjoint] [PartialOrder A]
    [StarOrderedRing A] : SelfAdjointDecompose A where
  exists_nonneg_sub_nonnpos {a} ha :=
    âŸ¨aâº, aâ», CFC.posPart_nonneg a, CFC.negPart_nonneg a, (posPart_sub_negPart a ha).symmâŸ©

namespace Ultraweak

variable {M P : Type*} [CStarAlgebra M] [PartialOrder M] [StarOrderedRing M]
variable [NormedAddCommGroup P] [NormedSpace â„‚ P] [Predual â„‚ M P] [CompleteSpace P]

instance : StarHomClass (Ïƒ(M, P) â†’â‚š[â„‚] â„‚) Ïƒ(M, P) â„‚ :=
  inferInstanceAs (StarHomClass (M â†’â‚š[â„‚] â„‚) M â„‚)

open Complex
/-- If `a : Ïƒ(M, P)` is a selfadjoint element which is not nonnegative, then there is some
positive continuous linear functional which takes a negative value at `a`.

This is Sakai 1.7.2. Our approach is essentially the same, but instead of applying
Hahn-Banach to the `â„`-locally convex space of selfadjoint elements and then extending this
functional to `â„‚`-linear one defined everywhere, we apply it for `Ïƒ(M, P)` and then
precompose with the real part before extending to a `â„‚`-linear functional. -/
lemma exists_positiveCLM_apply_lt_zero (a : Ïƒ(M, P)) (haâ‚ : IsSelfAdjoint a) (haâ‚‚ : Â¬ 0 â‰¤ a) :
    âˆƒ Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚, Ï† a < 0 := by
  /- Since the nonnegative elements form a convex set, by the Hahn-Banach theorem,
  there is a continuous `â„`-linear functional `f` which separates them. Moreover,
  since the positive elements are an `â„`-convex cone, `f` must be nonnegative on
  nonnegative elements, so that `f a < 0`. -/
  have hâ‚ : Convex â„ {x : Ïƒ(M, P) | 0 â‰¤ x} := ConvexCone.positive â„ Ïƒ(M, P) |>.convex
  obtain âŸ¨f, u, hfa, hfâŸ© := geometric_hahn_banach_point_closed hâ‚ Ultraweak.isClosed_nonneg haâ‚‚
  have hu : u < 0 := map_zero f â–¸ hf 0 le_rfl
  have hf_nonneg (x : Ïƒ(M, P)) (hx : 0 â‰¤ x) : 0 â‰¤ f x := by
    by_contra! hfx
    have : 0 < u * (f x)â»Â¹ := mul_pos_of_neg_of_neg hu (inv_neg''.mpr hfx)
    simpa [hfx.ne] using hf _ (smul_nonneg this.le hx)
  replace hfa := hfa.trans hu
  clear u hu hf
  /- `g := x â†¦ f (â„œ x)` is a continuous `â„`-linear functional, and we may extend
  it to a continuous `ğ•œ`-linear functional `Ï† := x â†¦ f (â„œ x) + I â€¢ f (â„‘ x)`. -/
  let g : StrongDual â„ Ïƒ(M, P) := (2â»Â¹ : â„) â€¢ (f + f âˆ˜L (starL' â„ (A := Ïƒ(M, P))))
  have hfg (x : Ïƒ(M, P)) : g x = f (â„œ x) := by simp [g, realPart_apply_coe]
  let Ï† : StrongDual â„‚ Ïƒ(M, P) := g.extendRCLike
  have hÏ† (x : Ïƒ(M, P)) : Ï† x = f (â„œ x) + I â€¢ f (â„‘ x) := by
    conv_lhs =>
      rw [â† realPart_add_I_smul_imaginaryPart x, map_add, map_smul]
      simp [Ï†, StrongDual.extendRCLike_apply, hfg, â† smul_eq_mul]
  have hÏ†_sa {x : Ïƒ(M, P)} (hx : IsSelfAdjoint x) : Ï† x = f x := by
    simp [hÏ†, hx.imaginaryPart, hx.coe_realPart]
  /- Since `f` is nonnegative and coincides with `Ï†` on selfadjoint elements,
  `Ï†` is the desired positive continuous linear map. -/
  use .mkâ‚€ Ï† fun x hx â†¦ by simpa [hÏ†_sa hx.isSelfAdjoint] using hf_nonneg x hx
  simpa [hÏ†_sa, haâ‚]

instance : SelfAdjointDecompose Ïƒ(M, P) where
  exists_nonneg_sub_nonnpos {a} ha := by
    have âŸ¨_, _, _, _, keyâŸ© := ha.ofUltraweak.exists_nonneg_sub_nonpos
    replace key := by simpa using congr(toUltraweak â„‚ P $key)
    exact âŸ¨_, _, by simpa, by simpa, keyâŸ©

lemma eq_zero_of_forall_positiveCLM (a : Ïƒ(M, P))
    (ha : âˆ€ Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚, Ï† a = 0) :
    a = 0 := by
  suffices âˆ€ {a}, IsSelfAdjoint a â†’ (âˆ€ Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚, Ï† a = 0) â†’ a = 0 by
    have âŸ¨hâ‚, hâ‚‚âŸ© := And.intro (this (â„œ a).2 (fun Ï† â†¦ ?_)) (this (â„‘ a).2 (fun Ï† â†¦ ?_))
    Â· simpa [realPart_add_I_smul_imaginaryPart] using congr($hâ‚ + I â€¢ $hâ‚‚)
    Â· simp [Ï†.map_realPart, ha]
    Â· simp [Ï†.map_imaginaryPart, ha]
  intro a ha h
  have hâ‚ := by simpa using mt <| exists_positiveCLM_apply_lt_zero _ ha
  have hâ‚‚ := by simpa using mt <| exists_positiveCLM_apply_lt_zero _ ha.neg
  refine le_antisymm (hâ‚‚ ?_) (hâ‚ ?_)
  all_goals simp [h]

lemma ext_positiveCLM {a b : Ïƒ(M, P)} (h : âˆ€ Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚, Ï† a = Ï† b) :
    a = b :=
  sub_eq_zero.mp <| eq_zero_of_forall_positiveCLM _ fun Ï† â†¦ by simp [h]

lemma ext_positiveCLM_iff {a b : Ïƒ(M, P)} :
    a = b â†” âˆ€ Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚, Ï† a = Ï† b :=
  âŸ¨by congr!, ext_positiveCLMâŸ©

end Ultraweak

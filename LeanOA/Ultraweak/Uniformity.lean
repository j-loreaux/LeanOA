import LeanOA.Ultraweak.Bornology
import LeanOA.Ultraweak.SeparatingDual
import LeanOA.WeakDual.UniformSpace
import LeanOA.Mathlib.Topology.Bornology.Basic

/-! # Properties of the uniformity and topology of `Ïƒ(M, P)`

This file contains some nice characterizations of the uniformity and the topology of `Ïƒ(M, P)`
in terms of positive linear functionals.

In particular, on *bounded sets*, the uniformity of `Ïƒ(M, P)` is the same as the uniformity of
on the weak topology of `M` induced by the pairing with the type, herein denoted by the private
declaration `Ultraweak.E`, of linear combinations of positive ultraweakly continuous linear
functionals. The private declaration `Ultraweak.WeakE` denotes `M` equipped with the weak topology
induced by `E`.

Crucially, `WeakE` is Hausdorff, and weaker that `Ïƒ(M, P)` which is compact on preimages (under
`ofUltraweak`) of closed and bounded sets. Therefore, the identity map from `Ïƒ(M, P)` to `WeakE` is
a uniform equivalence on bounded sets.

Consequently, a filter `l` in `Ïƒ(M, P)`, disjoint from the cobounded filter, is cauchy if and only
if `map Ï† l` is cauchy for every positive ultraweakly continuous linear functional `Ï†`. Similarly,
a function `f` defined on `Ïƒ(M, P)` whose range is eventually bounded tends to `ğ“ x` if and only
if `map Ï† f` tends to `ğ“ (Ï† x)` for every positive ultraweakly continuous linear functional `Ï†`.

## Main statements

-/

-- these attributes are added in #35261
attribute [push] Filter.not_neBot
attribute [push â†] Filter.neBot_iff

variable {M P : Type*} [CStarAlgebra M] [PartialOrder M] [StarOrderedRing M]
variable [NormedAddCommGroup P] [NormedSpace â„‚ P] [Predual â„‚ M P] [CompleteSpace P]

namespace Ultraweak

open scoped Topology ComplexOrder
open Filter Set Bornology

variable (P) in
/-- The map `toUltraweak` as a positive continuous linear map. -/
@[simps]
def toUltraweakPosCLM : M â†’P[â„‚] Ïƒ(M, P) where
  toFun m := toUltraweak â„‚ P m
  map_add' := by simp
  map_smul' := by simp
  monotone' _ _ := id
  cont := by fun_prop

variable (M P)

/-- Linear combinations of ultraweakly continuous positive linear functionals. -/
private noncomputable def E : Submodule â„‚ (StrongDual â„‚ Ïƒ(M, P)) :=
  Submodule.span â„‚ (Set.range PositiveContinuousLinearMap.toContinuousLinearMap)

/-- The natural bilinear induced by the pairing of `M` with `E M P`. -/
@[simps!]
private noncomputable def fromEâ‚— : M â†’â‚—[â„‚] E M P â†’â‚—[â„‚] â„‚ :=
  letI e : E M P â†’â‚—[â„‚] Ïƒ(M, P) â†’â‚—[â„‚] â„‚ :=
    (ContinuousLinearMap.coeLM â„‚).compRight â„‚ (E M P).subtype
  (linearEquiv â„‚ M P).arrowCongr (.refl â„‚ _) e.flip

/-- `E` separates points of `M` because positive continuous linear maps
do as well. -/
private lemma fromEâ‚—_injective : Function.Injective (fromEâ‚— M P) := by
  intro x y h
  rw [â† toUltraweak_inj (ğ•œ := â„‚) (P := P)]
  apply ext_positiveCLM fun Ï† â†¦ ?_
  congrm($h âŸ¨Ï†.toContinuousLinearMap, Submodule.subset_span <| by simpâŸ©)

/-- The weak topology on `M` induced by pairing with linear combinations of
positive continuous linear maps. -/
private abbrev WeakE := WeakBilin (fromEâ‚— M P)

private instance : T2Space (WeakE M P) :=
  WeakBilin.isEmbedding (fromEâ‚—_injective M P) |>.t2Space

-- we're missing `WeakBilin` API
private noncomputable def weakEEquiv : WeakE M P â‰ƒâ‚—[â„‚] M := .refl â„‚ _

omit [StarOrderedRing M] [CompleteSpace P] in
/-- A filter is cauchy relative to the `WeakE M P` topology if and only if
mapping it through `Ï†` is cauchy for every `Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚`. -/
private lemma cauchy_weakE_iff_forall_posCLM {l : Filter (WeakE M P)} :
    Cauchy l â†” âˆ€ Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚,
      Cauchy (Filter.map (fun m â†¦ Ï† (toUltraweak â„‚ P (weakEEquiv M P m))) l) := by
  rw [WeakBilin.cauchy_iff (fromEâ‚— M P)]
  refine âŸ¨fun h Ï† â†¦ h âŸ¨Ï†.toContinuousLinearMap, Submodule.subset_span <| by simpâŸ©,
    fun h âŸ¨Ï†, hÏ†âŸ© â†¦ ?_âŸ©
  simp only [fromEâ‚—_apply_apply]
  have hl : l.NeBot := (h 0).1.of_map
  induction hÏ† using Submodule.span_induction with
  | mem Ï† hÏ† => obtain âŸ¨Ï†, hÏ†, rflâŸ© := hÏ†; exact h Ï†
  | zero => exact h 0
  | add Ï† Ïˆ hÏ† hÏˆ ihÏ† ihÏˆ =>
    simpa using (ihÏ†.prod ihÏˆ).mono (tendsto_map.prodMk tendsto_map) |>.map uniformContinuous_add
  | smul a Ï† hÏ† ihÏ† => simpa using ihÏ†.map <| uniformContinuous_const_smul a

private lemma tendsto_weakE_iff_forall_posCLM {Î± : Type*} [TopologicalSpace Î±]
    {l : Filter Î±} (x : WeakE M P) {f : Î± â†’ WeakE M P} :
    Tendsto f l (ğ“ x) â†” âˆ€ Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚,
      Tendsto (fun m â†¦ Ï† (toUltraweak â„‚ P (weakEEquiv M P (f m)))) l
        (ğ“ (Ï† (toUltraweak â„‚ P (weakEEquiv M P x)))) := by
  rw [WeakBilin.tendsto_iff_forall_eval_tendsto (fromEâ‚— M P) (fromEâ‚—_injective M P)]
  refine âŸ¨fun h Ï† â†¦ h âŸ¨Ï†.toContinuousLinearMap, Submodule.subset_span <| by simpâŸ©,
    fun h âŸ¨Ï†, hÏ†âŸ© â†¦ ?_âŸ©
  simp only [fromEâ‚—_apply_apply]
  induction hÏ† using Submodule.span_induction with
  | mem Ï† hÏ† => obtain âŸ¨Ï†, hÏ†, rflâŸ© := hÏ†; exact h Ï†
  | zero => exact h 0
  | add Ï† Ïˆ hÏ† hÏˆ ihÏ† ihÏˆ => simpa using ihÏ†.add ihÏˆ
  | smul a Ï† hÏ† ihÏ† => simpa using ihÏ†.const_smul a

-- ugh, `WeakBilin` has some nasty defeq abuse.
-- we should get this out of tactic mode as a proof.
private noncomputable def weakEUniformEquiv (r : â„) :
    (ofUltraweak â»Â¹' Metric.closedBall (0 : M) r : Set Ïƒ(M, P)) â‰ƒáµ¤
      (weakEEquiv M P â»Â¹' (Metric.closedBall (0 : M) r)) :=
  letI e : (ofUltraweak â»Â¹' Metric.closedBall (0 : M) r : Set Ïƒ(M, P)) â‰ƒ
      (weakEEquiv M P â»Â¹' (Metric.closedBall (0 : M) r)) :=
    { toFun := Subtype.map ((weakEEquiv M P).symm âˆ˜ ofUltraweak) fun _ â†¦ id
      invFun := Subtype.map (toUltraweak â„‚ P âˆ˜ weakEEquiv M P) (by simp)
      left_inv _ := by ext; simp
      right_inv _ := by ext; simp }
  haveI := isCompact_iff_compactSpace.mp <| isCompact_closedBall â„‚ P (0 : M) r
  Continuous.uniformOfEquivCompactToT2 e <| by
    rw [continuous_induced_rng, Function.comp_def]
    refine WeakBilin.continuous_of_continuous_eval _ fun âŸ¨Ï†, hÏ†âŸ© â†¦ ?_
    exact (map_continuous Ï†).comp continuous_subtype_val

private lemma isCompact_weakE_closedBall (r : â„) :
    IsCompact (weakEEquiv M P â»Â¹' Metric.closedBall (0 : M) r) := by
  have := Ultraweak.isCompact_closedBall â„‚ P (0 : M) r
  rw [isCompact_iff_compactSpace] at this âŠ¢
  exact weakEUniformEquiv M P r |>.toHomeomorph.compactSpace

private lemma uniformContinuousOn_toUltraweak_comp_weakEEquiv (r : â„) :
    UniformContinuousOn (toUltraweak â„‚ P âˆ˜ weakEEquiv M P)
      (weakEEquiv M P â»Â¹' Metric.closedBall (0 : M) r) := by
  rw [uniformContinuousOn_iff_restrict]
  exact uniformContinuous_subtype_val.comp (weakEUniformEquiv M P r).symm.uniformContinuous

private lemma mapsTo_weakEEquiv_symm_comp_ofUltraweak_preimage_closedBall (r : â„) :
    Set.MapsTo ((weakEEquiv M P).symm âˆ˜ ofUltraweak (ğ•œ := â„‚) (P := P))
      (ofUltraweak â»Â¹' Metric.closedBall (0 : M) r)
      (weakEEquiv M P â»Â¹' (Metric.closedBall (0 : M) r)) :=
  fun x hx â†¦ (weakEUniformEquiv M P r âŸ¨x, hxâŸ©).2

variable {M P}

/-- A bounded filter `l` in `Ïƒ(M, P)` is cauchy if and only if `map Ï† l` is cauchy in `â„‚`
for every positive continuous linear functional `Ï†`. -/
lemma cauchy_of_forall_posCLM_of_eventually {l : Filter Ïƒ(M, P)} {r : â„}
    (hlr : âˆ€á¶  x in l, â€–ofUltraweak xâ€– â‰¤ r) (hl : âˆ€ Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚, Cauchy (map Ï† l)) :
    Cauchy l := by
  have key : Cauchy (map ((weakEEquiv M P).symm âˆ˜ ofUltraweak) l) := by
    rw [cauchy_weakE_iff_forall_posCLM]
    simpa [Function.comp_def]
  replace hlr : l â‰¤ ğ“Ÿ (ofUltraweak â»Â¹' Metric.closedBall (0 : M) r) := by
    suffices {x | x âˆˆ ofUltraweak â»Â¹' Metric.closedBall (0 : M) r} âˆˆ l by
      simpa only [le_principal_iff]
    simpa
  have hlr' : map ((weakEEquiv M P).symm âˆ˜ ofUltraweak) l â‰¤
      ğ“Ÿ (weakEEquiv M P â»Â¹' (Metric.closedBall (0 : M) r)) :=
    map_mono hlr |>.trans <|
      mapsTo_weakEEquiv_symm_comp_ofUltraweak_preimage_closedBall M P r |>.tendsto
  simpa using key.map_of_le (uniformContinuousOn_toUltraweak_comp_weakEEquiv M P r) hlr'

/-- A bounded filter `l` in `Ïƒ(M, P)` is cauchy if and only if `map Ï† l` is cauchy in `â„‚`
for every positive continuous linear functional `Ï†`. -/
lemma cauchy_of_forall_posCLM_of_disjoint {l : Filter Ïƒ(M, P)}
    (hl_bdd : Disjoint l (cobounded Ïƒ(M, P))) (hl : âˆ€ Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚, Cauchy (map Ï† l)) :
    Cauchy l := by
  obtain âŸ¨s, hsl, hsâŸ© := exists_isBounded_of_disjoint hl_bdd
  obtain âŸ¨r, hrâŸ© := isBounded_image_ofUltraweak.mpr hs |>.subset_closedBall 0
  refine cauchy_of_forall_posCLM_of_eventually (r := r) ?_ hl
  filter_upwards [mem_of_superset hsl (by simpa using hr)] using by simp

variable {Î± : Type*} [TopologicalSpace Î±] {l : Filter Î±} {a : Î±} {s : Set Î±}
  {x : Ïƒ(M, P)} {f : Î± â†’ Ïƒ(M, P)}

-- this proof is totally gross
/-- If `f : Î± â†’ Ïƒ(M, P)` is eventually bounded along a filter `l`, and for every
positive continuous linear functional `Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚`, `Ï† âˆ˜ f` converges to `Ï† x`,
then `f` converges to `x`. -/
lemma tendsto_of_forall_posCLM_of_eventually {r : â„} (hfl : âˆ€á¶  x in l, â€–ofUltraweak (f x)â€– â‰¤ r)
    (hf : âˆ€ Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚, Tendsto (fun m â†¦ Ï† (f m)) l (ğ“ (Ï† x))) :
    Tendsto f l (ğ“ x) := by
  by_cases! h_bot : l = âŠ¥
  Â· simp [h_bot]
  replace hfl : Tendsto f l (ğ“Ÿ (ofUltraweak â»Â¹' Metric.closedBall (0 : M) r)) := by simpa
  have key : Tendsto (fun m : Î± â†¦ (weakEEquiv M P).symm (ofUltraweak (f m))) l
      (ğ“ ((weakEEquiv M P).symm (ofUltraweak x))) := by
    rw [tendsto_weakE_iff_forall_posCLM]
    simpa [Function.comp_def]
  have hfl' : Tendsto (fun m : Î± â†¦ (weakEEquiv M P).symm (ofUltraweak (f m))) l
      (ğ“Ÿ (weakEEquiv M P â»Â¹' (Metric.closedBall (0 : M) r))) :=
    map_mono hfl |>.trans <|
      mapsTo_weakEEquiv_symm_comp_ofUltraweak_preimage_closedBall M P r |>.tendsto
  have := (uniformContinuousOn_toUltraweak_comp_weakEEquiv M P r).continuousOn
  have hx : (weakEEquiv M P).symm (ofUltraweak x) âˆˆ
      weakEEquiv M P â»Â¹' (Metric.closedBall (0 : M) r) :=
    isCompact_weakE_closedBall M P r |>.isClosed.mem_of_tendsto key <| by
      simpa using hfl'
  have := this _ hx |>.tendsto
  have key2 : Tendsto (fun m : Î± â†¦ (weakEEquiv M P).symm (ofUltraweak (f m))) l
      (ğ“[weakEEquiv M P â»Â¹' (Metric.closedBall (0 : M) r)]
        ((weakEEquiv M P).symm (ofUltraweak x))) := by
    rw [tendsto_nhdsWithin_iff]
    refine âŸ¨key, by simpa using hfl'âŸ©
  simpa using this.comp key2

/-- If `f : Î± â†’ Ïƒ(M, P)` is eventually bounded along a filter `l`, and for every
positive continuous linear functional `Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚`, `Ï† âˆ˜ f` converges to `Ï† x`,
then `f` converges to `x`. -/
lemma tendsto_of_forall_posCLM_of_disjoint (hfl : Disjoint (map f l) (cobounded Ïƒ(M, P)))
    (hf : âˆ€ Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚, Tendsto (fun m â†¦ Ï† (f m)) l (ğ“ (Ï† x))) :
    Tendsto f l (ğ“ x) := by
  obtain âŸ¨s, hsl, hsâŸ© := exists_isBounded_of_disjoint hfl
  obtain âŸ¨r, hrâŸ© := isBounded_image_ofUltraweak.mpr hs |>.subset_closedBall 0
  refine tendsto_of_forall_posCLM_of_eventually (r := r) ?_ hf
  filter_upwards [mem_of_superset (mem_map.mp hsl) (preimage_mono <| by simpa using hr)]
  simp

/-- If `f : Î± â†’ Ïƒ(M, P)` is eventually bounded along `ğ“[s] a`, and for every
positive continuous linear functional `Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚`, `Ï† âˆ˜ f` is continuous at `a` within
`s`, then `f` is continuous at `a` within `s`. -/
lemma continuousWithinAt_of_forall_posCLM (hfl : Disjoint (map f (ğ“[s] a)) (cobounded Ïƒ(M, P)))
    (hf : âˆ€ Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚, ContinuousWithinAt (Ï† âˆ˜ f) s a) :
    ContinuousWithinAt f s a :=
  tendsto_of_forall_posCLM_of_disjoint hfl hf

/-- If `f : Î± â†’ Ïƒ(M, P)` is eventually bounded along `ğ“ a`, and for every
positive continuous linear functional `Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚`, `Ï† âˆ˜ f` is continuous at `a`,
then `f` is continuous at `a`. -/
lemma continuousAt_of_forall_posCLM (hfl : Disjoint (map f (ğ“ a)) (cobounded Ïƒ(M, P)))
    (hf : âˆ€ Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚, ContinuousAt (Ï† âˆ˜ f) a) :
    ContinuousAt f a :=
  tendsto_of_forall_posCLM_of_disjoint hfl hf

/-- If `f : Î± â†’ Ïƒ(M, P)` is bounded on a set `s`, and for every positive continuous linear
functional `Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚`, `Ï† âˆ˜ f` is continuous on `s`, then `f` is continuous on `s`.

Note that this theorem is weaker than applying `continuousWithinAt_of_forall_posCLM` at each point
of `s`. That theorem would work if `f` is only eventually bounded within each neighborhood
`ğ“[s] x` for `x âˆˆ s`. -/
lemma continuousOn_of_forall_posCLM (hfl : IsBounded (f '' s))
    (hf : âˆ€ Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚, ContinuousOn (Ï† âˆ˜ f) s) :
    ContinuousOn f s :=
  fun x hx â†¦ continuousWithinAt_of_forall_posCLM
    (hfl.disjoint_cobounded_of_mem <| image_mem_map self_mem_nhdsWithin) (hf Â· x hx)

/-- If `f : Î± â†’ Ïƒ(M, P)` is bounded function, and for every positive continuous linear
functional `Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚`, `Ï† âˆ˜ f` is continuous, then `f` is continuous.

Note that this theorem is weaker than applying `continuousAt_of_forall_posCLM` at each point.
That theorem would work if `f` is only eventually bounded within each neighborhood `ğ“ x`. -/
lemma continuous_of_forall_posCLM (hfl : IsBounded (Set.range f))
    (hf : âˆ€ Ï† : Ïƒ(M, P) â†’P[â„‚] â„‚, Continuous (Ï† âˆ˜ f)) :
    Continuous f := by
  simp_rw [â† continuousOn_univ] at hf âŠ¢
  exact continuousOn_of_forall_posCLM (hfl.subset <| by simp) hf

end Ultraweak

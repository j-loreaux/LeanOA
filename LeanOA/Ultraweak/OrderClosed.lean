import LeanOA.Ultraweak.Basic
import LeanOA.KreinSmulian
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Basic

open scoped NNReal

variable {M P : Type*}
  [CStarAlgebra M] [NormedAddCommGroup P] [NormedSpace ‚ÑÇ P] [Predual ‚ÑÇ M P]

open scoped Ultraweak

lemma isSelfAdjoint_ofUltraweak {x : œÉ(M, P)} :
    IsSelfAdjoint (ofUltraweak x) ‚Üî IsSelfAdjoint x := by
  simp [isSelfAdjoint_iff, ‚Üê Ultraweak.ofUltraweak_star]

alias ‚ü®IsSelfAdjoint.of_ofUltraweak, IsSelfAdjoint.ofUltraweak‚ü© := isSelfAdjoint_ofUltraweak

lemma isSelfAdjoint_toUltraweak {x : M} :
    IsSelfAdjoint (toUltraweak ‚ÑÇ P x) ‚Üî IsSelfAdjoint x := by
  simp [isSelfAdjoint_iff, ‚Üê Ultraweak.toUltraweak_star]

alias ‚ü®IsSelfAdjoint.of_toUltraweak, IsSelfAdjoint.toUltraweak‚ü© := isSelfAdjoint_toUltraweak

namespace Ultraweak

-- Should go in `Basic`
scoped instance : StarModule ‚ÑÇ œÉ(M, P) := inferInstanceAs (StarModule ‚ÑÇ M)
instance : T2Space œÉ(M, P) := (weakDualCLE ‚ÑÇ M P).symm.toHomeomorph.t2Space
instance [Nontrivial M] : Nontrivial œÉ(M, P) := linearEquiv ‚ÑÇ M P |>.nontrivial
instance [Subsingleton M] : Subsingleton œÉ(M, P) := linearEquiv ‚ÑÇ M P |>.subsingleton



open WeakDual


lemma ofUltraweak_preimage (s : Set M) :
    ofUltraweak ‚Åª¬π' s =
      weakDualCLE ‚ÑÇ M P ‚Åª¬π' (WeakDual.toStrongDual ‚Åª¬π' (Predual.equivDual.symm ‚Åª¬π' s)) := by
  ext; simp [weakDualCLE]

lemma ofUltraweak_preimage_ball (x : M) (r : ‚Ñù) :
    ofUltraweak ‚Åª¬π' (Metric.ball x r) =
      weakDualCLE ‚ÑÇ M P ‚Åª¬π' (WeakDual.toStrongDual ‚Åª¬π' (Metric.ball (Predual.equivDual x) r)) := by
  convert ofUltraweak_preimage _
  simp

lemma ofUltraweak_preimage_closedBall (x : M) (r : ‚Ñù) :
    ofUltraweak ‚Åª¬π' (Metric.closedBall x r) =
      weakDualCLE ‚ÑÇ M P ‚Åª¬π'
        (WeakDual.toStrongDual ‚Åª¬π'
          (Metric.closedBall (Predual.equivDual x) r)) := by
  convert ofUltraweak_preimage _
  simp

variable (P) in
lemma isCompact_closedBall (x : M) (r : ‚Ñù) :
    IsCompact (ofUltraweak ‚Åª¬π' (Metric.closedBall x r) : Set œÉ(M, P)) := by
  rw [ofUltraweak_preimage_closedBall]
  exact (weakDualCLE ‚ÑÇ M P).toHomeomorph.isCompact_preimage.mpr <|
    WeakDual.isCompact_closedBall ..

variable (P) in
lemma isClosed_closedBall (x : M) (r : ‚Ñù) :
    IsClosed (ofUltraweak ‚Åª¬π' (Metric.closedBall x r) : Set œÉ(M, P)) :=
  isCompact_closedBall P x r |>.isClosed

variable [CompleteSpace P]

/-- ugh... so much work just to transfer Krein-Smulian over to `Ultraweak`. -/
protected lemma krein_smulian_of_submodule (S : Submodule ‚Ñù‚â•0 œÉ(M, P))
    (hS : IsClosed ((S : Set œÉ(M, P)) ‚à© ofUltraweak ‚Åª¬π' (Metric.closedBall (0 : M) 1))) :
    IsClosed (S : Set œÉ(M, P)) := by
  have := (weakDualCLE ‚ÑÇ M P).preimage_symm_preimage (S : Set œÉ(M, P))
  rw [‚Üê this] at hS ‚ä¢
  rw [ofUltraweak_preimage_closedBall, ‚Üê Set.preimage_inter] at hS
  apply (weakDualCLE ‚ÑÇ M P).toHomeomorph.isClosed_preimage.mp at hS
  refine .preimage (map_continuous _) ?_
  simp only [map_zero] at hS
  exact krein_smulian_of_submodule
    (S.comap ((weakDualCLE ‚ÑÇ M P).symm.restrictScalars ‚Ñù‚â•0 |>.toLinearMap)) hS

open scoped ComplexStarModule Topology

open Filter Topology in
lemma tendsto_sqrt_one_add_sq_sub_self_atTop :
    Tendsto (fun x : ‚Ñù ‚Ü¶ ‚àö(1 + x ^ 2) - x) atTop (ùìù 0) := by
  -- This can be solved instantaneously with `compute_asymptotics`,
  -- but it isn't yet merged into Mathlib, so it's unavailable here.
  sorry

open Complex in
/-- An element in a non-unital star `‚ÑÇ`-algebra is normal if and only if its real and imaginary
parts commute. -/
lemma _root_.isStarNormal_iff_commute_realPart_imaginaryPart
    {A : Type*} [NonUnitalRing A] [StarRing A]
    [Module ‚ÑÇ A] [SMulCommClass ‚ÑÇ A A] [IsScalarTower ‚ÑÇ A A] [StarModule ‚ÑÇ A]
    {x : A} : IsStarNormal x ‚Üî Commute (realPart x : A) (imaginaryPart x : A) := by
  conv_lhs => rw [isStarNormal_iff, ‚Üê realPart_add_I_smul_imaginaryPart x]
  rw [commute_iff_eq]
  simp only [star_add, selfAdjoint.star_val_eq, star_smul, RCLike.star_def, Complex.conj_I,
    neg_smul, ‚Üê sub_eq_add_neg, mul_add, sub_mul, smul_mul_assoc, mul_smul_comm, smul_sub,
    smul_smul, Complex.I_mul_I, one_smul, sub_neg_eq_add, mul_sub, add_mul, smul_add]
  rw [sub_eq_add_neg, add_assoc, add_sub_assoc, add_left_cancel_iff, ‚Üê sub_add,
    ‚Üê add_assoc, add_right_cancel_iff, ‚Üê sub_eq_zero]
  noncomm_ring
  rw [add_comm, neg_smul, ‚Üê sub_eq_add_neg, sub_eq_zero]
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ congr(2 ‚Ä¢ I ‚Ä¢ $h)‚ü©
  have := congr(I ‚Ä¢ (2‚Åª¬π : ‚ÑÇ) ‚Ä¢ $h)
  rw [‚Üê smul_one_smul ‚ÑÇ (2 : ‚Ñ§) (I ‚Ä¢ (‚Ñë x * ‚Ñú x : A)), ‚Üê smul_one_smul ‚ÑÇ (2 : ‚Ñ§)] at this
  simpa

open Complex in
lemma _root_.IsSelfAdjoint.norm_add_I_smul_sq_of_commute {A : Type*}
    [NonUnitalCStarAlgebra A] {x y : A} (hx : IsSelfAdjoint x)
    (hy : IsSelfAdjoint y) (hxy : Commute x y) :
    ‚Äñx + I ‚Ä¢ y‚Äñ ^ 2 = ‚Äñx * x + y * y‚Äñ := by
  simp [sq, ‚Üê CStarRing.norm_star_mul_self, hx.star_eq, hy.star_eq,
    ‚Üê sub_eq_add_neg, mul_add, sub_mul, smul_mul_assoc, mul_smul_comm,
    hxy.eq, smul_sub, smul_smul, ‚Üê add_assoc]

open Complex in
lemma _root_.IsSelfAdjoint.norm_add_I_smul_le_of_commute {A : Type*}
    [NonUnitalCStarAlgebra A] {x y : A} (hx : IsSelfAdjoint x)
    (hy : IsSelfAdjoint y) (hxy : Commute x y) :
    ‚Äñx + I ‚Ä¢ y‚Äñ ‚â§ ‚àö(‚Äñx‚Äñ ^ 2 + ‚Äñy‚Äñ ^ 2) := by
  rw [Real.le_sqrt (by positivity) (by positivity), hx.norm_add_I_smul_sq_of_commute hy hxy,
    ‚Üê hx.norm_mul_self, ‚Üê hy.norm_mul_self]
  exact norm_add_le ..

open Complex in
-- this lemma is poorly named, and maybe we should remove it entirely.
-- or else we could parametermize the lemma above with inequalities on `‚Äñx‚Äñ` and `‚Äñy‚Äñ`.
lemma _root_.IsSelfAdjoint.norm_isSelfAdjoint_add_I_smul_algebraMap
    {M : Type*} [CStarAlgebra M] {x : M} (hx : IsSelfAdjoint x)
    (hx‚ÇÅ : ‚Äñx‚Äñ ‚â§ 1) (r : ‚Ñù) :
    ‚Äñx + I ‚Ä¢ algebraMap ‚Ñù M r‚Äñ ‚â§ ‚àö(1 + r ^ 2) := by
  nontriviality M
  apply hx.norm_add_I_smul_le_of_commute (.algebraMap M (.all r))
    (Algebra.commute_algebraMap_right r x) |>.trans
  simp only [norm_algebraMap', Real.norm_eq_abs, sq_abs]
  gcongr
  rwa [sq_le_one_iff‚ÇÄ (by positivity)]

-- this lemma seems a bit too technical? Maybe it's useful though.
lemma _root_.IsSelfAdjoint.max_norm_add_sub_algebraMap_ge
    {A : Type*} [NormedRing A] [StarRing A] [NormedAlgebra ‚Ñù A]
    [IsometricContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint] {x : A}
    (hx : IsSelfAdjoint x) (c : ‚Ñù) (hc : c ‚àà spectrum ‚Ñù x) (r : ‚Ñù) (hr : r ‚â• 0) :
    |c| + r ‚â§ max ‚Äñx + algebraMap ‚Ñù A r‚Äñ ‚Äñx - algebraMap ‚Ñù A r‚Äñ := by
  obtain (hc' | hc') := le_total 0 c
  ¬∑ apply le_max_of_le_left
    convert norm_apply_le_norm_cfc (fun y : ‚Ñù ‚Ü¶ y + r) x hc using 1
    ¬∑ simp [abs_of_nonneg (a := c + r) (by positivity), abs_of_nonneg hc']
    ¬∑ rw [cfc_add .., cfc_const .., cfc_id' ..]
  ¬∑ apply le_max_of_le_right
    convert norm_apply_le_norm_cfc (fun y : ‚Ñù ‚Ü¶ -y + r) x hc using 1
    ¬∑ rw [abs_of_nonpos hc', Real.norm_eq_abs, abs_of_nonneg]
      positivity [neg_nonneg.mpr hc']
    ¬∑ rw [cfc_add _ (- ¬∑) _, cfc_neg_id .., cfc_const .., norm_sub_rev]
      congr! 1
      abel

open Complex Filter in
/-- The selfadjoint elements are closed in the ultraweak topology. -/
lemma isClosed_setOf_isSelfAdjoint : IsClosed {x : œÉ(M, P) | IsSelfAdjoint x} := by
  nontriviality œÉ(M, P)
  have : Nontrivial M := linearEquiv ‚ÑÇ M P |>.symm.nontrivial
  /- By the Krein-Smulian theorem, it suffices to show that the set of
  selfadjoint elements in the closed unit ball is closed. -/
  apply Ultraweak.krein_smulian_of_submodule <|
    (selfAdjoint.submodule ‚Ñù œÉ(M, P)).restrictScalars ‚Ñù‚â•0
  /- take any filter within the closed unit ball of selfadjoint elements which is
  also contained within `ùìù x`. We must show `x` is in the closed unit ball of
  selfadjoint elements. This is the filter equivalent of "let x_Œ± be a net of
  selfadjoint elements of norm at most 1 converging to x. We show x is selfadjoint." -/
  rw [isClosed_iff_forall_filter]
  intro x l hl_neBot hl_sa hlx
  refine Set.mem_inter (?_ : IsSelfAdjoint x) ?mem_ball
  -- Since the closed unit ball is ultraweakly closed, `x` is in the closed unit ball.
  case mem_ball =>
    exact isClosed_iff_forall_filter.mp (isClosed_closedBall  P (0 : M) 1)
      x l hl_neBot (by aesop) hlx
  /- To show `x` is self-adjoint, we show every element `c` of the `‚Ñù`-spectrum of
  `‚Ñë (ofUltraweak x)` is zero. -/
  refine .of_ofUltraweak ?_
  rw [‚Üê imaginaryPart_eq_zero_iff, ‚Üê ZeroMemClass.coe_eq_zero]
  refine CFC.eq_zero_of_spectrum_subset_zero _ (R := ‚Ñù) (fun c hc ‚Ü¶ ?_) (‚Ñë (ofUltraweak x)).2
    -- missing `cfc_tac` lemma for the last argument on the previous line
  rw [Set.mem_singleton_iff]
  rw [‚Üê abs_eq_zero]
  refine le_antisymm ?_ (abs_nonneg c)
  -- To show `c ‚â§ 0` It suffices to show `Tendsto (fun n : ‚Ñï ‚Ü¶ ‚àö(1 + n ^ 2) - n) atTop (ùìù 0)`
  refine ge_of_tendsto' (tendsto_sqrt_one_add_sq_sub_self_atTop.comp tendsto_natCast_atTop_atTop)
    fun n ‚Ü¶ ?_
  /- By `IsSelfAdjoint.max_norm_add_sub_algebraMap_ge` it suffices to show that
  `‚Äñ‚Ñë x ¬± n‚Äñ ‚â§ ‚àö(1 + n ^ 2)`. -/
  grw [Function.comp_apply, le_sub_iff_add_le,
    IsSelfAdjoint.max_norm_add_sub_algebraMap_ge (‚Ñë (ofUltraweak x)).2 c hc n (by positivity)]
  trans max ‚ÄñofUltraweak x + I ‚Ä¢ algebraMap ‚Ñù M n‚Äñ ‚ÄñofUltraweak x - I ‚Ä¢ algebraMap ‚Ñù M n‚Äñ
  ¬∑ apply max_le_max
    ¬∑ convert imaginaryPart.norm_le (ofUltraweak x + I ‚Ä¢ algebraMap ‚Ñù M n) using 1
      simp [IsSelfAdjoint.coe_realPart]
    ¬∑ convert imaginaryPart.norm_le (ofUltraweak x - I ‚Ä¢ algebraMap ‚Ñù M n) using 1
      simp [IsSelfAdjoint.coe_realPart]
  /- `fun m : œÉ(M, P) ‚Ü¶ m + I ‚Ä¢ n` stays within the closed ball of radius `‚àö(1 + n ^ 2)`
  and converges to `x + I ‚Ä¢ n` along the filter `l`. -/
  have h‚ÇÉ (n : ‚Ñù) : Tendsto (fun m : œÉ(M, P) ‚Ü¶ m + I ‚Ä¢ algebraMap ‚Ñù œÉ(M, P) n) l
      (ùìü (ofUltraweak ‚Åª¬π' Metric.closedBall (0 : M) ‚àö(1 + n ^ 2)) ‚äì
        ùìù (x + I ‚Ä¢ algebraMap ‚Ñù œÉ(M, P) n)) := by
    refine tendsto_inf.mpr ‚ü®.mono_left ?_ hl_sa, .add_const _ hlx‚ü©
    simpa using fun x hx hx_norm ‚Ü¶
      hx.ofUltraweak.norm_isSelfAdjoint_add_I_smul_algebraMap hx_norm n
  apply max_le
  ¬∑ have := isClosed_iff_forall_filter.mp <| isClosed_closedBall P (0 : M) (‚àö(1 + n ^ 2))
    simpa using this _ _ inferInstance (tendsto_inf.mp (h‚ÇÉ n) |>.1) (tendsto_inf.mp (h‚ÇÉ n) |>.2)
  ¬∑ have := isClosed_iff_forall_filter.mp <| isClosed_closedBall P (0 : M) (‚àö(1 + n ^ 2))
    simpa [sub_eq_add_neg] using this _ _ inferInstance
      (by simpa only [even_two, Even.neg_pow] using (tendsto_inf.mp (h‚ÇÉ (-n)) |>.1))
      (tendsto_inf.mp (h‚ÇÉ (-n)) |>.2)

variable [PartialOrder M] [StarOrderedRing M]

--def Nonneg.nnrealSubmodule {Œ± : Type*} [AddCommMonoid Œ±] [Preorder Œ±] [Module ‚Ñù Œ±]
    --[AddLeftMono Œ±] [IsStrictOrderedModule ‚Ñù Œ±] :
    --Submodule ‚Ñù‚â•0 Œ± :=
  --{ carrier := {x : Œ± | 0 ‚â§ x}
    --zero_mem' := le_rfl
    --add_mem' := add_nonneg
    --smul_mem' r _ h := smul_nonneg r.2 h }
--def Nonneg.nnrealSubmodule : Submodule ‚Ñù‚â•0 œÉ(M, P) :=
  --{ carrier := {x : œÉ(M, P) | 0 ‚â§ x}
    --zero_mem' := le_rfl
    --add_mem' := add_nonneg
    --smul_mem' r _ h := smul_nonneg r.2 h }

--def Nonneg.convexCone :

open Pointwise in
/-- The nonnegative elements are closed in the ultraweak topology. -/
lemma isClosed_nonneg : IsClosed {x : œÉ(M, P) | 0 ‚â§ x} := by
  nontriviality œÉ(M, P)
  have : Nontrivial M := linearEquiv ‚ÑÇ M P |>.symm.nontrivial
  set N := {x : œÉ(M, P) | 0 ‚â§ x}
  set S : Set œÉ(M, P) := ofUltraweak ‚Åª¬π' (Metric.closedBall (0 : M) 1)
  set M_sa := {x : œÉ(M, P) | IsSelfAdjoint x}
  have h‚ÇÅ : N ‚à© S ‚äÜ (1 : œÉ(M, P)) +·µ• M_sa ‚à© S := by
    intro x ‚ü®hx_nonneg, hx_norm‚ü©
    refine ‚ü®x - 1, ‚ü®by aesop, ?_‚ü©, by simp‚ü©
    have := SpectrumRestricts.nnreal_iff_nnnorm (t := 1) hx_nonneg.isSelfAdjoint.ofUltraweak
      (by simpa [S] using hx_norm) |>.mp <| SpectrumRestricts.nnreal_of_nonneg hx_nonneg
    simpa [norm_sub_rev, S] using NNReal.coe_le_coe.mpr this
  have h‚ÇÇ : (1 : œÉ(M, P)) +·µ• M_sa ‚à© S ‚äÜ N := by
    rintro - ‚ü®x, ‚ü®hx_sa, hx‚ü©, rfl‚ü©
    simp only [vadd_eq_add, Set.mem_setOf_eq, N]
    change 0 ‚â§ 1 + ofUltraweak x
    rw [nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts,
      quasispectrumRestricts_iff_spectrumRestricts]
    refine ‚ü®by aesop, ?_‚ü©
    rw [SpectrumRestricts.nnreal_iff_nnnorm (t := 1 + 1)]
    ¬∑ simpa using norm_sub_le .. |>.trans <| by simpa [S] using hx
    ¬∑ aesop
    ¬∑ exact norm_add_le .. |>.trans <| by simpa [S] using hx
  have h‚ÇÉ : IsClosed ((1 : œÉ(M, P)) +·µ• M_sa ‚à© S) :=
    isClosed_setOf_isSelfAdjoint.inter (isClosed_closedBall P (0 : M) 1) |>.vadd _
  let N_submodule : Submodule ‚Ñù‚â•0 œÉ(M, P) :=
    { carrier := N
      zero_mem' := by simp [N]
      add_mem' := add_nonneg
      smul_mem' r x hx := smul_nonneg r.2 hx }
  refine Ultraweak.krein_smulian_of_submodule N_submodule ?_
  change IsClosed (N ‚à© S)
  convert h‚ÇÉ.inter (show IsClosed S from isClosed_closedBall P (0 : M) 1) using 1
  exact subset_antisymm (Set.subset_inter h‚ÇÅ Set.inter_subset_right) (by gcongr)

instance : OrderClosedTopology œÉ(M, P) where
  isClosed_le' := isClosed_le_of_isClosed_nonneg isClosed_nonneg

end Ultraweak

open Ultraweak



--- none of what follows is necessary, but it should all go in Mathlib.

-- should be in Mathlib, quite trivial
lemma IsClosed.setOf_isSelfAdjoint {R : Type*} [Star R]
    [TopologicalSpace R] [ContinuousStar R] [T2Space R] :
    IsClosed {x : R | IsSelfAdjoint x} :=
  isClosed_eq continuous_star continuous_id

/-- A linear map with closed kernel of finite index is continuous. -/
lemma LinearMap.continuous_of_isClosed_ker_of_finiteDimensional
    {ùïú E F : Type*} [NontriviallyNormedField ùïú]
    [AddCommGroup E] [Module ùïú E] [TopologicalSpace E]
    [IsTopologicalAddGroup E] [ContinuousSMul ùïú E]
    [AddCommGroup F] [Module ùïú F] [TopologicalSpace F]
    [IsTopologicalAddGroup F] [ContinuousSMul ùïú F]
    [CompleteSpace ùïú]
    (f : E ‚Üí‚Çó[ùïú] F) (hf : IsClosed (f.ker : Set E))
    (hf_findim : FiniteDimensional ùïú (E ‚ß∏ f.ker)) :
    Continuous f :=
  have h : Continuous (Quotient.mk _ : E ‚Üí E ‚ß∏ f.ker) := { isOpen_preimage := fun _ a ‚Ü¶ a }
  f.ker.liftQ f le_rfl |>.continuous_of_finiteDimensional.comp h

@[simp]
theorem ker_imaginaryPart {E : Type*} [AddCommGroup E]
    [Module ‚ÑÇ E] [StarAddMonoid E] [StarModule ‚ÑÇ E] :
    imaginaryPart.ker = selfAdjoint.submodule ‚Ñù E := by
  ext x
  simp [selfAdjoint.submodule, selfAdjoint.mem_iff, imaginaryPart, Subtype.ext_iff]
  grind

instance ContinuousSMul.smulMemClass (S M Œ± : Type*) [Monoid M] [MulAction M Œ±]
    [TopologicalSpace M] [TopologicalSpace Œ±] [ContinuousSMul M Œ±] [SetLike S Œ±]
    [SMulMemClass S M Œ±] (s : S) : ContinuousSMul M s where
  continuous_smul := by fun_prop

instance ContinuousSMul.complexToReal {E : Type*} [AddCommGroup E] [Module ‚ÑÇ E] [TopologicalSpace E]
    [ContinuousSMul ‚ÑÇ E] : ContinuousSMul ‚Ñù E :=
  IsScalarTower.continuousSMul ‚ÑÇ

instance selfAdjoint.instContinuousSMul {R A : Type*} [Star R] [TrivialStar R]
    [AddGroup A] [StarAddMonoid A] [SMul R A] [StarModule R A] [TopologicalSpace R]
    [TopologicalSpace A] [ContinuousSMul R A] : ContinuousSMul R (selfAdjoint A) where
  continuous_smul := by
    rw [continuous_induced_rng]
    fun_prop

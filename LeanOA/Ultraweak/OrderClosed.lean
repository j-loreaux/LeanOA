import LeanOA.Ultraweak.Basic
import LeanOA.KreinSmulian
import LeanOA.Mathlib.LinearAlgebra.Complex.Module
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Basic

open scoped NNReal Ultraweak ComplexStarModule Topology
open Filter Complex

section KreinSmulian

variable {ğ•œ M P : Type*} [RCLike ğ•œ] [NormedAddCommGroup M] [NormedSpace ğ•œ M]
variable [NormedAddCommGroup P] [NormedSpace ğ•œ P] [Predual ğ•œ M P] [CompleteSpace P]
variable [Module â„â‰¥0 M] [IsScalarTower â„â‰¥0 ğ•œ M]

/-- The Krein-Smulian theorem transferred from `WeakDual â„‚ P` to `Ïƒ(M, P)`. This could
generalize trivially from `â„‚` to `RCLike ğ•œ`. -/
protected lemma Ultraweak.krein_smulian_of_submodule (S : Submodule â„â‰¥0 (Ïƒ(M, P)_ğ•œ))
    (hS : IsClosed ((S : Set (Ïƒ(M, P)_ğ•œ)) âˆ© ofUltraweak â»Â¹' (Metric.closedBall (0 : M) 1))) :
    IsClosed (S : Set (Ïƒ(M, P)_ğ•œ)) := by
  have := (weakDualCLE ğ•œ M P).preimage_symm_preimage (S : Set (Ïƒ(M, P)_ğ•œ))
  rw [â† this] at hS âŠ¢
  rw [ofUltraweak_preimage_closedBall, â† Set.preimage_inter] at hS
  apply (weakDualCLE ğ•œ M P).toHomeomorph.isClosed_preimage.mp at hS
  refine .preimage (map_continuous _) ?_
  simp only [map_zero] at hS
  exact krein_smulian_of_submodule
    (S.comap ((weakDualCLE ğ•œ M P).symm.restrictScalars â„â‰¥0 |>.toLinearMap)) hS

end KreinSmulian

variable {M P : Type*} [CStarAlgebra M]
variable [NormedAddCommGroup P] [NormedSpace â„‚ P] [Predual â„‚ M P] [CompleteSpace P]

lemma tendsto_sqrt_one_add_sq_sub_self_atTop :
    Tendsto (fun x : â„ â†¦ âˆš(1 + x ^ 2) - x) atTop (ğ“ 0) := by
  -- This can be solved instantaneously with `compute_asymptotics`,
  -- but it isn't yet merged into Mathlib, so it's unavailable here.
  sorry

lemma IsSelfAdjoint.norm_add_I_smul_sq_of_commute {A : Type*}
    [NonUnitalCStarAlgebra A] {x y : A} (hx : IsSelfAdjoint x)
    (hy : IsSelfAdjoint y) (hxy : Commute x y) :
    â€–x + I â€¢ yâ€– ^ 2 = â€–x * x + y * yâ€– := by
  simp [sq, â† CStarRing.norm_star_mul_self, hx.star_eq, hy.star_eq,
    â† sub_eq_add_neg, mul_add, sub_mul, smul_mul_assoc, mul_smul_comm,
    hxy.eq, smul_sub, smul_smul, â† add_assoc]

lemma IsSelfAdjoint.norm_add_I_smul_le_of_commute {A : Type*}
    [NonUnitalCStarAlgebra A] {x y : A} (hx : IsSelfAdjoint x)
    (hy : IsSelfAdjoint y) (hxy : Commute x y) :
    â€–x + I â€¢ yâ€– â‰¤ âˆš(â€–xâ€– ^ 2 + â€–yâ€– ^ 2) := by
  rw [Real.le_sqrt (by positivity) (by positivity), hx.norm_add_I_smul_sq_of_commute hy hxy,
    â† hx.norm_mul_self, â† hy.norm_mul_self]
  exact norm_add_le ..

-- this lemma is poorly named, and maybe we should remove it entirely.
-- or else we could parametermize the lemma above with inequalities on `â€–xâ€–` and `â€–yâ€–`.
lemma IsSelfAdjoint.norm_isSelfAdjoint_add_I_smul_algebraMap
    {M : Type*} [CStarAlgebra M] {x : M} (hx : IsSelfAdjoint x)
    (hxâ‚ : â€–xâ€– â‰¤ 1) (r : â„) :
    â€–x + I â€¢ algebraMap â„ M râ€– â‰¤ âˆš(1 + r ^ 2) := by
  nontriviality M
  apply hx.norm_add_I_smul_le_of_commute (.algebraMap M (.all r))
    (Algebra.commute_algebraMap_right r x) |>.trans
  simp only [norm_algebraMap', Real.norm_eq_abs, sq_abs]
  gcongr
  rwa [sq_le_one_iffâ‚€ (by positivity)]

-- this lemma seems a bit too technical? Maybe it's useful though.
lemma IsSelfAdjoint.max_norm_add_sub_algebraMap_ge
    {A : Type*} [NormedRing A] [StarRing A] [NormedAlgebra â„ A]
    [IsometricContinuousFunctionalCalculus â„ A IsSelfAdjoint] {x : A}
    (hx : IsSelfAdjoint x) (c : â„) (hc : c âˆˆ spectrum â„ x) (r : â„) (hr : r â‰¥ 0) :
    |c| + r â‰¤ max â€–x + algebraMap â„ A râ€– â€–x - algebraMap â„ A râ€– := by
  obtain (hc' | hc') := le_total 0 c
  Â· apply le_max_of_le_left
    convert norm_apply_le_norm_cfc (fun y : â„ â†¦ y + r) x hc using 1
    Â· simp [abs_of_nonneg (a := c + r) (by positivity), abs_of_nonneg hc']
    Â· rw [cfc_add .., cfc_const .., cfc_id' ..]
  Â· apply le_max_of_le_right
    convert norm_apply_le_norm_cfc (fun y : â„ â†¦ -y + r) x hc using 1
    Â· rw [abs_of_nonpos hc', Real.norm_eq_abs, abs_of_nonneg]
      positivity [neg_nonneg.mpr hc']
    Â· rw [cfc_add _ (- Â·) _, cfc_neg_id .., cfc_const .., norm_sub_rev]
      congr! 1
      abel

namespace Ultraweak

/-- The selfadjoint elements are closed in the ultraweak topology. -/
lemma isClosed_setOf_isSelfAdjoint : IsClosed {x : Ïƒ(M, P) | IsSelfAdjoint x} := by
  nontriviality Ïƒ(M, P)
  have : Nontrivial M := linearEquiv â„‚ M P |>.symm.nontrivial
  /- By the Krein-Smulian theorem, it suffices to show that the set of
  selfadjoint elements in the closed unit ball is closed. -/
  apply Ultraweak.krein_smulian_of_submodule <|
    (selfAdjoint.submodule â„ Ïƒ(M, P)).restrictScalars â„â‰¥0
  /- take any filter within the closed unit ball of selfadjoint elements which is
  also contained within `ğ“ x`. We must show `x` is in the closed unit ball of
  selfadjoint elements. This is the filter equivalent of "let x_Î± be a net of
  selfadjoint elements of norm at most 1 converging to x. We show x is selfadjoint." -/
  rw [isClosed_iff_forall_filter]
  intro x l hl_neBot hl_sa hlx
  refine Set.mem_inter (?_ : IsSelfAdjoint x) ?mem_ball
  -- Since the closed unit ball is ultraweakly closed, `x` is in the closed unit ball.
  case mem_ball =>
    exact isClosed_iff_forall_filter.mp (isClosed_closedBall â„‚ P (0 : M) 1)
      x l hl_neBot (by aesop) hlx
  /- To show `x` is self-adjoint, we show every element `c` of the `â„`-spectrum of
  `â„‘ (ofUltraweak x)` is zero. -/
  refine .of_ofUltraweak ?_
  rw [â† imaginaryPart_eq_zero_iff, â† ZeroMemClass.coe_eq_zero]
  refine CFC.eq_zero_of_spectrum_subset_zero _ (R := â„) (fun c hc â†¦ ?_) (â„‘ (ofUltraweak x)).2
    -- missing `cfc_tac` lemma for the last argument on the previous line
  rw [Set.mem_singleton_iff]
  rw [â† abs_eq_zero]
  refine le_antisymm ?_ (abs_nonneg c)
  -- To show `c â‰¤ 0` It suffices to show `Tendsto (fun n : â„• â†¦ âˆš(1 + n ^ 2) - n) atTop (ğ“ 0)`
  refine ge_of_tendsto' (tendsto_sqrt_one_add_sq_sub_self_atTop.comp tendsto_natCast_atTop_atTop)
    fun n â†¦ ?_
  /- By `IsSelfAdjoint.max_norm_add_sub_algebraMap_ge` it suffices to show that
  `â€–â„‘ x Â± nâ€– â‰¤ âˆš(1 + n ^ 2)`. -/
  grw [Function.comp_apply, le_sub_iff_add_le,
    IsSelfAdjoint.max_norm_add_sub_algebraMap_ge (â„‘ (ofUltraweak x)).2 c hc n (by positivity)]
  trans max â€–ofUltraweak x + I â€¢ algebraMap â„ M nâ€– â€–ofUltraweak x - I â€¢ algebraMap â„ M nâ€–
  Â· apply max_le_max
    Â· convert imaginaryPart.norm_le (ofUltraweak x + I â€¢ algebraMap â„ M n) using 1
      simp [IsSelfAdjoint.coe_realPart]
    Â· convert imaginaryPart.norm_le (ofUltraweak x - I â€¢ algebraMap â„ M n) using 1
      simp [IsSelfAdjoint.coe_realPart]
  /- `fun m : Ïƒ(M, P) â†¦ m + I â€¢ n` stays within the closed ball of radius `âˆš(1 + n ^ 2)`
  and converges to `x + I â€¢ n` along the filter `l`. -/
  have hâ‚ƒ (n : â„) : Tendsto (fun m : Ïƒ(M, P) â†¦ m + I â€¢ algebraMap â„ Ïƒ(M, P) n) l
      (ğ“Ÿ (ofUltraweak â»Â¹' Metric.closedBall (0 : M) âˆš(1 + n ^ 2)) âŠ“
        ğ“ (x + I â€¢ algebraMap â„ Ïƒ(M, P) n)) := by
    refine tendsto_inf.mpr âŸ¨.mono_left ?_ hl_sa, .add_const _ hlxâŸ©
    simpa using fun x hx hx_norm â†¦
      hx.ofUltraweak.norm_isSelfAdjoint_add_I_smul_algebraMap hx_norm n
  apply max_le
  Â· have := isClosed_iff_forall_filter.mp <| isClosed_closedBall â„‚ P (0 : M) (âˆš(1 + n ^ 2))
    simpa using this _ _ inferInstance (tendsto_inf.mp (hâ‚ƒ n) |>.1) (tendsto_inf.mp (hâ‚ƒ n) |>.2)
  Â· have := isClosed_iff_forall_filter.mp <| isClosed_closedBall â„‚ P (0 : M) (âˆš(1 + n ^ 2))
    simpa [sub_eq_add_neg] using this _ _ inferInstance
      (by simpa only [even_two, Even.neg_pow] using (tendsto_inf.mp (hâ‚ƒ (-n)) |>.1))
      (tendsto_inf.mp (hâ‚ƒ (-n)) |>.2)

variable [PartialOrder M] [StarOrderedRing M]

open Pointwise in
/-- The nonnegative elements are closed in the ultraweak topology. -/
lemma isClosed_nonneg : IsClosed {x : Ïƒ(M, P) | 0 â‰¤ x} := by
  nontriviality Ïƒ(M, P)
  have : Nontrivial M := linearEquiv â„‚ M P |>.symm.nontrivial
  set N := {x : Ïƒ(M, P) | 0 â‰¤ x}
  set S : Set Ïƒ(M, P) := ofUltraweak â»Â¹' (Metric.closedBall (0 : M) 1)
  set M_sa := {x : Ïƒ(M, P) | IsSelfAdjoint x}
  have hâ‚ : N âˆ© S âŠ† (1 : Ïƒ(M, P)) +áµ¥ M_sa âˆ© S := by
    intro x âŸ¨hx_nonneg, hx_normâŸ©
    refine âŸ¨x - 1, âŸ¨by aesop, ?_âŸ©, by simpâŸ©
    have := SpectrumRestricts.nnreal_iff_nnnorm (t := 1) hx_nonneg.isSelfAdjoint.ofUltraweak
      (by simpa [S] using hx_norm) |>.mp <| SpectrumRestricts.nnreal_of_nonneg hx_nonneg
    simpa [norm_sub_rev, S] using NNReal.coe_le_coe.mpr this
  have hâ‚‚ : (1 : Ïƒ(M, P)) +áµ¥ M_sa âˆ© S âŠ† N := by
    rintro - âŸ¨x, âŸ¨hx_sa, hxâŸ©, rflâŸ©
    simp only [vadd_eq_add, Set.mem_setOf_eq, N]
    change 0 â‰¤ 1 + ofUltraweak x
    rw [nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts,
      quasispectrumRestricts_iff_spectrumRestricts]
    refine âŸ¨by aesop, ?_âŸ©
    rw [SpectrumRestricts.nnreal_iff_nnnorm (t := 1 + 1)]
    Â· simpa using norm_sub_le .. |>.trans <| by simpa [S] using hx
    Â· aesop
    Â· exact norm_add_le .. |>.trans <| by simpa [S] using hx
  have hâ‚ƒ : IsClosed ((1 : Ïƒ(M, P)) +áµ¥ M_sa âˆ© S) :=
    isClosed_setOf_isSelfAdjoint.inter (isClosed_closedBall â„‚ P (0 : M) 1) |>.vadd _
  refine Ultraweak.krein_smulian_of_submodule (Nonneg.nnrealSubmodule Ïƒ(M, P)) ?_
  convert hâ‚ƒ.inter (isClosed_closedBall â„‚ P (0 : M) 1) using 1
  exact subset_antisymm (Set.subset_inter hâ‚ Set.inter_subset_right) (by gcongr; exact hâ‚‚)

instance : OrderClosedTopology Ïƒ(M, P) where
  isClosed_le' := isClosed_le_of_isClosed_nonneg isClosed_nonneg

end Ultraweak

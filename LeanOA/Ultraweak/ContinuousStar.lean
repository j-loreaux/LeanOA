import LeanOA.Ultraweak.OrderClosed
import LeanOA.Mathlib.Analysis.RCLike.Extend
import Mathlib.Analysis.Complex.Basic

open scoped NNReal Topology

namespace WeakDual

variable {ğ•œ E : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace E]

set_option backward.isDefEq.respectTransparency false in
/-- A linear map from the weak dual of a Banach space to itself is continuous if
it is continuous on the closed unit ball. -/
lemma continuous_of_continuousOn (f : WeakDual ğ•œ E â†’â‚—[ğ•œ] WeakDual ğ•œ E)
    (hf : ContinuousOn f (toStrongDual â»Â¹' Metric.closedBall 0 1)) : Continuous f := by
  refine continuous_of_continuous_eval fun x â†¦ ?_
  let xf : Module.Dual ğ•œ (WeakDual ğ•œ E) :=
    WeakBilin.eval _ x |>.toLinearMap |>.comp f
  refine xf.continuous_of_isClosed_ker <| krein_smulian_of_submodule (xf.ker.restrictScalars â„â‰¥0) ?_
  rw [Set.inter_comm]
  exact eval_continuous x |>.comp_continuousOn hf |>.preimage_isClosed_of_isClosed
    (isClosed_closedBall 0 1) isClosed_singleton

set_option backward.isDefEq.respectTransparency false in
/-- A *real* linear man from the weak dual of a Banach space to itself is continuous
if it is continuous on the closed unit ball. -/
lemma continuous_of_continuousOn_of_real (f : WeakDual ğ•œ E â†’â‚—[â„] WeakDual ğ•œ E)
    (hf : ContinuousOn f (toStrongDual â»Â¹' Metric.closedBall 0 1)) : Continuous f := by
  refine WeakBilin.continuous_of_continuous_eval_re _ fun x â†¦ ?_
  let xf : Module.Dual â„ (WeakDual ğ•œ E) :=
    Module.Dual.extendRCLikeâ‚—.symm.toLinearMap
      (WeakBilin.eval (topDualPairing ğ•œ E) x |>.toLinearMap) |>.comp f
  refine xf.continuous_of_isClosed_ker <| krein_smulian_of_submodule (xf.ker.restrictScalars â„â‰¥0) ?_
  rw [Set.inter_comm]
  refine RCLike.continuous_re.comp_continuousOn (eval_continuous x |>.comp_continuousOn hf)
    |>.preimage_isClosed_of_isClosed (isClosed_closedBall 0 1) isClosed_singleton

end WeakDual

namespace Ultraweak

section generic

variable {ğ•œ M P : Type*} [RCLike ğ•œ]
  [NormedAddCommGroup M] [NormedSpace ğ•œ M]
  [NormedAddCommGroup P] [NormedSpace ğ•œ P]
  [Predual ğ•œ M P] [CompleteSpace P]

lemma continuous_of_continuousOn (f : Ïƒ(M, P)_ğ•œ â†’â‚—[ğ•œ] Ïƒ(M, P)_ğ•œ)
    (hf : ContinuousOn f (ofUltraweak â»Â¹' Metric.closedBall 0 1)) :
    Continuous f := by
  -- transport the problem to the weak dual of `P`, then apply the result there.
  suffices Continuous ((weakDualCLE ğ•œ M P).toLinearEquiv.toLinearMap.comp <|
      f.comp (weakDualCLE ğ•œ M P).symm.toLinearEquiv.toLinearMap) from
    (weakDualCLE ğ•œ M P).symm.toHomeomorph.comp_continuous_iff'.mp <|
      (weakDualCLE ğ•œ M P).toHomeomorph.comp_continuous_iff.mp this
  refine WeakDual.continuous_of_continuousOn _ ?_
  refine map_continuous (weakDualCLE ğ•œ M P) |>.comp_continuousOn ?_
  refine hf.comp (map_continuous (weakDualCLE ğ•œ M P).symm).continuousOn fun x hx â†¦ ?_
  rw [ofUltraweak_preimage_closedBall]
  simpa using hx

variable [Module â„ M] [IsScalarTower â„ ğ•œ M]
set_option backward.isDefEq.respectTransparency false in
lemma continuous_of_continuousOn_of_real (f : Ïƒ(M, P)_ğ•œ â†’â‚—[â„] Ïƒ(M, P)_ğ•œ)
    (hf : ContinuousOn f (ofUltraweak â»Â¹' Metric.closedBall 0 1)) :
    Continuous f := by
  -- transport the problem to the weak dual of `P`, then apply the result there.
  suffices Continuous (((weakDualCLE ğ•œ M P).toLinearEquiv.toLinearMap.restrictScalars â„).comp <|
      f.comp <| (weakDualCLE ğ•œ M P).symm.toLinearEquiv.toLinearMap.restrictScalars â„) from
    (weakDualCLE ğ•œ M P).symm.toHomeomorph.comp_continuous_iff'.mp <|
      (weakDualCLE ğ•œ M P).toHomeomorph.comp_continuous_iff.mp this
  refine WeakDual.continuous_of_continuousOn_of_real _ ?_
  refine map_continuous (weakDualCLE ğ•œ M P) |>.comp_continuousOn ?_
  refine hf.comp (map_continuous (weakDualCLE ğ•œ M P).symm).continuousOn fun x hx â†¦ ?_
  rw [ofUltraweak_preimage_closedBall]
  simpa using hx

end generic

variable {M P : Type*} [CStarAlgebra M]
  [NormedAddCommGroup P] [NormedSpace â„‚ P]
  [Predual â„‚ M P] [CompleteSpace P]

set_option backward.isDefEq.respectTransparency false in
open Filter Complex in
open scoped Pointwise ComplexStarModule in
instance : ContinuousStar Ïƒ(M, P) where
  continuous_star := by
    -- It suffices to show that the `â„`-linear `realPart` map is continuous
    suffices Continuous ((selfAdjoint.submodule â„ Ïƒ(M, P)).subtype.comp (realPart)) by
      convert (this.const_smul (2 : â„)).sub continuous_id using 2 with x
      -- `change` is necessary to see through the defeq between `selfAdjoint.submodule â„ Ïƒ(M, P)
      -- and `selfAdjoint Ïƒ(M, P)`. To do this correctly, we would need to set up a linear
      -- equivalence between them.
      change star x = (2 : â„) â€¢ realPart x - x
      simp [realPart_apply_coe, smul_smul]
    /- Since this map is `â„`-linear, it suffices to show that it is
    continuous on the closed unit ball, for which it suffices to check that
    it is continuous within the closed unit ball at each `x`. -/
    refine Ultraweak.continuous_of_continuousOn_of_real _ ?_
    simp only [LinearMap.coe_comp, Submodule.coe_subtype, Function.comp_def]
    intro x hx
    /- Because `realPart` is contractive, it stays within the closed unit ball
    along the `ğ“[closedUnitBall] x` filter. -/
    have hâ‚ : âˆ€á¶  m in (ğ“[ofUltraweak â»Â¹' Metric.closedBall 0 1] x),
        (realPart m : Ïƒ(M, P)) âˆˆ ofUltraweak â»Â¹' Metric.closedBall 0 1 := by
      filter_upwards [self_mem_nhdsWithin] with m hm
      simp only [Set.mem_preimage, Metric.mem_closedBall, dist_zero_right] at hm
      grw [â† hm]
      simpa using realPart.norm_le (ofUltraweak m)
    /- Since the closed unit ball intersected with the selfadjoint elements
    is compact, it suffices to show that every selfadjoint `y` satisfying
    `mapClusterPt y (ğ“[ofUltraweak â»Â¹' Metric.closedBall 0 1] x) realPart`
    is precisely `realPart x`. -/
    refine Ultraweak.isCompact_closedBall â„‚ P (0 : M) 1
      |>.inter_right Ultraweak.isClosed_setOf_isSelfAdjoint
      |>.tendsto_nhds_of_unique_mapClusterPt (by simpa using hâ‚)
      fun y âŸ¨_, (hy : IsSelfAdjoint y)âŸ© hxy â†¦ ?_
    /- Extract an ultrafilter `l` smaller than
    `ğ“[ofUltraweak â»Â¹' Metric.closedBall 0 1] x` such that `realPart` converges
    to `y` along `l`. Then `m â†¦ m - realPart m` converges to `x - y` along
    `l`. -/
    obtain âŸ¨l, hl, hlyâŸ© := mapClusterPt_iff_ultrafilter.mp hxy
    have hâ‚‚ : Tendsto (fun m : Ïƒ(M, P) â†¦ m - realPart m) l (ğ“ (x - y)) :=
      .sub (hl.trans nhdsWithin_le_nhds) hly
    /- Since `m â†¦ m - realPart m` is always skewadjoint, and the skewadjoint
    element of `Ïƒ(M, P)` are closed, `x - y = I â€¢ z` for some selfadjoint `z`.
    Since `y` is also selfadjoint, we must have `y = realPart x`. -/
    obtain âŸ¨z, (hz : IsSelfAdjoint z), (hxyz : I â€¢ z = _)âŸ© :=
      Ultraweak.isClosed_setOf_isSelfAdjoint.smulâ‚€ I |>.mem_of_tendsto hâ‚‚ <|
        .of_forall fun m â†¦ by
          nth_rewrite 1 [â† realPart_add_I_smul_imaginaryPart m]
          simp
    simpa only [eq_comm (a := y), map_sub, AddSubgroupClass.coe_sub, hy.coe_realPart,
      realPart_I_smul, hz.imaginaryPart, neg_zero, ZeroMemClass.coe_zero, sub_eq_zero]
      using congr((â„œ $hxyz : Ïƒ(M, P))).symm

end Ultraweak
